;奉仕精神の可否
@ISABLUP7
;恋慕、親愛、服従、隷属はMAXLV10
IF ABL:奉仕精神 >= 10
	RETURN 0
ELSEIF ABL:奉仕精神 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:服従 == 0 && TALENT:隷属 == 0
	RETURN 0
ENDIF

CALL DECIDE_ABLUP7

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF


;奉仕精神
@ABLUP7
DRAWLINE
;恋慕、親愛、服従、隷属はMAXLV10
IF ABL:奉仕精神 >= 10
	PRINTW 既にMAXです
	RETURN 0
ELSEIF ABL:奉仕精神 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:服従 == 0 && TALENT:隷属 == 0
	PRINTW 既にMAXです
	RETURN 0
ENDIF

CALL DECIDE_ABLUP7

;ＬＶ４から５に上げるときは異常経験以上必要（素質：[恋慕][親愛]なら無視できる）
IF ABL:奉仕精神 == 4 && TALENT:恋慕 == 0 && TALENT:親愛 == 0
	PRINTFORML %EXPNAME:50%あり、かつ
;ＬＶ８から９に上げるときは異常経験5以上必要（素質：[恋慕][親愛]なら無視できる）
ELSEIF ABL:奉仕精神 == 8 && TALENT:恋慕 == 0 && TALENT:親愛 == 0
	PRINTFORML %EXPNAME:50%5以上、かつ
ENDIF

;従順が必要
PRINTFORML %ABLNAME:0%{ABL:奉仕精神 + 1}LV以上、かつ

PRINTFORM [0] - %PALAMNAME:7%の珠×{A}
SIF E > 0
	PRINTFORM 、%EXPNAME:2%{E}以上、%EXPNAME:20%{E}以上
PRINT ……

IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
	SIF I & 4
		PRINT 能力不足 
ENDIF
PRINTL 

IF B > 0
	;恭順の珠で上げる
	SIF JUEL:恭順 < B
		J |= 1
	;この場合奉仕快楽経験が必要なことあり
	SIF EXP:奉仕快楽経験 < D
		J |= 2

	PRINTFORM [1] - %PALAMNAME:5%の珠×{B}
	SIF D > 0
		PRINTFORM 、%EXPNAME:21%{D}以上
	PRINT ……
	
	IF J == 0
		PRINT ＯＫ
	ELSE
		SIF J & 1
			PRINT 珠不足 
		SIF J & 2
			PRINT 経験不足 
		SIF J & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
ELSE
	J = 256
ENDIF

IF C > 0
	;習得の珠で上げる
	SIF JUEL:習得 < C
		K |= 1
	;この場合絶頂経験が１以上必要
	SIF EXP:絶頂経験 < 1
		K |= 2

	PRINTFORM [2] - %PALAMNAME:8%の珠×{C}、%EXPNAME:2%1以上……
	
	IF K == 0
		PRINT ＯＫ
	ELSE
		SIF K & 1
			PRINT 珠不足 
		SIF K & 2
			PRINT 経験不足 
		SIF K & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
ELSE
	K = 256
ENDIF

PRINTL [100] - やめる

$INPUT_LOOP
INPUT
IF (RESULT < 0 || RESULT > 2) && RESULT != 100
	GOTO INPUT_LOOP
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません再入力してください
	GOTO INPUT_LOOP
ELSEIF J == 256 && RESULT == 1
	GOTO INPUT_LOOP
ELSEIF J != 0 && RESULT == 1
	PRINTL 条件を満たしていません再入力してください
	GOTO INPUT_LOOP
ELSEIF K == 256 && RESULT == 2
	GOTO INPUT_LOOP
ELSEIF K != 0 && RESULT == 2
	PRINTL 条件を満たしていません再入力してください
	GOTO INPUT_LOOP
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:奉仕精神 += 1

IF RESULT == 0
	JUEL:屈服 -= A
ELSEIF RESULT == 1
	JUEL:恭順 -= B
ELSEIF RESULT == 2
	JUEL:習得 -= C
ENDIF

PRINTFORMW %ABLNAME:7%のレベルが{ABL:奉仕精神}になりました

;奉仕精神LvUP判定
@DECIDE_ABLUP7
;条件別にＯＫかダメかを記録する
I = 0
J = 0
K = 0
;能力アップ可能なら1にする
Z = 0

IF ABL:奉仕精神 == 0
	A = 100
	B = 20
	C = 100
	D = 5
	E = 5
ELSEIF ABL:奉仕精神 == 1
	A = 1200
	B = 100
	C = 0
	D = 5
	E = 10
ELSEIF ABL:奉仕精神 == 2
	A = 5000
	B = 600
	C = 0
	D = 20
	E = 20
ELSEIF ABL:奉仕精神 == 3
	A = 10000
	B = 2000
	C = 0
	D = 50
	E = 20
ELSEIF ABL:奉仕精神 == 4
	A = 30000
	B = 8000
	C = 0
	D = 100
	E = 30
ELSEIF ABL:奉仕精神 == 5
	A = 35000
	B = 8500
	C = 0
	D = 150
	E = 40
ELSEIF ABL:奉仕精神 == 6
	A = 40000
	B = 10000
	C = 0
	D = 200
	E = 100
ELSEIF ABL:奉仕精神 == 7
	A = 45000
	B = 15000
	C = 0
	D = 250
	E = 150
ELSEIF ABL:奉仕精神 == 8
	A = 50000
	B = 20000
	C = 0
	D = 300
	E = 200
ELSEIF ABL:奉仕精神 == 9
	A = 60000
	B = 30000
	C = 0
	D = 400
	E = 300
ENDIF

;恋慕
SIF TALENT:恋慕
	TIMES B , 0.80
;親愛
SIF TALENT:親愛
	TIMES B , 0.60
;服従
SIF TALENT:服従
	TIMES A , 0.70
;隷属
SIF TALENT:隷属
	TIMES A , 0.40
;一線越えない
IF TALENT:一線越えない && (TALENT:恋慕 == 0 && TALENT:淫乱 == 0 && TALENT:服従 == 0 && TALENT:親愛 == 0 && TALENT:娼婦 == 0 && TALENT:隷属 == 0)
	TIMES A , 2.00
	TIMES B , 2.00
	TIMES C , 2.00
	TIMES D , 2.00
	TIMES E , 2.00
ENDIF

;従順が奉仕精神＋１レベルでないといけない
IF ABL:従順 < ABL:奉仕精神+1
	;従順が不足
	I |= 4
	J |= 4
	K |= 4
ENDIF

;ＬＶ４から５に上げるときは異常経験以上必要（素質：[恋慕][親愛]なら無視できる）
IF ABL:奉仕精神 == 4 && TALENT:恋慕 == 0 && TALENT:親愛 == 0
	IF EXP:異常経験 == 0
		;異常経験が不足
		I |= 2
		J |= 2
		K |= 2
	ENDIF
;ＬＶ８から９に上げるときは異常経験5以上必要（素質：[恋慕][親愛]なら無視できる）
ELSEIF ABL:奉仕精神 == 8 && TALENT:恋慕 == 0 && TALENT:親愛 == 0
	IF EXP:異常経験 < 5
		;異常経験が不足
		I |= 2
		J |= 2
		K |= 2
	ENDIF
ENDIF

;屈服の珠で上げる
SIF JUEL:屈服 < A
	I |= 1
;絶頂経験が必要な場合
SIF EXP:絶頂経験 < E
	I |= 2
;精液経験が必要な場合
SIF EXP:精液経験 < E
	I |= 2

IF I == 0
	Z = 1
ENDIF

IF B > 0
	;恭順の珠で上げる
	SIF JUEL:恭順 < B
		J |= 1
	;この場合奉仕快楽経験が必要なことあり
	SIF EXP:奉仕快楽経験 < D
		J |= 2
	
	IF J == 0
		Z = 1
	ENDIF
ELSE
	J = 256
ENDIF

IF C > 0
	;習得の珠で上げる
	SIF JUEL:習得 < C
		K |= 1
	;この場合絶頂経験が１以上必要
	SIF EXP:絶頂経験 < 1
		K |= 2

	IF K == 0
		Z = 1
	ENDIF
ELSE
	K = 256
ENDIF
RETURN 1


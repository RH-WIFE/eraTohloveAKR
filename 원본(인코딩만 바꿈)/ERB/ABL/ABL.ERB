@SHOP_ABLUP(ARG)
DRAWLINE
FOR LOCAL, 0, CHARANUM
	;労役中のキャラは除外
	SIF CFLAG:LOCAL:12 > 0
		CONTINUE
	;成長前の子供は除外
	SIF CFLAG:LOCAL:26
		CONTINUE
	;育児部屋に移動しているキャラは除外
	C = LOCAL
	CALL CHECK_CHILD_CARE
	SIF R == 1 && RESULT == 1
		CONTINUE
	PRINTFORM [{LOCAL,2}] %CALLNAME:LOCAL,18,LEFT%
	LOCAL:2 = 0
	TARGET = LOCAL
	FOR LOCAL:1, 0, 22
		X = LOCAL:1
		CALLFORM ISABLUP{X}
		SIF RESULT
			LOCAL:2 = 1
	NEXT
	X = 99
	CALL ISABLUP99
	SIF RESULT
		LOCAL:2 = 1
	IF LOCAL:2
		PRINTL ＜レベルアップ可能です＞
	ELSE
		PRINTL 
	ENDIF
NEXT
PRINTL [999] 戻る
$INPUT_LOOP
INPUT
IF RESULT == 999
	TARGET = ARG
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= CHARANUM || CFLAG:RESULT:12 > 0 || CFLAG:RESULT:26
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
C = RESULT
CALL CHECK_CHILD_CARE
IF R == 1 && RESULT == 1
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
TARGET = C
$SHOW_LOOP
CALL SHOW_JUEL
CALL SHOW_ABLUP_SELECT
$INPUT_LOOP2
INPUT
IF RESULT == 100
ELSE
	SIF ABLNAME:RESULT == "" && RESULT != 99
		GOTO INPUT_LOOP2
	CLEARLINE 1
	CALLFORM ABLUP{RESULT}
	GOTO SHOW_LOOP
ENDIF
RESTART

@SHOW_JUEL
DRAWLINE
CALL NEW_PRINT_EXP(TARGET)
CALL SHOW_INFO_JUEL(TARGET)
DRAWLINE

@SHOW_ABLUP_SELECT
REPEAT 23
	X = COUNT
	CALLFORM ISABLUP{COUNT}
	;[攻め]のときＶ感覚、Ａ感覚は上げられない
	SIF TALENT:攻め && (X == 4 || X == 5)
		CONTINUE
	;ヴァギナがないときＶ感覚は上げられない
	SIF !VAGINA(TARGET) && X == 4
		CONTINUE
	;男性のときレズっ気、レズ中毒は上げられない
	SIF MALE(TARGET) && (X == 11 || X == 17)
		CONTINUE
	;女性のときホモっ気、ゲイ中毒は上げられない
	SIF !MALE(TARGET) && (X == 20 || X == 21)
		CONTINUE
	;レズっ気がないならレズ中毒は非表示
	SIF !ABL:レズっ気 && X == 17
		CONTINUE
	;ホモっ気がないならゲイ中毒は非表示
	SIF !ABL:ホモっ気 && X == 21
		CONTINUE
	;自慰経験がないなら自慰中毒は非表示
	SIF !EXP:自慰経験 && X == 15
		CONTINUE
	;精液経験がないなら精液中毒は非表示
	SIF !EXP:精液経験 && X == 16
		CONTINUE
	;性交経験がないなら性交中毒は非表示
	SIF !EXP:性交経験 && X == 18
		CONTINUE
	;噴乳経験がないなら噴乳中毒は非表示
	SIF !EXP:噴乳経験 && X == 19
		CONTINUE
	;スカ経験がないならスカ中毒は非表示
	SIF !EXP:スカ経験 && X == 22
		CONTINUE
	PRINTFORM [{COUNT,2}] %ABLNAME:X,12,LEFT%：LV{ABL:X,2}
	IF TALENT:恋慕 || TALENT:親愛
		IF COUNT == 22
			IF TALENT:親愛
				BAR ABL:X ,5, 5
				PRINT      
			ELSE
				BAR ABL:X ,3, 3
				PRINT         
			ENDIF
		ELSE
			BAR ABL:X, 10, 10
		ENDIF
	ELSEIF TALENT:淫乱 || TALENT:娼婦
		IF COUNT == 1 || COUNT == 3 || COUNT == 4 || COUNT == 5 || COUNT == 6 || COUNT == 8 || COUNT == 11 || COUNT == 15 || COUNT == 16 || COUNT == 17 || COUNT == 18 || COUNT == 19 || COUNT == 20 || COUNT == 21
			BAR ABL:X, 10, 10
		ELSEIF COUNT == 22
			BAR ABL:X, 3, 3
			PRINT         
		ELSE
			BAR ABL:X, 5, 5
			PRINT      
		ENDIF
	ELSEIF TALENT:服従 || TALENT:隷属
		IF COUNT == 0 || COUNT == 2 || COUNT == 7 || COUNT == 9 || COUNT == 10 || COUNT == 12 || COUNT == 13 || COUNT == 14
			BAR ABL:X, 10, 10
		ELSE
			BAR ABL:X, 5, 5
			PRINT      
		ENDIF
	ELSE
		IF COUNT == 22
			BAR ABL:X, 3, 3
			PRINT         
		ELSE
			BAR ABL:X, 5, 5
			PRINT      
		ENDIF
	ENDIF
	SIF RESULT == 1
		PRINT  ◇レベルアップ可能です◇
	PRINTL 
REND

CALL ISABLUP99
PRINTFORM [99] %MARKNAME:3,12,LEFT%：LV{MARK:反発刻印,2}
BAR MARK:反発刻印, 3, 3
PRINT         
SIF RESULT == 1
	PRINT  ◇刻印消去可能です◇
PRINTL 

PRINTL [100] 能力値アップの終了

@USERABLUP
IF RESULT == 100
	CALL JUJUN_UP_CHECK
	CALL YOKUBO_UP_CHECK
	CALL RYOURI_UP_CHECK
	CALL SKINSHIP_UP_CHECK
	BEGIN TURNEND
	RETURN 1
ENDIF

RETURN 0

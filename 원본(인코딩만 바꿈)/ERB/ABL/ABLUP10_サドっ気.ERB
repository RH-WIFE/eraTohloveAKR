;サドっ気の可否
@ISABLUP10
;恋慕、親愛、服従、隷属はMAXLV10
IF ABL:サドっ気 >= 10
	RETURN 0
ELSEIF ABL:サドっ気 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:服従 == 0 && TALENT:隷属 == 0
	RETURN 0
ENDIF

CALL DECIDE_ABLUP10

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF


;サドっ気
@ABLUP10
DRAWLINE
;恋慕、親愛、服従、隷属はMAXLV10
IF ABL:サドっ気 >= 10
	PRINTW 既にMAXです
	RETURN 0
ELSEIF ABL:サドっ気 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:服従 == 0 && TALENT:隷属 == 0
	PRINTW 既にMAXです
	RETURN 0
ENDIF

CALL DECIDE_ABLUP10

IF I & 8
	PRINTW 上昇不可
	RETURN 0
ENDIF

;服従陥落
IF TALENT:服従 || TALENT:隷属
	;技巧がサドっ気＋１レベルでないといけない
	PRINTFORML %ABLNAME:2%{ABL:サドっ気+1}LV以上、かつ
ELSE
	;欲望がサドっ気＋１レベルでないといけない
	PRINTFORML %ABLNAME:1%{ABL:サドっ気+1}LV以上、かつ
ENDIF

;ＬＶ３から４、４から５に上げるときは異常経験必要（素質：[サド]なら無視できる）
IF ABL:サドっ気 == 3 && TALENT:サド == 0
	PRINTFORML %EXPNAME:50%あり、かつ
;ＬＶ４から５に上げるときは異常経験3以上必要（素質：[サド]なら無視できる）
ELSEIF ABL:サドっ気 == 4 && TALENT:サド == 0
	PRINTFORML %EXPNAME:50%3以上、かつ
;ＬＶ８から９に上げるときは異常経験5以上必要（素質：[サド]なら無視できる）
ELSEIF ABL:サドっ気 == 8 && TALENT:サド == 0
	PRINTFORML %EXPNAME:50%5以上、かつ
ENDIF


PRINTFORM [0] - %PALAMNAME:6%の珠×{A}、%EXPNAME:32%{B}以上、%EXPNAME:2%1以上……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
	SIF I & 4
		PRINT 能力不足 
ENDIF
PRINTL 

PRINTL [100] - やめる


INPUT
IF RESULT != 0 && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:サドっ気 += 1

IF RESULT == 0
	JUEL:欲情 -= A
ENDIF

PRINTFORMW %ABLNAME:10%のレベルが{ABL:サドっ気}になりました

;サドっ気LVUP判定
@DECIDE_ABLUP10
;条件別にＯＫかダメかを記録する
I = 0
J = 0
;能力アップ可能なら1にする
Z = 0

IF ABL:サドっ気 == 0
	A = 100
	B = 5
ELSEIF ABL:サドっ気 == 1
	A = 500
	B = 10
ELSEIF ABL:サドっ気 == 2
	A = 1500
	B = 20
ELSEIF ABL:サドっ気 == 3
	A = 3000
	B = 50
ELSEIF ABL:サドっ気 == 4
	A = 5000
	B = 80
ELSEIF ABL:サドっ気 == 5
	A = 8000
	B = 100
ELSEIF ABL:サドっ気 == 6
	A = 12000
	B = 150
ELSEIF ABL:サドっ気 == 7
	A = 15000
	B = 200
ELSEIF ABL:サドっ気 == 8
	A = 25000
	B = 250
ELSEIF ABL:サドっ気 == 9
	A = 30000
	B = 350
ENDIF

;一線越えない
IF TALENT:一線越えない && (TALENT:恋慕 == 0 && TALENT:淫乱 == 0 && TALENT:服従 == 0 && TALENT:親愛 == 0 && TALENT:娼婦 == 0 && TALENT:隷属 == 0)
	TIMES A , 2.00
	TIMES B , 2.00
ENDIF
;サド
IF TALENT:サド
	TIMES A , 0.50
	TIMES B , 0.50
ENDIF

;服従陥落
IF TALENT:服従 || TALENT:隷属
	;技巧がサドっ気＋１レベルでないといけない
	IF ABL:技巧 < ABL:サドっ気 + 1
		;技巧が不足
		I |= 4
	ENDIF
ELSE
	;欲望がサドっ気＋１レベルでないといけない
	IF ABL:欲望 < ABL:サドっ気+1
		;欲望が不足
		I |= 4
	ENDIF
ENDIF

;ＬＶ３から４、４から５に上げるときは異常経験必要（素質：[サド]なら無視できる）
IF ABL:サドっ気 == 3 && TALENT:サド == 0
	IF EXP:異常経験 == 0
		;異常経験が不足
		I |= 2
	ENDIF
;ＬＶ４から５に上げるときは異常経験3以上必要（素質：[サド]なら無視できる）
ELSEIF ABL:サドっ気 == 4 && TALENT:サド == 0
	IF EXP:異常経験 < 3
		;異常経験が不足
		I |= 2
	ENDIF
;ＬＶ８から９に上げるときは異常経験5以上必要（素質：[サド]なら無視できる）
ELSEIF ABL:サドっ気 == 8 && TALENT:サド == 0
	IF EXP:異常経験 < 5
		;異常経験が不足
		I |= 2
	ENDIF
ENDIF

;欲情の珠で上げる
SIF JUEL:欲情 < A
	I |= 1
;嗜虐快楽経験が必要な場合
SIF EXP:嗜虐快楽経験 < B
	I |= 2

SIF I == 0
	Z = 1

;調教者は自動上昇のみ
SIF CFLAG:1 == 3
	I |= 8

RETURN 1


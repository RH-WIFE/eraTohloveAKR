;=============================================================================
;体力・気力の最大値上昇処理
;=============================================================================
;調教終了後それぞれ2/5の確率で体力・気力の最大値が上昇。異常経験の値で上昇する数値が増加します
;ただし増加する値はそれぞれ＋４０が限度かつ、性別(及び処女性)によって
;体力、気力それぞれにある値を超えるとそれ以上は成長しない限界値というものが設定されています。
;また、体力・気力を減らしすぎた場合は上昇しないよう条件を変えました。
@EVENT_GROWTH
A = MAXBASE:体力-BASE:体力
B = MAXBASE:気力-BASE:気力
C = RAND:4
D = 1
G = EXP:異常経験
SIF G > 10
	G = 10
H = G*2

;調教対象の性別、処女性によって成長限界が変化(オトコは限界値がかなり高く設定されている)
IF TALENT:オトコ
	I = 9000
	J = 5000
ELSE
	I = 5000
	IF TALENT:処女 && EXP:Ｖ経験 == 0 && ABL:Ｖ感覚 == 0
		J = 4000
	ELSEIF TALENT:処女 && EXP:Ｖ経験 == 0
		J = 3500
	ELSE
		J = 3000
	ENDIF
ENDIF

;体力が大きい場合は気力が、気力が大きい場合は体力が成長する確率が上昇
IF C == 1 && MAXBASE:体力 > MAXBASE:気力 && RAND:3 == 0
	C = 2
ELSEIF C == 2 && MAXBASE:気力 > MAXBASE:体力 && RAND:3 == 0
	C = 1
ENDIF

IF C <= 1 && A != 0 && BASE:体力 >= 300 && MAXBASE:体力 < I
	SIF A > 0 && A <= 500
		D = 250
	SIF A > 500 && A <= 1000
		D = 300
	SIF A > 1000 && A <= 1500
		D = 350
	SIF A > 1500 && A <= 2000
		D = 400
	SIF A > 2000 && A <= 2500
		D = 450
	SIF A > 2500
		D = 500
	SIF BASE:体力 < 500
		D = 750
	IF MAXBASE:体力 < 1300
		TIMES D , 0.40
	ELSEIF MAXBASE:体力 < 1500
		TIMES D , 0.60
	ELSEIF MAXBASE:体力 < 2000
		TIMES D , 0.80
	ENDIF
	E = (A/D)
	SIF E > 20
		E = 20
	SIF !TALENT:恋慕 && !TALENT:親愛 && !TALENT:隷属
		E /= 2
	IF (E+H) > 0
		MAXBASE:体力 += E+H
		PRINTFORML 激しい調教によって%CALLNAME%は我慢することの快感を知った。
		PRINTFORMW %CALLNAME%の体力の最大値が{E+H}上がりました。
	ENDIF
ELSEIF C <= 1 && A != 0 && BASE:体力 >= 300 && MAXBASE:体力 >= I
	C = 0
ENDIF

IF (C == 0 || C == 2) && B != 0 && BASE:気力 >= 100 && MAXBASE:気力 < J
	SIF B > 0 && B <= 300
		D = 200
	SIF B > 300 && B <= 500
		D = 250
		SIF B > 500 && B <= 1000
			D = 300
	SIF B > 1000 && B <= 1500
		D = 350
	SIF B > 1500 && B <= 2000
		D = 400
	SIF B > 2000 && B <= 2500
		D = 450
	SIF B > 2500
		D = 500
	SIF BASE:気力 < 500
		D = 750
	IF MAXBASE:気力 < 800
		TIMES D , 0.40
	ELSEIF MAXBASE:気力 < 1000
		TIMES D , 0.60
	ELSEIF MAXBASE:気力 < 1200
		TIMES D , 0.80
	ENDIF
	F = (B/D)
	SIF F > 20
		F = 20
	SIF !TALENT:恋慕 && !TALENT:親愛 && !TALENT:隷属
		F /= 2
	IF (F+H) > 0
		MAXBASE:気力 += F+H
		PRINTFORML 激しい調教によって%CALLNAME%の意志は強くなった。
		PRINTFORMW %CALLNAME%の気力の最大値が{F+H}上がりました。
	ENDIF
ENDIF


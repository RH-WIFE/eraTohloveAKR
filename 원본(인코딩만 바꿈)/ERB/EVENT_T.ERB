;===========================================================
;EVENT_T.ERB、EVENT_TRAIN、調教時及び調教前後のイベント
;===========================================================
@EVENTTRAIN
#PRI

;-------------------------------------------------
;ゲージの初期値と限界数確保
;-------------------------------------------------
;射精の初期値は0
BASE:射精 = 0
BASE:PLAYER:射精 = 0
SIF ASSI >= 0
	BASE:ASSI:射精 = 0
;射精限界数の確保
REPEAT CHARANUM
	IF COUNT == MASTER
		CFLAG:COUNT:11 = 10
	ELSEIF COUNT == ASSI
		CFLAG:COUNT:11 = 10
	ELSEIF COUNT == TARGET
		CFLAG:COUNT:11 = 10
	ENDIF
	SIF TALENT:COUNT:絶倫
		CFLAG:COUNT:11 += 10
	SIF TALENT:COUNT:馬並み
		CFLAG:COUNT:11 += 3
REND
;射精フラグ、処女喪失フラグなどをリセット
REPEAT 200
	TFLAG:COUNT = 0
REND

;母乳
BASE:母乳 = 0
BASE:PLAYER:母乳 = 0
SIF ASSI >= 0
	BASE:ASSI:母乳 = 0

;尿意
BASE:尿意 = 10
BASE:PLAYER:尿意 = 10
SIF ASSI >= 0
	BASE:ASSI:尿意 = 10

;便意
BASE:便意 = 10
BASE:PLAYER:便意 = 10
SIF ASSI >= 0
	BASE:ASSI:便意 = 10

;吐き気
BASE:吐き気 = 10
BASE:PLAYER:吐き気 = 10
SIF ASSI >= 0
	BASE:ASSI:吐き気 = 10


;膣内にヴァギナ汚れを付加
SIF VAGINA(TARGET)
	STAIN:膣内 |= 1
SIF VAGINA(MASTER)
	STAIN:MASTER:膣内 |= 1
IF ASSI >= 0
	SIF VAGINA(ASSI)
		STAIN:ASSI:膣内 |= 1
ENDIF

;コンドーム自動装着設定のリセット
FLAG:98 = 0

;コンドーム射精フラグをリセット
CFLAG:107 = 0

;調教者は誰か
IF ASSIPLAY == 0
	PLAYER = MASTER
ELSE
	PLAYER = ASSI
	ASSI = MASTER
ENDIF

SIF TEQUIP:下着（下） == 5
	TEQUIP:貞操帯 = 1

;残り時間をセット
TFLAG:74 = 30

@EVENTTURNEND
#PRI
;売却や助手化が可能になったか
SIF TARGET < 0
	RETURN 0
SIF ABL:従順 + ABL:欲望 < 6
	RETURN 0
SIF ABL:従順 < 4 && ((TALENT:反抗的 && TALENT:服従 == 0 && TALENT:隷属 == 0) || TALENT:気丈)
	RETURN 0
SIF ABL:欲望 < 4 && (TALENT:自制心 || TALENT:抑圧 || TALENT:抵抗)
	RETURN 0

IF ABL:技巧 >= 3 || ABL:奉仕精神 >= 3 || ABL:マゾっ気 >= 3 || ABL:従順 >= 5 || ABL:欲望 >= 5 || ABL:Ｃ感覚+ABL:Ｖ感覚+ABL:Ａ感覚+ABL:Ｂ感覚 >= 13
	IF CFLAG:1 <= 0
		PRINTFORMW %NAME:TARGET%が売却可能になりました
		CFLAG:1 = 1
	ENDIF
ELSE
	RETURN 0
ENDIF

IF (ABL:従順+ABL:欲望+ABL:技巧+ABL:Ｃ感覚+ABL:レズっ気+ABL:ホモっ気 >= 12) || (ABL:従順 >=5 && ABL:欲望 >= 4)
	IF CFLAG:1 <= 1
		PRINTFORMW %NAME:TARGET%が助手可能になりました
		CFLAG:1 = 2
	ENDIF
ENDIF

@EVENTTURNEND
IF TARGET >= 0
	;ストレス値の加算
	CFLAG:TARGET:10 += 1
	SIF MARK:反発刻印 > 2
		CFLAG:TARGET:10 += 1
	
	;ストレス値の減少（白山が助手か主人時のみ）
	LOCAL = SINGI(TARGET)
	CFLAG:TARGET:10 -= LOCAL
	PRINTL
	SIF LOCAL > 0
		PRINTFORML 白山吉光の神技が発動しました
	
	;ストレス値100以上で崩壊を得る
	SIF CFLAG:TARGET:10 >= 100 && TALENT:崩壊 == 0
		CALL COLLAPSE_MIND_TRAIN
ENDIF

IF ASSI >= 0
	IF TALENT:ASSI:妖狐 && ITEM:媚薬 == 0
		PRINTW [妖狐]の効果で媚薬を手に入れました
		ITEM:媚薬 = 1
	ENDIF
ENDIF

;消耗品の自動補充
CALL ITEM_AUTO_SUPPLY

REPEAT 200
	SIF ITEM:COUNT < 0
		ITEM:COUNT = 0
REND

RETURN 0

@ASSI_CHANGE
;[狐]かつ技巧LV5、Ｃ感覚LV5で処女かバックバージン
IF TALENT:ASSI:狐 && (ABL:ASSI:技巧 >= 5 && ABL:ASSI:Ｃ感覚 >= 5 && ((TALENT:ASSI:処女 && VAGINA(ASSI)) || (TALENT:ASSI:バックバージン && !VAGINA(ASSI))))
	DRAWLINE
	PRINTFORML %NAME:ASSI%は[妖狐]となった
	DRAWLINE
	WAIT
	TALENT:ASSI:狐 = 0
	TALENT:ASSI:妖狐 = 1
ENDIF

$INPUT_LOOP
;[不思議な根]かつＣ感覚LV5、精液中毒LV3以上
IF TALENT:ASSI:半陰半陽 == 0 &&  TALENT:ASSI:ふたなり == 0 && TALENT:ASSI:不思議な根 && (ABL:ASSI:Ｃ感覚 >= 5 && ABL:ASSI:精液中毒 >= 3)
	DRAWLINE
	PRINTFORML ……どうやら[不思議な根]の効果が現れそうだ
	PRINTFORM %CALLNAME:ASSI%を[
	IF MALE(ASSI)
		PRINT 半陰半陽
	ELSE
		PRINT ふたなり
	ENDIF
	PRINTL ]にしますか？
	PRINTL [0] - する
	PRINTL [1] - しない
	INPUT
	IF RESULT == 0
		C = ASSI
		;CALL FUTANARI
	ELSEIF RESULT == 1
		PRINTFORML %NAME:ASSI%は[不思議な根]を失った
		TALENT:ASSI:不思議な根 = 0
	ELSE
		GOTO INPUT_LOOP_2
	ENDIF
	DRAWLINE
	WAIT
ENDIF

$INPUT_LOOP_2
;[具現]かつ欲望LV5かレズっ気orホモっ気LV4以上
;オトコは半陰半陽、そうでないときはふたなり化
IF TALENT:ASSI:半陰半陽 == 0 &&  TALENT:ASSI:ふたなり == 0 && TALENT:ASSI:具現 && (ABL:ASSI:欲望 >= 5 || ABL:ASSI:レズっ気 >= 4 || ABL:ASSI:ホモっ気 >= 4)
	DRAWLINE
	PRINTFORML ……どうやら[具現]の効果が現れそうだ
	PRINTFORM %CALLNAME:ASSI%を[
	IF MALE(ASSI)
		PRINT 半陰半陽
	ELSE
		PRINT ふたなり
	ENDIF
	PRINTL ]にしますか？
	PRINTL [0] - する
	PRINTL [1] - しない
	INPUT
	IF RESULT == 0
		C = ASSI
		;CALL FUTANARI
	ELSEIF RESULT == 1
		PRINTFORML %NAME:ASSI%は[具現]を失った
		TALENT:ASSI:具現 = 0
	ELSE
		GOTO INPUT_LOOP_2
	ENDIF
	DRAWLINE
	WAIT
ENDIF

ISASSI:ASSI = 1

RETURN 0


@YOUJI
;調教者が幼稚
IF TALENT:PLAYER:幼稚
	PRINTS NAME:PLAYER
	PRINT が乳を吸ううちに
	PRINTS NAME:TARGET
	PRINTW は母乳が出るようになった
	TALENT:母乳体質 = 1
ENDIF

@EVENTCOMEND

IF VAGINA(TARGET) && TALENT:処女 && EXP:Ａ経験 > 0 && TALENT:貞操観念 && !CFLAG:100
	CFLAG:100 = 1
	PRINTW 貞操を守れるなら、少しは後ろを責められてもよいと思っているようだ……
ENDIF

;神技の処理　ストレス値の減少値を返す
@SINGI(ARG)
#FUNCTION
;白山が主人ではない　かつ　助手がいない
SIF NO:MASTER != 164 && ASSI < 0
	RETURNF 0
;白山が主人ではない　かつ　助手がいる、ただし白山ではない
SIF NO:MASTER != 164 && (ASSI >= 0 && NO:ASSI != 164)
	RETURNF 0
;ARGのストレス値が一定以下
SIF CFLAG:ARG:10 < 90
	RETURNF 0
;白山の体力が1000未満
SIF (NO:MASTER == 164 && BASE:MASTER:体力 < 1000) || (ASSI >= 0 && NO:ASSI == 164 && BASE:ASSI:体力 < 1000)
	RETURNF 0

;最低でも5は回復
LOCAL = 5
;0から5までランダムで回復
LOCAL += RAND:6

;技巧によって更に回復
;白山が主人
IF NO:MASTER == 164
	LOCAL += ABL:MASTER:技巧
	;一律で体力が削れる
	BASE:MASTER:体力 -= 500
;白山が助手
ELSE
	LOCAL += ABL:ASSI:技巧
	;一律で体力が削れる
	BASE:ASSI:体力 -= 500
ENDIF
RETURNF LOCAL

;調教で【崩壊】を得た場合のイベント
@COLLAPSE_MIND_TRAIN
TFLAG:13 = 52
;口上分岐フラグ
TFLAG:53 = 3
CALL KOJO_MESSAGE_EVENT
;お守りがあれば復活
IF CFLAG:693 || CFLAG:694
	TFLAG:13 = 72
	CALL KOJO_MESSAGE_EVENT
	IF CFLAG:694
		CFLAG:10 = 0
		CFLAG:694 -= 1
	ELSE
		CFLAG:10 = 90
		CFLAG:693 -= 1
	ENDIF
ELSE
	TALENT:崩壊 = 1
	IF CFLAG:39
		PRINTFORM %CALLNAME%は[
		SIF TALENT:恋慕
			PRINTS TALENTNAME:3
		SIF TALENT:淫乱
			PRINTS TALENTNAME:4
		SIF TALENT:服従
			PRINTS TALENTNAME:5
		SIF TALENT:親愛
			PRINTS TALENTNAME:6
		SIF TALENT:娼婦
			PRINTS TALENTNAME:7
		SIF TALENT:隷属
			PRINTS TALENTNAME:8
		PRINTW ]を失った
	ENDIF
	SIF TALENT:恋慕
		TALENT:恋慕 = 0
	SIF TALENT:淫乱
		TALENT:淫乱 = 0
	SIF TALENT:服従
		TALENT:服従 = 0
	SIF TALENT:親愛
		TALENT:親愛 = 0
	SIF TALENT:娼婦
		TALENT:娼婦 = 0
	SIF TALENT:隷属
		TALENT:隷属 = 0
	;相愛がある場合、相愛相手の[相愛]を消し、陥落素質記録フラグを[親愛]に戻す
	IF CFLAG:39 == 165
		TALENT:(NO2(CFLAG:LOCAL:41)):相愛 = 0
		CFLAG:(NO2(CFLAG:LOCAL:41)):39 = 6
	ENDIF
	CFLAG:40 = 0
	CFLAG:41 = -1
	CFLAG:10 = 0
ENDIF

@EVENTEND
#PRI
PLAYER = MASTER
CALL MASTER_CHANGE_ON
CALL MASTER_CHANGE_OFF

@EVENTEND
#LATER
PRINTL 調教を終了しました

;調教後の後処理
IF TEQUIP:触手膣内産卵 || TEQUIP:触手腸内産卵
	IF TEQUIP:触手膣内産卵 && TEQUIP:触手腸内産卵
		CALL TRAIN_MESSAGE_DOUBLE_EGGPLAY_OUT
		CALL DOUBLE_EGGPLAY_OUT
	ELSEIF TEQUIP:触手膣内産卵
		CALL TRAIN_MESSAGE_EGGPLAY_OUT
		CALL EGGPLAY_OUT
	ELSEIF TEQUIP:触手腸内産卵
		CALL TRAIN_MESSAGE_KANTYO_EGGPLAY_OUT
		CALL KANTYO_EGGPLAY_OUT
	ENDIF
ELSEIF BASE:便意 >= 60
	CALL AFTERTRAIN_KANTYO
ENDIF


;調教後行為のチェック
CALL SELF_CHECK

;搾乳した母乳の売却
CALL SELL_MILK

;調教時に録画したビデオを売却
CALL SELL_VIDEO

;調教時に撮影した写真を売却
CALL SELL_PICTURE

;調教時に減った体力、気力に応じて体力、気力の最大値上昇
CALL EVENT_GROWTH

;何の珠を得られたか
CALL JUEL_CHECK

;否定の珠の打ち消しチェック
CALL DENIAL_CHECK

BEGIN ABLUP

@JUEL_CHECK
REPEAT 15
	IF PALAM:COUNT < PALAMLV:1
		G = 0
	ELSEIF PALAM:COUNT < PALAMLV:1*3
		G = 1
	ELSEIF PALAM:COUNT < PALAMLV:2
		G = 2
	ELSEIF PALAM:COUNT < PALAMLV:2*3
		G = 10
	ELSEIF PALAM:COUNT < PALAMLV:3
		G = 20
	ELSEIF PALAM:COUNT < PALAMLV:3*2
		G = 100
	ELSEIF PALAM:COUNT < PALAMLV:4
		G = 200
	ELSEIF PALAM:COUNT < PALAMLV:5
		G = 1000
	ELSEIF PALAM:COUNT < PALAMLV:6
		G = 2000
	ELSEIF PALAM:COUNT < PALAMLV:7
		G = 3000
	ELSEIF PALAM:COUNT < PALAMLV:8
		G = 5000
	ELSEIF PALAM:COUNT < PALAMLV:9
		G = 8000
	ELSEIF PALAM:COUNT < 1000000
		G = 12000
	ELSEIF PALAM:COUNT < 5000000
		G = 25000
	ELSEIF PALAM:COUNT < 30000000
		G = 40000
	ELSEIF PALAM:COUNT < 100000000
		G = 60000
	ELSE
		G = 100000
	ENDIF

	IF COUNT == 0
		GOTJUEL:COUNT = G + EX:0 * 1000
	ELSEIF COUNT == 1
		GOTJUEL:COUNT = G + EX:1 * 1000
	ELSEIF COUNT == 2
		GOTJUEL:COUNT = G + EX:2 * 1000
	ELSEIF COUNT == 3
		GOTJUEL:COUNT = G + EX:3 * 1000
	ELSEIF COUNT < 12
		GOTJUEL:COUNT = G
	ELSE
		GOTJUEL:100 += G
	ENDIF
REND

DRAWLINE
PRINTL 調教の結果：
REPEAT 15
	D = COUNT
	
	IF D == 12
		PRINTFORML %PALAMNAME:100%の珠×({JUEL:否定}+{GOTJUEL:100})
		JUEL:否定 += GOTJUEL:100
	ELSEIF D != 4 && D != 13 && D != 14
		PRINTFORML %PALAMNAME:D%の珠×({JUEL:D}+{GOTJUEL:D})
		JUEL:D += GOTJUEL:D
	ENDIF
	
REND

PRINTW 以上の珠を得ました

@DENIAL_CHECK
SIF JUEL:否定 == 0
	RETURN 0

$LABEL_1
A = RAND:3 + 5
B = JUEL:否定 / 2
SIF B == 0 && JUEL:否定 > 0
	B = 1
SIF JUEL:A < B
	B = JUEL:A
JUEL:A -= B
JUEL:否定 -= B

SIF B > 0
	PRINTFORML %PALAMNAME:A%の珠×{B}減少
SIF JUEL:否定 > 0 && (JUEL:恭順 + JUEL:欲情 + JUEL:屈服) > 0
	GOTO LABEL_1

$LABEL_2
A = RAND:3 + 5
B = JUEL:否定 / 2
SIF B == 0 && JUEL:否定 > 0
	B = 1
SIF JUEL:A < B
	B = JUEL:A
JUEL:A -= B
JUEL:否定 -= B
SIF B > 0
	PRINTFORML %PALAMNAME:A%の珠×{B}個減少
SIF JUEL:否定 > 0 && (JUEL:恭順 + JUEL:欲情 + JUEL:屈服) > 0
	GOTO LABEL_2

DRAWLINE
PRINTL 否定の珠と他の珠の打ち消しが発生しています
PRINTL その結果、
REPEAT 13
	D = COUNT
	
	IF D == 12
		PRINTFORML %PALAMNAME:100%の珠×({JUEL:否定})
	ELSEIF D != 4
		PRINTFORML %PALAMNAME:D%の珠×({JUEL:D})
	ENDIF

REND

PRINTW 以上のように変化しました


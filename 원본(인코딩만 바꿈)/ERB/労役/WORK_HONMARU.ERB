;====================================================================
;労役指示(本丸)
;====================================================================
;本丸要素の濃い労役を分けました

;該当する労役
;料理を作る　料理を作ってもらう
;手合わせ　畑当番　馬当番
;====================================================================

;-------------------------------------------------
@CHARA_WORKING_HONMARU
;-------------------------------------------------
;項目表示用のカウント変数
#DIM DYNAMIC TEAWASE = 0
#DIM DYNAMIC HATAKE = 0
#DIM DYNAMIC UMA = 0
#DIM DYNAMIC KIWAME = 0

D = 1
CALL DAILY_LIFE_MASTER_CHANGE

REPEAT CHARANUM
	;あなたは不可
	SIF NO:COUNT == 0
		CONTINUE
	;現在の主人は不可
	SIF NO:COUNT == NO:MASTER
		CONTINUE
	;既に労役中は不可
	SIF CFLAG:COUNT:12 > 0
		CONTINUE
	;気絶中は不可
	SIF TALENT:COUNT:気絶
		CONTINUE
	;育児中は不可
	SIF TALENT:COUNT:育児中
		CONTINUE
	;発情期は不可
	SIF TALENT:COUNT:発情期
		CONTINUE
	
	;手合わせ
	;妊娠中は不可
	IF TALENT:COUNT:妊娠
	;体力1000未満で不可
	ELSEIF BASE:COUNT:体力 < 1000
	ELSE
		CFLAG:COUNT:151 |= 64
		TEAWASE += 1
	ENDIF
	
	;畑当番　馬当番
	;達磨は不可
	IF TALENT:COUNT:達磨
	;体力1000未満で不可
	ELSEIF BASE:COUNT:体力 < 1000
	ELSE
		CFLAG:COUNT:151 |= 128
		HATAKE += 1
		CFLAG:COUNT:151 |= 256
		UMA += 1
	ENDIF
	
	;修行
	;極キャラのCSVがなかったら不可
	;該当男士の極キャラが既にいたら不可（売却してても不可）
	;髭切は先に処理
	IF NO:COUNT == 107
		SIF !EXISTCSV(111 ,0)
			CONTINUE
		SIF ITEMSALES:(199 + 111) == 2
			CONTINUE
	;膝丸は先に処理
	ELSEIF NO:COUNT == 112
		SIF !EXISTCSV(115 ,0)
			CONTINUE
		SIF ITEMSALES:(199 + 115) == 2
			CONTINUE
	ELSE
		SIF !EXISTCSV(NO:COUNT + 1 ,0)
			CONTINUE
		SIF ITEMSALES:(199 + NO:COUNT + 1) == 2
			CONTINUE
	ENDIF
	;助手可能、共犯者でなかったら不可
	SIF CFLAG:COUNT:1 != 2 && CFLAG:COUNT:1 != 3
		CONTINUE
	;すでに極だと不可
	SIF TALENT:COUNT:極
		CONTINUE
	;達磨、妊娠は不可
	SIF TALENT:COUNT:達磨 || TALENT:COUNT:妊娠
		CONTINUE
	;崩壊、失意、狂気は不可
	SIF TALENT:COUNT:崩壊 || TALENT:COUNT:失意 || TALENT:COUNT:狂気
		CONTINUE
	;後天的な幼児／幼児退行は不可
	IF TALENT:COUNT:幼児／幼児退行
		SIF !CSVTALENT(NO:COUNT, 150, 0)
			CONTINUE
	ENDIF
	;体力1000未満で不可
	IF BASE:COUNT:体力 < 1000
		CONTINUE
	ENDIF
	;ここまでで弾かれなかったら修行可能としてフラグオン
	CFLAG:COUNT:151 |= 512
	KIWAME += 1
REND


;以下画面表示部分
SIF TARGET != -1 && ((KANRAKU(TARGET) || (ABL:従順 >= 3 && ABL:奉仕精神 >= 3) || TALENT:料理上手) && MARK:反発刻印 != 3 && TALENT:達磨 == 0 && TALENT:発情期 == 0)
	PRINTFORML [10] - %CALLNAME%に料理を作ってもらう

SIF ABL:MASTER:技巧 >= 3 || ABL:MASTER:料理技能 || TALENT:MASTER:料理上手 || TALENT:MASTER:メシマズ
	PRINTL [11] - 自分で料理を作る

;内番は昼なら可能
SIF TIME == 0 && (FLAG:161 & 1) == 0 && TEAWASE >= 2
	PRINTL [20] 内番 -手合わせ
SIF TIME == 0 && (FLAG:161 & 2) == 0 && HATAKE >= 2
	PRINTL [30] 内番 -畑当番
SIF TIME == 0 && (FLAG:161 & 4) == 0 && UMA >= 2
	PRINTL [40] 内番 -馬当番

;修行も昼なら可能
SIF TIME == 0 && (FLAG:161 & 8) == 0 && KIWAME > 0
	PRINTL [99] 修行

PRINTL [100]- 戻る
$INPUT_LOOP
INPUT
IF RESULT == 10
	CALL LUNCH_JUDGE
ELSEIF RESULT == 11
	CALL LUNCH_SELF
ELSEIF RESULT == 20 && TEAWASE >= 2
	CALL DUTY_PRACTICE
ELSEIF RESULT == 30 && HATAKE >= 2
	CALL DUTY_HATAKE
ELSEIF RESULT == 40 && UMA >= 2
	CALL DUTY_UMA
ELSEIF RESULT == 99 && KIWAME > 0
	CALL KIWAME
ELSEIF RESULT == 100
	;労役可能フラグのリセット
	REPEAT CHARANUM
		CFLAG:COUNT:151 = 0
	REND
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF

;労役可能フラグのリセット
REPEAT CHARANUM
	CFLAG:COUNT:151 = 0
REND


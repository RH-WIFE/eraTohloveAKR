;----------------------------------------------------------------
;精飲絶頂関連の処理の始動
;----------------------------------------------------------------
@SEIIN_START
;口で射精させていなければ処理しない
IF TFLAG:0 == 0 && TFLAG:43 != 13
	RETURN
ENDIF

;調教者でない方だけが射精した場合、一時的にPLAYERに入れる
IF TFLAG:0 == 0 && TFLAG:43 == 13
	M = PLAYER
	PLAYER = ASSIPLAY ? MASTER # ASSI
ENDIF

;強制精飲絶頂の回数（通称：パブロフカウンター）をチェックする
CALL SEIIN_CHECK

;PLAYERを戻す
SIF TFLAG:0 == 0 && TFLAG:43 == 13
	PLAYER = M


;----------------------------------------------------------------
;強制精飲絶頂の回数（パブロフカウンター）チェック
;パブロフカウンター+1回で精飲絶頂へ
;パブロフカウンターが足りない場合
;器具やパイズリなどで絶頂していれば
;強制精飲絶頂へ
;----------------------------------------------------------------
@SEIIN_CHECK

;規定の回数
P = 20
;娼婦、淫乱は基本15
SIF TALENT:淫乱 == 1 || TALENT:娼婦 == 1
	P = 15

;奴隷の素質により規定回数を増減する
;気丈
SIF TALENT:気丈 == 1
	P += 2
;プライド高い
SIF TALENT:プライド高い == 1
	P += 5
;保守的
SIF TALENT:保守的 == 1
	P += 2
;楽観的
SIF TALENT:楽観的 == 1
	P -= 2
;悲観的
SIF TALENT:悲観的 == 1
	P += 2
;一線越えない
SIF TALENT:一線越えない == 1
	P += 5
;抑圧
SIF TALENT:抑圧 == 1
	P += 2
;解放
SIF TALENT:解放 == 1
	P -= 2
;薬毒耐性
SIF TALENT:薬毒耐性 == 1
	P += 2
;汚れ無視
SIF TALENT:汚れ無視 == 1
	P -= 3
;汚臭鈍感
SIF TALENT:汚臭鈍感 == 1
	P -= 2
;汚臭敏感
SIF TALENT:汚臭敏感 == 1
	P += 2
;快感に素直
SIF TALENT:快感に素直 == 1
	P -= 2
;快感の否定
SIF TALENT:快感の否定 == 1
	P += 2
;中毒しやすい
SIF TALENT:中毒しやすい == 1
	P -= 5
;倒錯的
SIF TALENT:倒錯的 == 1
	P -= 2
;男嫌い
SIF TALENT:男嫌い == 1
	P += 2

IF EXP:強制精飲絶頂 >= P
	CALL SEIIN_ORGASM
ELSEIF TFLAG:29 > 0
	CALL SEIIN_COMPULSION_ORGASM
ENDIF

;----------------------------------------------------------------
;精飲絶頂の処理
;----------------------------------------------------------------
@SEIIN_ORGASM

;精飲絶頂経験の回数追加
EX:13 += 1
EXP:精飲絶頂経験 += 1

;精飲絶頂経験の処理
;経験の回数によって処理が変化
IF EXP:精飲絶頂経験 == 1
	PRINTFORML 
	PRINTFORMW %CALLNAME%の様子がおかしい…
	PRINTFORML %CALLNAME:PLAYER%が%CALLNAME%の口に射精した瞬間、
	;通常の絶頂の有無によって地の文が変化
	IF TFLAG:29 > 0
		PRINTFORMW %CALLNAME%はいつもよりも大きく身体を震わせた…
	ELSE
		PRINTFORML %CALLNAME%は性的な刺激が無いにも関わらず
		PRINTFORMW 精液を飲んだだけでイってしまったようだ……
	ENDIF
	PRINTFORML 
	PRINTFORMW %CALLNAME%は口内射精に過剰に反応するようになりました
	;精液中毒がLV3に上がる
	IF ABL:精液中毒 < 3
		PRINTFORMW %CALLNAME%の%ABLNAME:16%が3に上がりました
		ABL:精液中毒 = 3
	ENDIF
	PRINTFORMW %EXPNAME:50%＋1
	EXP:異常経験 += 1
	
	;素質の変化
	;汚臭敏感がなくなり、弱味を握る
	SIF TALENT:汚臭敏感 == 1
		PRINTFORMW %CALLNAME%は%TALENTNAME:62%でなくなりました
		TALENT:汚臭敏感 = 0
	SIF TALENT:弱味 == 0
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME%の%TALENTNAME:37%を握りました
	PRINTL 
	TALENT:弱味 = 1
ELSE
	PRINTFORML %CALLNAME:PLAYER%の射精と同時に
	PRINTFORML ペニスをくわえ込んでいた%CALLNAME%はくぐもった声をあげ、
	PRINTFORML ピクッと肩を小さく震わせて絶頂に達した…
ENDIF

PRINTFORML 精飲絶頂経験＋1

;パラメーター増減
;基本ソース　露出、欲情、潤滑、屈従、反発
SOURCE:液体 += 500
SOURCE:欲情 += 2000
SOURCE:露出 += 1000
SOURCE:屈従 += 3000
SOURCE:反発 += 800

IF EXP:精飲絶頂経験 == 1
	TIMES SOURCE:露出 , 1.50
	TIMES SOURCE:屈従 , 2.00
	TIMES SOURCE:反発 , 1.50
ELSEIF ABL:精液中毒 == 5
	TIMES SOURCE:屈従 , 1.20
ELSEIF ABL:精液中毒 == 8
	TIMES SOURCE:欲情 , 1.50
	TIMES SOURCE:反発 , 0.80
	TIMES SOURCE:屈従 , 1.50
ELSEIF ABL:精液中毒 == 10
	TIMES SOURCE:欲情 , 2.00
	TIMES SOURCE:屈従 , 2.00
	TIMES SOURCE:反発 , 0.50
ENDIF


;----------------------------------------------------------------
;強制精飲絶頂の処理
;強制精飲絶頂の経験回数をCFLAG:4000に格納
;----------------------------------------------------------------
@SEIIN_COMPULSION_ORGASM

EXP:強制精飲絶頂 += 1

;強制精飲絶頂の経験回数によって処理を分岐
IF EXP:強制精飲絶頂 == 1
	PRINTFORML 
	PRINTFORML %CALLNAME%は%CALLNAME:PLAYER%が放った精液を飲み下しながら
	PRINTFORML 与えられ続ける刺激によって
	PRINTFORML 強制的にイかされてしまっている…
ELSEIF EXP:強制精飲絶頂 == P
	PRINTFORML 
	PRINTFORML %CALLNAME%はすっかり
	PRINTFORML 精液の味を覚えこんでしまったようだ…
ELSE
	PRINTFORML 強制精飲絶頂
	PRINTL 
ENDIF

;パラメーター増減
;基本ソース
;屈従、恐れ、鬱屈、反発
SOURCE:恐れ += 500
SOURCE:屈従 += 1000
SOURCE:鬱屈 += 200
SOURCE:反発 += 300

IF EXP:強制精飲絶頂 == 1
	TIMES SOURCE:恐れ , 1.50
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:鬱屈 , 1.50
	TIMES SOURCE:反発 , 1.50
ELSEIF EXP:強制精飲絶頂 == P
	SOURCE:欲情 = 1000
ENDIF

;EVENT_P、EVENT_PREGNANCY、妊娠関連のイベント

;---------------------------------------------------------
;フラグ管理　口上用のフラグと被るので、変更
;---------------------------------------------------------

;CFLAG
;101→誰によってどこで妊娠させられたか
;（一の位：2=マスター, 3=助手, 4=奴隷, 5=顧客　百の位：1=膣での妊娠, 2=アナルでの妊娠）
;102→マスターによる中田氏カウント用
;103→助手による中田氏カウント用
;104→奴隷による中田氏カウント用
;105→娼館などの客と奴隷間の中田氏カウント用
;106→ぶっかけ場所判別フラグ
;107→使用済みコンドーム判定（コンドーム精飲に使用）
;108→避妊結界の使用の有無（ファイル外で使用）
;109→排卵誘発剤の使用の有無
;110→出産日
;111→父親の固有番号（顧客は-1が格納）

;702→マスターによる中田氏カウント用
;703→助手による中田氏カウント用
;704→奴隷による中田氏カウント用
;705→娼館などの客と奴隷間の中田氏カウント用
;


;FLAG
;97→臨月or育児中の奴隷はいるか？
;98→射精箇所選択機能および妊娠機能のON・OFF
;99→射精選択の自動モード時射精先選択機能のON・OFF

;TFLAG
;700→育児部屋コマンド実行した？

;---------------------------------------------------------
;eraheta用変更
;---------------------------------------------------------
;淫乱時の父親との相性によるストレス軽減処理を変更
;育児室判定の変更(育児期間が30日になってた)
;口上表示時のASSI番号保存処理削除(必要だとは思えないので)

;妊娠発覚処理のGET_CHILD系がどうにもおかしかったのでまるっと変更
;ついでに主人も妊娠するように魔改造……どうしてこうなった

;---------------------------------------------------------
;eraTolove用変更
;---------------------------------------------------------
;アナルで孕めるように妊娠チェックや妊娠処理を改造
;一文字変数をできるだけ置き換えました
;PREGNANT		チェック・処理対象キャラの登録番号を格納
;EJAC_FLAG		膣射・腸射を格納しているCFLAG
;FATHER			父親の登録番号を格納
;FERTILITY		排卵誘発剤・発情期の場合の妊娠チェック時用の値を格納

;---------------------------------------------------------
;中田氏・妊娠フラグに絡む関数郡
;---------------------------------------------------------
@IN_VAGINA_ANAL_ALL
FOR PREGNANT, 0, CHARANUM
	;妊娠判定（妊娠してたら102-105か702-705がRESULTに格納）
	CALL PREGNANCY_CHECK
	;父親の固有番号記録（マスター=2　助手=3　奴隷=4　不明=5）
	;妊娠していない場合はスルーされるはず
	SELECTCASE (RESULT % 100)
		;父親が主人
		CASE 2
			CFLAG:PREGNANT:111 = NO:MASTER
		;父親が助手
		CASE 3
			CFLAG:PREGNANT:111 = NO:ASSI
		;父親が奴隷
		CASE 4
			CFLAG:PREGNANT:111 = NO:TARGET
		;父親が顧客（-1を格納）
		CASE 5
			CFLAG:PREGNANT:111 = -1
		;妊娠してないなら次のループへ
		CASEELSE
			CONTINUE
	ENDSELECT
	;出産予定日を設定
	CFLAG:PREGNANT:110 = DAY + 60 + RAND:6
	;現在の子供の数フラグ＋
	FLAG:33++
NEXT
;中田氏カウントをクリア
CVARSET CFLAG, 102, 0
CVARSET CFLAG, 103, 0
CVARSET CFLAG, 104, 0
CVARSET CFLAG, 105, 0

;腸射カウントクリア
CVARSET CFLAG, 702, 0
CVARSET CFLAG, 703, 0
CVARSET CFLAG, 704, 0
CVARSET CFLAG, 705, 0

@PREGNANCY_CHECK
;膣射・腸射による妊娠、非妊娠のチェック
;膣射・腸射1～10までは5刻み、10～50までは10刻みで確率が上昇する。50～100までは一律。毎回リセット。
;小柄体系は妊娠しづらい。

;妊娠確定・妊娠中・育児中なら関数終了
SIF CFLAG:PREGNANT:110 > 0 || TALENT:PREGNANT:妊娠 || TALENT:PREGNANT:育児中
	RETURN 0
	
;オトコ・半陰半陽・カントボーイでオメガ持ちでないなら関数終了
SIF MALE(PREGNANT) && TALENT:PREGNANT:オメガ == 0
	RETURN 0
	
;小人体型なら関数終了
SIF TALENT:PREGNANT:小人体型
	RETURN 0

;子供の数が多すぎるなら関数終了
SIF FLAG:33 >= 10
	RETURN 0
	

;EJAC_FLAG = チェックするフラグ　CFLAG:102-105、もしくはCFLAG:702-705
FOR EJAC_FLAG, 2, 5
	
	;助手がいない場合　奴隷がいない場合は　次のフラグまで処理を飛ばす
	SIF (EJAC_FLAG == 3 && ASSI < 0) || (EJAC_FLAG == 4 && TARGET < 0)
		CONTINUE
	
	;オトコなら腸射（CFLAG:702-705）を見る
	IF MALE(PREGNANT)
		EJAC_FLAG += 700
	;それ以外なら膣射（CFLAG:102-105）を見る
	ELSE
		EJAC_FLAG += 100
	ENDIF
	
	
	;膣射・腸射されてないなら次のフラグのチェックへ
	SIF CFLAG:PREGNANT:EJAC_FLAG == 0
		CONTINUE

	;排卵誘発剤・発情期の定数を設定
	;FERTILITYは4(初期値)か2か1が設定される
	;排卵誘発剤
	FERTILITY = 4
	FERTILITY -= CFLAG:PREGNANT:109 * 2
	;発情期
	SIF TALENT:PREGNANT:発情期
		FERTILITY -= (TALENT:PREGNANT:発情期)

	IF CFLAG:PREGNANT:EJAC_FLAG >= 1 && CFLAG:PREGNANT:EJAC_FLAG < 5
		SIF RAND:((8 + TALENT:PREGNANT:小柄体型 * 2) * FERTILITY) == 0
			CFLAG:PREGNANT:101 = EJAC_FLAG
	ELSEIF CFLAG:PREGNANT:EJAC_FLAG >= 5 && CFLAG:PREGNANT:EJAC_FLAG < 10
		SIF RAND:((7 + TALENT:PREGNANT:小柄体型 * 2) * FERTILITY) == 0
			CFLAG:PREGNANT:101 = EJAC_FLAG
	ELSEIF CFLAG:PREGNANT:EJAC_FLAG >= 10 && CFLAG:PREGNANT:EJAC_FLAG < 20
		SIF RAND:((6 + TALENT:PREGNANT:小柄体型 * 2) * FERTILITY) == 0
			CFLAG:PREGNANT:101 = EJAC_FLAG
	ELSEIF CFLAG:PREGNANT:EJAC_FLAG >= 20 && CFLAG:PREGNANT:EJAC_FLAG < 30
		SIF RAND:((5 + TALENT:PREGNANT:小柄体型 * 2) * FERTILITY) == 0
			CFLAG:PREGNANT:101 = EJAC_FLAG
	ELSEIF CFLAG:PREGNANT:EJAC_FLAG >= 30 && CFLAG:PREGNANT:EJAC_FLAG < 40
		SIF RAND:((4 + TALENT:PREGNANT:小柄体型 * 2) * FERTILITY) == 0
			CFLAG:PREGNANT:101 = EJAC_FLAG
	ELSEIF CFLAG:PREGNANT:EJAC_FLAG >= 40 && CFLAG:PREGNANT:EJAC_FLAG < 50
		SIF RAND:((3 + TALENT:PREGNANT:小柄体型 * 2) * FERTILITY) == 0
			CFLAG:PREGNANT:101 = EJAC_FLAG
	ELSEIF CFLAG:PREGNANT:EJAC_FLAG >= 50
		SIF RAND:((2 + TALENT:PREGNANT:小柄体型 * 2) * FERTILITY) == 0
			CFLAG:PREGNANT:101 = EJAC_FLAG
	ENDIF

	;妊娠デバッグフラグ
	SIF FLAG:151
		CFLAG:PREGNANT:101 = EJAC_FLAG
		
	;妊娠確定したらループを抜ける
	SIF CFLAG:PREGNANT:101 > 0
		BREAK
NEXT

RETURN CFLAG:PREGNANT:101

;---------------------------------------------------------
;妊娠処理関数郡
;---------------------------------------------------------
@GET_CHILD_ALL
;キャラの妊娠発覚時の処理
FOR PREGNANT, 0, CHARANUM
	;対象が妊娠中・育児中・労役中でない　かつ　出産日まで54日以内　かつ　妊娠が確定している
	IF !TALENT:PREGNANT:妊娠 && !TALENT:PREGNANT:育児中 && !CFLAG:PREGNANT:12 && CFLAG:PREGNANT:110 <= DAY+54 && CFLAG:PREGNANT:101 > 0
	;上記の条件に引っかかったら次のキャラへ
	ELSE
		CONTINUE
	ENDIF
	;父親をFATHERとする(いなければ-1)
	FATHER = GETCHARA(CFLAG:PREGNANT:111)
	IF PREGNANT == MASTER || PREGNANT == 0
		PRINTW どうも体調がおかしい……
	ELSE
		PRINTFORMW %CALLNAME:PREGNANT%の様子がおかしい……
	ENDIF
	IF CFLAG:PREGNANT:111 >= 0 && FATHER >= 0
		PRINTFORML %CALLNAME:PREGNANT%は%CALLNAME:FATHER%の子供を身篭った様だ
	ELSEIF CFLAG:PREGNANT:101 % 100 == 5
		PRINTFORML %CALLNAME:PREGNANT%は名も知らぬ男の子供を身篭った様だ
	ELSEIF (CFLAG:PREGNANT:111 > 0 && CFLAG:PREGNANT:111 < 890) && EXISTCSV(CFLAG:PREGNANT:111, 0)
		PRINTFORML %CALLNAME:PREGNANT%は%CSVCALLNAME(CFLAG:PREGNANT:111, 0)%の子供を身篭っていた様だ
	ELSE
		PRINTFORML %CALLNAME:PREGNANT%は先日売却した奴隷の子供を身篭っていた様だ
	ENDIF
	PRINTFORMW %CALLNAME:PREGNANT%は[%TALENTNAME:153%]した
	TALENT:PREGNANT:妊娠 = 1
	EXP:PREGNANT:妊娠経験 += 1
	PRINTL EXP:妊娠経験＋1
	CALL CHANGE_N_STATUS
NEXT

@CHANGE_N_STATUS
;妊娠時のステータスの変化
;共通処理
;体力上限を減らす
;体力が上限超えることもあるので、体力501以上なら500にする
MAXBASE:PREGNANT:体力 -= 500
IF BASE:PREGNANT:体力 >= 501
	BASE:PREGNANT:体力 = 500
	IF PREGNANT == MASTER
		PRINTFORMW なんだか酷く疲れた……
	ELSE
		PRINTFORMW 酷く疲れているようだ…休ませた方が良いだろう
	ENDIF
	PRINTFORMW 　
ENDIF

;オトコ・半陰半陽・カントボーイでない
IF MALE(PREGNANT) == 0
	;乳がワンランクパワーアップする
	;絶→貧　貧→普　普→巨　巨・爆→そのまま
	;胸のサイズ関連の素質をCFLAG:23に保存(1で標準、他はTALENT番号そのまま)
	IF TALENT:PREGNANT:絶壁 == 1
		PRINTFORMW %CALLNAME:PREGNANT%の胸が%TALENTNAME:110%から%TALENTNAME:109%ぐらいに大きくなった
		CFLAG:PREGNANT:23 = 110
		TALENT:PREGNANT:絶壁 = 0
		TALENT:PREGNANT:貧乳 = 1
	ELSEIF TALENT:PREGNANT:貧乳 == 1
		PRINTFORMW %CALLNAME:PREGNANT%の胸が%TALENTNAME:109%から普通程度に大きくになった
		PRINTFORMW 　
		CFLAG:PREGNANT:23 = 109
		TALENT:PREGNANT:貧乳 = 0
	ELSEIF TALENT:PREGNANT:巨乳 == 0 && TALENT:PREGNANT:爆乳 == 0
		PRINTFORMW %CALLNAME:PREGNANT%の胸が普通程度から%TALENTNAME:111%ぐらいに大きくなった
		PRINTFORMW 　
			CFLAG:PREGNANT:23 = 1
		TALENT:PREGNANT:巨乳 = 1
	ELSEIF TALENT:PREGNANT:爆乳
		CFLAG:PREGNANT:23 = 112
	ELSEIF TALENT:PREGNANT:巨乳
		CFLAG:PREGNANT:23 = 111
	ENDIF
ENDIF

;母乳出るようになる
IF TALENT:PREGNANT:母乳体質 == 0 && (((FLAG:2 & 256) == 0 && (BREAST(TARGET) || TALENT:絶壁)) || (FLAG:2 & 256))
	PRINTFORMW %CALLNAME:PREGNANT%は母乳が出るようになった
	PRINTFORMW 　
	TALENT:PREGNANT:母乳体質 = 1
ENDIF

SIF PREGNANT == MASTER
	RETURN 0

;父親が誰なのかと母親の状態によるストレス値の分岐
;淫乱時の相性によるストレス軽減処理
A = FATHER
R = 10
IF CFLAG:PREGNANT:111 >= 0 && FATHER >= 0
	IF RELATION:PREGNANT:FATHER != 0
		R = RELATION:FATHER:FATHER / 10
	ENDIF
ENDIF

;母親が未陥落
IF !KANRAKU2(PREGNANT)
	CFLAG:PREGNANT:10 += 100 - EXP:PREGNANT:出産経験 * 5 - TALENT:PREGNANT:母性 * 5 - TALENT:PREGNANT:子供好き * 5
;父親が顧客
ELSEIF CFLAG:PREGNANT:101 % 100 == 5
	;母親が服従陥落
	IF KANRAKU2(PREGNANT) == 3
		CFLAG:PREGNANT:10 += 20
	;母親が恋慕陥落
	ELSEIF KANRAKU2(PREGNANT) == 1
		CFLAG:PREGNANT:10 += 50 - EXP:PREGNANT:出産経験 * 5 - TALENT:PREGNANT:母性 * 5 - TALENT:PREGNANT:子供好き * 5
	;母親が淫乱陥落
	ELSE
		CFLAG:PREGNANT:10 +=  30 - EXP:PREGNANT:出産経験 * 5 - TALENT:PREGNANT:母性 * 5 - TALENT:PREGNANT:子供好き * 5
	ENDIF
;母親が淫乱陥落
ELSEIF KANRAKU2(PREGNANT) == 2
	CFLAG:PREGNANT:10 += 40 / R - EXP:PREGNANT:出産経験 * 5 - TALENT:PREGNANT:母性 * 5 - TALENT:PREGNANT:子供好き * 5
;父親が売却された元思慕・畏怖対象
ELSEIF CFLAG:PREGNANT:38 > 0 && CFLAG:PREGNANT:111 == CFLAG:PREGNANT:38
	CFLAG:PREGNANT:10 -= 50
;母親が服従陥落
ELSEIF KANRAKU2(PREGNANT) == 3
	;畏怖対象が父親
	IF CFLAG:PREGNANT:41 == NO:FATHER
		CFLAG:PREGNANT:10 += 0
	;それ以外
	ELSE
		CFLAG:PREGNANT:10 += 20
	ENDIF
;母親が恋慕陥落
ELSE
	;思慕対象が父親
	IF CFLAG:PREGNANT:41 == NO:FATHER
		CFLAG:PREGNANT:10 -= 50
	;それ以外
	ELSE
		CFLAG:PREGNANT:10 += 50
	ENDIF
ENDIF

SIF CFLAG:PREGNANT:10 < 0
	CFLAG:PREGNANT:10 = 0

;ストレス値の累積が100を超えた場合
IF CFLAG:PREGNANT:10 >= 100 && TALENT:PREGNANT:崩壊 == 0
	TFLAG:13 = 52
	;口上分岐フラグ
	TFLAG:53 = 2
	LOCAL = TARGET
	TARGET = T
	;愛の無い妊娠扱いされてしまうので、父親を呼び出してみます
	IF CFLAG:PREGNANT:111 >= 0 && FATHER >= 0
		PRINTFORMW %CALLNAME:MASTER%は父親である%CALLNAME:FATHER%を呼び出し、%CALLNAME%に妊娠を告げさせた
		LOCAL:3 = MASTER
		MASTER = FATHER
		CALL MASTER_CHANGE_OFF
		CALL MASTER_CHANGE_ON
	ENDIF
	CALL KOJO_MESSAGE_EVENT
	TARGET = LOCAL
	IF CFLAG:PREGNANT:111 >= 0 && FATHER >= 0
		MASTER = LOCAL:3
		CALL MASTER_CHANGE_OFF
		CALL MASTER_CHANGE_ON
	ENDIF
	;お守りがあれば復活
	IF CFLAG:PREGNANT:693 || CFLAG:PREGNANT:694
		TFLAG:13 = 72
		CALL KOJO_MESSAGE_EVENT
		IF CFLAG:PREGNANT:694
			CFLAG:PREGNANT:10 = 0
			CFLAG:PREGNANT:694 -= 1
		ELSE
			CFLAG:PREGNANT:10 = 90
			CFLAG:PREGNANT:693 -= 1
		ENDIF
	ELSE
		IF KANRAKU(PREGNANT) && CFLAG:PREGNANT:10 >= 100
			PRINTFORMW %CALLNAME:PREGNANT%は[%TALENTNAME:(CFLAG:PREGNANT:39)%]を失った
			TALENT:PREGNANT:恋慕 = 0
			TALENT:PREGNANT:淫乱 = 0
			TALENT:PREGNANT:服従 = 0
			TALENT:PREGNANT:親愛 = 0
			TALENT:PREGNANT:娼婦 = 0
			TALENT:PREGNANT:隷属 = 0
		ENDIF
		SIF CFLAG:PREGNANT:10 >= 100
			TALENT:PREGNANT:崩壊 = 1
	ENDIF
ENDIF

;助手・奴隷によって妊娠
IF CFLAG:PREGNANT:101 % 100 == 3 || CFLAG:PREGNANT:101 % 100 == 4
	W = FATHER
ENDIF
;現在の調教対象の退避
V = TARGET
TARGET = PREGNANT
;ここでのAはCFLAG:PREGNANT:38に入ってるかを判定するので父親の固有番号
A = CFLAG:PREGNANT:111
;妊娠発覚時のイベント口上
TFLAG:13 = 11
CALL KOJO_MESSAGE_EVENT
;調教対象を戻す
TARGET = V
;助手・奴隷によって妊娠
IF CFLAG:PREGNANT:101 % 100 == 3 || CFLAG:PREGNANT:101 % 100 == 4
	FATHER = W
ENDIF

;臨月到達
@REACH_FULL_TERM
PREGNANT = COUNT
PRINTFORML %CALLNAME:PREGNANT%は臨月を迎えました
PRINTFORMW %CALLNAME:PREGNANT%は出産に備え育児室へ移動します
SIF TARGET == PREGNANT
	TARGET = -1
SIF ASSI == PREGNANT
	ASSI = -1
;イベントキャンセルフラグ
SIF !CFLAG:PREGNANT:42
	CFLAG:PREGNANT:42 = 2
;主人の場合そのまま出産までの日数をスキップ
IF MASTER == PREGNANT
	PRINTFORMW その三日後……
	PRINTFORMW ・・・
	PRINTFORMW ・・
	PRINTFORMW ・
	DAY += 3 + 1
	C = MASTER
	CALL CHILD_BIRTH
ENDIF

;出産
@CHILD_BIRTH
PREGNANT = C
FATHER = GETCHARA(CFLAG:PREGNANT:111)
LOCAL = FATHER
IF CFLAG:C:111 >= 0 && FATHER > -1
	PRINTFORML %CALLNAME:PREGNANT%は、%CSVCALLNAME(CFLAG:PREGNANT:111, 0)%の子供を無事出産しました
ELSE
	PRINTFORML %CALLNAME:PREGNANT%は子供を無事出産しました
ENDIF
PRINTL 
;膣で孕む
IF CFLAG:PREGNANT:101 / 100 == 1
	PRINTL Ｖ拡張経験＋10
	EXP:PREGNANT:Ｖ拡張経験 += 10
;アナルで孕む
ELSEIF CFLAG:PREGNANT:101 / 100 == 7
	PRINTL Ａ拡張経験＋10
	EXP:PREGNANT:Ａ拡張経験 += 10
ENDIF
PRINTL 出産経験＋1
EXP:PREGNANT:出産経験 += 1

PRINTW 
IF PREGNANT == MASTER && FATHER >= 0
	PRINTFORML %CALLNAME:MASTER%は、%CALLNAME:FATHER%を呼び出して子供の顔を見せてやった
	PRINTFORM %CALLNAME:FATHER%は
	IF (TALENT:FATHER:恋慕 || TALENT:FATHER:親愛) && CFLAG:FATHER:41 == NO:MASTER
		PRINTL とても幸せそうに子供の顔を覗き込んでいる
	ELSEIF TALENT:FATHER:淫乱 || TALENT:FATHER:娼婦
		PRINTFORML これでまた%CALLNAME:MASTER%と気持ち良い事が出来ると喜んでいる
	ELSEIF (TALENT:FATHER:服従 || TALENT:FATHER:隷属) && CFLAG:FATHER:41 == NO:MASTER
		PRINTFORML %CALLNAME:MASTER%が無事で良かったと安堵している
	ELSE
		PRINTL 複雑な表情で2人を見下ろしている……
	ENDIF
	;反発刻印があったら１減少
	IF MARK:FATHER:反発刻印 >= 1
		MARK:FATHER:反発刻印 -= 1
		PRINTFORML %CALLNAME:FATHER%の%MARKNAME:3%がLv{MARK:反発刻印}になった
	ENDIF
ELSE
	T = TARGET
	TARGET = PREGNANT
	;愛の無い妊娠扱いされてしまうので、父親を呼び出してみます
	IF CFLAG:PREGNANT:111 >= 0 && FATHER > -1
		IF MASTER == FATHER
			PRINTFORMW %CALLNAME:MASTER%は見舞いに行くことにした
		ELSE
			PRINTFORMW %CALLNAME:MASTER%は父親である%CALLNAME:FATHER%に%CALLNAME%を見舞わせた
		ENDIF
		LOCAL:3 = MASTER
		MASTER = FATHER
		CALL MASTER_CHANGE_OFF
		CALL MASTER_CHANGE_ON
	ENDIF
	;出産時のイベント口上
	TFLAG:13 = 12
	CALL KOJO_MESSAGE_EVENT
	TARGET = T
	IF CFLAG:PREGNANT:111 >= 0 && FATHER > -1
		MASTER = LOCAL:3
		CALL MASTER_CHANGE_OFF
		CALL MASTER_CHANGE_ON
	ENDIF
ENDIF

;妊娠を失う
TALENT:PREGNANT:妊娠 = 0

$INPUT_LOOP
;育児放棄をした場合(主人出産の場合も育児放棄)
IF TALENT:PREGNANT:崩壊 || PREGNANT == MASTER
	;育児放棄の際の赤子の処置
	A = 0
	REPEAT CHARANUM
		SIF COUNT != MASTER && !TALENT:COUNT:妊娠 && (TALENT:COUNT:育児中 || TALENT:COUNT:母性) && CFLAG:COUNT:12 == 0
			A += 1
	REND
	IF A > 0
		PRINTFORMW 産まれた赤ん坊を他の奴隷に任せるか、里子に出すかを選択してください
		PRINTW 
		PRINTL 誰に任せますか？
		REPEAT CHARANUM
			;主人以外で現在[妊娠]状態でなく、[育児中]か[母性]持ちで労役中でないキャラを羅列
			SIF COUNT != MASTER && !TALENT:COUNT:妊娠 && (TALENT:COUNT:育児中 || TALENT:COUNT:母性) && CFLAG:COUNT:12 == 0
				PRINTFORML [{COUNT,2}]%NAME:COUNT% 
		REND
		PRINTFORML  [100]里子に出す
		INPUT
		IF RESULT == 100
			PRINTFORMW %CALLNAME:MASTER%はやむなく、赤ん坊を里子に出しました
			;現在の子供の数フラグ
			FLAG:33--
		ELSEIF RESULT < 0 || RESULT >= CHARANUM
			GOTO INPUT_LOOP
		ELSEIF COUNT == MASTER || TALENT:RESULT:妊娠 || (!TALENT:RESULT:育児中 && !TALENT:RESULT:母性) && CFLAG:RESULT:12 == 0
			GOTO INPUT_LOOP
		ENDIF
		IF RESULT != 100
			PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:RESULT%に乳母をやってもらうことにしました
			;育児期間を設定する
			CFLAG:RESULT:110 = DAY
			IF !TALENT:RESULT:育児中
				TALENT:RESULT:育児中 = 1
				SIF RESULT == TARGET
					TARGET = -1
				SIF RESULT == ASSI
					TARGET = -1
			ENDIF
			;heta用娘パッチ
			CALL CHILD_CHECK(PREGNANT, LOCAL, RESULT)
		ENDIF
	ELSE
		PRINTFORMW %CALLNAME:MASTER%はやむなく、赤ん坊を里子に出しました
		;現在の子供の数フラグ
		FLAG:33--
	ENDIF
	;妊娠時に減った体力の最大値が回復
	MAXBASE:PREGNANT:体力 += 500
	CALL CLEAR_FLAG
	;妊娠時に大きくなった胸のサイズを一段小さくする
	CALL REVERSE_B_SIZE

;育児に入った場合
ELSE
	PRINTFORML %CALLNAME:PREGNANT%は育児室で育児を始めた
	TALENT:PREGNANT:育児中 = 1
	;恋慕･親愛持ちで父親が思慕対象
	IF KANRAKU2(PREGNANT) == 1 && CFLAG:PREGNANT:111 == CFLAG:PREGNANT:41 && TALENT:PREGNANT:母性 == 0
		PRINTFORM どうやら%CALLNAME:PREGNANT%は[
		PRINTS TALENTNAME:155
		PRINTW ]に目覚めたようだ
		TALENT:PREGNANT:母性 = 1
	ENDIF
	;heta用娘パッチ
	CALL CHILD_CHECK(PREGNANT, LOCAL)
ENDIF


@CHILD_CARE_ROOM(ARG, ARG:1)
SIF ARG:1
	PRINTFORML %CALLNAME:ARG%は子供の様子を見にやってきた
PRINTL どの部屋へ様子を見に行きますか？
VARSET LOCAL
IF ARG:1
	LOCAL:5 = MASTER
	MASTER = ARG
	CALL MASTER_CHANGE_OFF
	CALL MASTER_CHANGE_ON
ENDIF
REPEAT CHARANUM
	IF COUNT == MASTER
		CONTINUE
	;現在臨月か[育児中]のキャラを羅列、一度訪れたら消す
	ELSEIF !LOCAL:(10 + COUNT) && ((TALENT:COUNT:妊娠 && (CFLAG:COUNT:110 - 3) <= DAY) || TALENT:COUNT:育児中)
		;育児中の場合、育てている子供を検索　実子を優先して表示する
		IF TALENT:COUNT:育児中
			FOR LOCAL, 1, CHARANUM
				IF CFLAG:LOCAL:115 == NO:COUNT && CFLAG:LOCAL:116
					;LOCAL:1にまだ実子が入ってなければ子供の登録番号を入れる
					SIF !LOCAL:2
						LOCAL:1 = LOCAL
					;子供が実子の場合、実子フラグを立てる
					IF CFLAG:COUNT:114 == NO:LOCAL
						LOCAL:2 += 1
					;子供が養子の場合、養子フラグを立てる
					ELSE
						LOCAL:3 += 1
					ENDIF
				ENDIF
			NEXT
		ENDIF
		PRINTFORM [{COUNT,2}]%CALLNAME:COUNT%
		IF TALENT:COUNT:育児中
			PRINTFORML と%CALLNAME:(LOCAL:1)%\@ LOCAL:2 + LOCAL:3 >= 2 ? たち # \@
		ELSE
			PRINTL 
		ENDIF
	ENDIF
REND
PRINTFORML [1000]戻る

$INPUT_LOOP
INPUT
IF RESULT == 1000
	IF ARG:1
		MASTER = LOCAL:5
		CALL MASTER_CHANGE_OFF
		CALL MASTER_CHANGE_ON
	ENDIF
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ELSEIF RESULT == MASTER
	GOTO INPUT_LOOP
ELSEIF !TALENT:RESULT:妊娠 && (CFLAG:RESULT:110 - 3) <= DAY && !TALENT:RESULT:育児中
	GOTO INPUT_LOOP
ELSE
	;育児中の場合、育てている子供を検索　実子を優先して表示する
	LOCAL = 0
	LOCAL:1 = 0
	LOCAL:2 = 0
	LOCAL:3 = 0
	IF TALENT:RESULT:育児中
		FOR LOCAL, 1, CHARANUM
			IF CFLAG:LOCAL:115 == NO:RESULT && CFLAG:LOCAL:116
				;LOCAL:1にまだ実子が入ってなければ子供の登録番号を入れる
				SIF !LOCAL:2
					LOCAL:1 = LOCAL
				;子供が実子の場合、実子フラグを立てる
				IF CFLAG:RESULT:114 == NO:LOCAL
					LOCAL:2 += 1
				;子供が養子の場合、養子フラグを立てる
				ELSE
					LOCAL:3 += 1
				ENDIF
			ENDIF
		NEXT
	ENDIF
	PRINTFORM %CALLNAME:MASTER%は%CALLNAME:RESULT%
	SIF TALENT:RESULT:育児中
		PRINTFORM と%CALLNAME:(LOCAL:1)%\@ LOCAL:2 + LOCAL:3 > 1 ? たち # \@
	PRINTL のところへ向かった
	PRINTW 
	;訪問済み
	LOCAL:(10 + RESULT)++
	;育児室のイベント口上
	A = TARGET
	TARGET = RESULT
	TFLAG:13 = 13
	CALL KOJO_MESSAGE_EVENT
	TARGET = A
	PRINTL 
	RESTART
ENDIF


RETURN 1


;親離れ
@DEPEARENT
VARSET LOCAL
LOCAL:3 = NO2(CFLAG:C:115)
PRINTFORML %CALLNAME:(LOCAL:3)%が育てていた%NAME:C%が親離れしました
;子供の成長フラグ、素質をクリア
CFLAG:C:26 = 0
CFLAG:C:116 = 0
;他に育てている子供がいないか検索
FOR LOCAL:1, 0, CHARANUM
	SIF CFLAG:(LOCAL:1):115 == CFLAG:C:115 && CFLAG:(LOCAL:1):116
		LOCAL:2++
NEXT
PRINTW 
LOCAL = ASSI
ASSI = C
PREGNANT = LOCAL:3
;他に育てている子がいなければ、母(乳母)は調教復帰
IF !LOCAL:2
	PRINTFORMW %CALLNAME:PREGNANT%は再び調教可能になりました
	;妊娠時に減った体力の最大値が回復
	MAXBASE:PREGNANT:体力 += 500
	TALENT:PREGNANT:育児中 = 0
	IF !TALENT:PREGNANT:貧乳 && !TALENT:PREGNANT:絶壁
		SIF BREAST(PREGNANT)
			PRINTFORMW %CALLNAME:PREGNANT%の胸は母乳を与える役割を終えたことで小さくなりました
		;妊娠時に大きくなった胸のサイズを一段小さくする
		CALL REVERSE_B_SIZE
	ENDIF
	CALL CLEAR_FLAG
ENDIF

;育てるか否か
$CHILD
PRINTFORMW %CALLNAME:ASSI%は立派に成長しました
PRINTFORML 手元に置いて調教しますか？
PRINTL 
PRINTL [0] 独り立ちさせる
PRINTL [1] 手元に置いて調教する
PRINTFORML [2] %CALLNAME:ASSI%の能力を見る
$INPUT_LOOP
INPUT
IF RESULT == 0
	;親離れ時のイベント口上
	TFLAG:13 = 14
	LOCAL:1 = TARGET
	TARGET = C
	CALL KOJO_MESSAGE_EVENT
	TARGET = LOCAL:1
	LOCAL:2 = ASSI
	;手元に番号被りの別の子供がいるかどうかチェック
	REPEAT CHARANUM
		SIF COUNT != ASSI && NO:COUNT == NO:ASSI
			LOCAL:4 = COUNT
	REND
	;親離れする子供に対する相性をリセット
	IF !LOCAL:4
		REPEAT CHARANUM
			RELATION:COUNT:(NO:ASSI) = 0
		REND
	ENDIF
	DELCHARA LOCAL:2
	IF LOCAL > LOCAL:2
		ASSI = (LOCAL - 1)
	ELSE
		ASSI = LOCAL
	ENDIF
	SIF MASTER > LOCAL:2
		MASTER -= 1
	SIF TARGET > LOCAL:2
		TARGET -= 1
	;現在の子供の数フラグ－
	FLAG:33--
ELSEIF RESULT == 1
	TARGET = C
	;手元に置く時のイベント口上
	TFLAG:13 = 45
	CALL KOJO_MESSAGE_EVENT
	TARGET = ASSI
	ASSI = -1
ELSEIF RESULT == 2
	CALL PRINT_STATUS(ASSI)
	GOTO CHILD
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

;妊娠パッチまわりのフラグをリセット
@CLEAR_FLAG
SIF CFLAG:PREGNANT:42 == 2
	CFLAG:PREGNANT:42 = 0
CFLAG:PREGNANT:101 = 0
CFLAG:PREGNANT:102 = 0
CFLAG:PREGNANT:103 = 0
CFLAG:PREGNANT:104 = 0
CFLAG:PREGNANT:105 = 0
CFLAG:PREGNANT:110 = 0
CFLAG:PREGNANT:111 = 0
CFLAG:PREGNANT:114 = 0
CFLAG:PREGNANT:702 = 0
CFLAG:PREGNANT:703 = 0
CFLAG:PREGNANT:704 = 0
CFLAG:PREGNANT:705 = 0


;子育て終了or妊娠強制終了時の胸サイズの縮小処理
@REVERSE_B_SIZE
;爆→巨　巨→普　普→貧　貧・絶→そのまま
;CFLAG:23が有ったら胸のサイズ関連の素質を復帰
IF CFLAG:PREGNANT:23 == 0
	IF TALENT:PREGNANT:爆乳
		TALENT:PREGNANT:爆乳 = 0
		TALENT:PREGNANT:巨乳 = 1
	ELSEIF TALENT:PREGNANT:巨乳
		TALENT:PREGNANT:巨乳 = 0
	ELSEIF TALENT:PREGNANT:貧乳 == 0 && TALENT:PREGNANT:絶壁 == 0
		TALENT:PREGNANT:貧乳 = 1
	ENDIF
ELSEIF CFLAG:PREGNANT:23 >= 109 && CFLAG:PREGNANT:23 <= 112
	A = CFLAG:PREGNANT:23
	TALENT:PREGNANT:A = 1
ELSEIF CFLAG:PREGNANT:23 == 1
	TALENT:PREGNANT:爆乳 = 0
	TALENT:PREGNANT:巨乳 = 0
ENDIF

;育児室にいるかの判定
@CHECK_CHILD_CARE
SIF (TALENT:C:妊娠 && CFLAG:C:110 - 3 <= DAY) || TALENT:C:育児中
	RETURN 1

RETURN 0


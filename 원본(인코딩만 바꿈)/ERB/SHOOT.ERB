;射精関係の処理

;TFLAG:703　～　デフォルト射精先設定一時変数
;1=膣内射精、2=アナル射精、3=手淫関係、4=フェラ・パイズリ、5=手と口以外（素股など）、
;6=助手射精、7=クンニ、9=騎乗位する
;TFLAG:704　～　射精箇所選択可能か否か
;0=不可、1=可

;---------------------------------------------------------
;eraheta用変更
;---------------------------------------------------------
;射精関連処理が色々おかしかったので色々と手を加えた(変えすぎて変更点が上げられない…)

;基本的にコマンド内の射精処理は調教者側です
;奴隷用はTRACHECKで行われてますが、膣射・腸射だけはここで制御してるので注意
;PLAYER以外が射精するようなコマンドの場合も注意が必要（3Pを参考にして下さい）

;---------------------------------------------------------
;射精するかのチェック
;コマンドファイル内でチェックした射精ゲージを反映。射精していればEを返す。
;---------------------------------------------------------
@SAMEN_CHECK(ARG,ARG:1)
;汎用的な関数なので、将来的にはすべての調教コマンドの射精チェックをここでやることも可能
S = BASE:ARG:射精
EJAC = MAXBASE:ARG:射精

SIF TALENT:ARG:早漏
	TIMES S ,1.50
SIF TALENT:ARG:遅漏
	TIMES S , 0.70
;童貞喪失時の射精ゲージボーナス
SIF TALENT:ARG:童貞 && TFLAG:160 == 2
	S += 5000
;射精限界が来ている場合は、射精ゲージは強制的に0にする
IF CFLAG:ARG:11 == 0
	S = 0
	BASE:ARG:射精 = 0
ENDIF

IF S > EJAC * 2
	;調教者以外の射精フラグ
	TFLAG:6 = 2
	E = 2
ELSEIF S > EJAC
	;調教者以外の射精フラグ
	TFLAG:6 = 2
	E = 1
ELSE
	E = 0
ENDIF
SIF ARG:1
	TFLAG:6 = 0


;さらにコマンドファイル内にて、Eがあれば射精時の処理を行う。
;このコマンドではどこに射精するのが普通か（セックスなら膣、フェラなら口など）を判定
;オプションで射精先を「都度選択する」に設定している場合、選択する事ができるか否かを判定
;射精時の処理
;IF E >= 1
;	;射精先タイプの設定
;	TFLAG:703 = 4
;	;射精先選択の可・不可
;	TFLAG:704 = 1
;	;汎用的な処理はこの関数で行う
;	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
;	CALL SAMEN_SHOOT
;ENDIF


;---------------------------------------------------------
;射精場所選択
;---------------------------------------------------------
@SAMEN_SHOOT
;ぶっかけ選択
;[0] 髪 手コキ区分
;[1] 顔	手コキ区分
;[2] 胸	フェラ・パイズリ区分
;[3] 腹	フェラ・パイズリ区分
;[4] 腋 フェラ・パイズリ2区分
;[5] 腿 セックス2区分
;[6] 割れ目 セックス2区分
;[7] 足　フェラ・パイズリ区分
;[8] ゴム射・中田氏

;各コマンド中で射精の判定に使われる変数Eを使用する。
;これまでの射精の処理をここに持たせ、各コマンド内で使わないようにする。
;よってここでEを初期化し、各COMFに処理を返す（E == 0ならば各コマンド内で射精の処理は行われない）

;射精した人物の設定
IF TFLAG:6
	P = ASSIPLAY ? MASTER # ASSI
ELSE
	P = PLAYER
ENDIF

;射精する対象の設定
IF (SELECTCOM == 270 || SELECTCOM == 271) && TFLAG:703 != 6
	T = MASTER
ELSEIF ((SELECTCOM == 440 || SELECTCOM == 441) && TFLAG:703 != 6) || (SELECTCOM == 270 || SELECTCOM == 271)
	T = ASSI
ELSE
	T = TARGET
ENDIF

;まずぶっかけ場所判別フラグを初期化(調教者以外の射精のときはフラグを保存)
SIF TFLAG:6
	F = CFLAG:T:106
CFLAG:T:106 = 0

;追加部分、射精箇所選択機能をOFFにした際にコンドームを含めた通常の射精処理を行う
;また、状況的に射精箇所の選択を行えないであろうコマンドの場合もここで通常処理に飛ばす

$INPUT_LOOP
;子供は射精できない。
IF TALENT:P:子供サイズ
	CALL KARA_SHOOT_CHECK
	RETURN 0
;オプションで「そのまま射精」が選択されている、もしくはコマンド内の射精箇所選択が不可の場合
ELSEIF FLAG:99 == 8 || TFLAG:704 == 0
	;コンドーム装備中は、そのままコンドーム射精へ。
	IF TEQUIP:P:コンドーム == 1
		CFLAG:T:106 = 9
	;通常射精
	ELSE
		;コマンドファイル内で設定されている、基本的な箇所を確認する処理へ飛ばす。
		CALL COMMAND_CHECK
		RETURN 0
	ENDIF
;オプションで「都度選択」を設定していて、射精箇所選択可能な場合
ELSEIF FLAG:99 == 9 && P == PLAYER
	PRINTFORMW %CALLNAME:P%のペニスは限界まで膨張し、もう射精寸前だ……
	PRINTFORMW 　
	PRINTFORMW %CALLNAME:T%のどこに射精しますか？
	PRINTL [0] 「髪」
	PRINTL [1] 「顔」
	PRINTL [2] 「胸」
	PRINTL [3] 「腹」
	PRINTL [4] 「腋」
	PRINTL [5] 「太もも」
	PRINTL [6] 「割れ目」
	PRINTL [7] 「尻」
	PRINTL [8] 「足」
	IF TEQUIP:P:コンドーム
		PRINTL [9] 「コンドームに射精する」
	ELSE
		PRINTL [9] 「そのまま射精する」
	ENDIF
	INPUT
	IF RESULT == 0
		CFLAG:T:106 = 1
	ELSEIF RESULT == 1
		CFLAG:T:106 = 2
	ELSEIF RESULT == 2
		CFLAG:T:106 = 3
	ELSEIF RESULT == 3
		CFLAG:T:106 = 4
	ELSEIF RESULT == 4
		CFLAG:T:106 = 5
	ELSEIF RESULT == 5
		CFLAG:T:106 = 6
	ELSEIF RESULT == 6
		CFLAG:T:106 = 7
	ELSEIF RESULT == 7
		CFLAG:T:106 = 8
	ELSEIF RESULT == 8
		CFLAG:T:106 = 10
	ELSEIF RESULT == 9
		;コンドーム付きの場合
		IF TEQUIP:P:コンドーム == 1
			CFLAG:T:106 = 9
		ELSE
			;コマンドファイル内で設定されている、基本的な箇所を確認する処理へ飛ばす。
			CALL COMMAND_CHECK
			RETURN 0
		ENDIF
	ELSE
		GOTO INPUT_LOOP
	ENDIF
;オプションで「基本的な箇所選択」がされていれば、それぞれの処理へ飛ばす。
ELSE
	IF FLAG:99 == 0
		CFLAG:T:106 = 1
	ELSEIF FLAG:99 == 1
		CFLAG:T:106 = 2
	ELSEIF FLAG:99 == 2
		CFLAG:T:106 = 3
	ELSEIF FLAG:99 == 3
		CFLAG:T:106 = 4
	ELSEIF FLAG:99 == 4
		CFLAG:T:106 = 5
	ELSEIF FLAG:99 == 5
		CFLAG:T:106 = 6
	ELSEIF FLAG:99 == 6
		CFLAG:T:106 = 7
	ELSEIF FLAG:99 == 7
		CFLAG:T:106 = 8
	ENDIF
ENDIF

;上の処理を抜けてきた場合、設定された箇所へ射精する処理へ飛ばす。
CALL CHECK_CFLAG106



;---------------------------------------------------------
;射精処理関数その１　コマンド毎に設定された、基本射精箇所に射精する場合。
;---------------------------------------------------------
@COMMAND_CHECK
;そのまま射精時の処理の分岐
;コマンドによる処理ターゲットの確認と分岐
IF TFLAG:703 == 1
	;膣内射精
	CALL SAMEN_SHOOT_OLD_SEX
	CALL IN_VAGINA_CHECK
ELSEIF TFLAG:703 == 2
	;アナル射精
	CALL SAMEN_SHOOT_OLD_SEX2
	CALL IN_VAGINA_CHECK
ELSEIF TFLAG:703 == 3
	;手淫
	CALL SAMEN_SHOOT_OLD_FINGER
ELSEIF TFLAG:703 == 4
	;フェラ・パイズリ
	CALL SAMEN_SHOOT_OLD_ORAL
ELSEIF TFLAG:703 == 5
	;手と口以外（素股など）
	CALL SAMEN_SHOOT_OLD_SUMATA
ELSEIF TFLAG:703 == 6
	;助手射精
	CALL SAMEN_SHOOT_OLD_ASSISTANT
ELSEIF TFLAG:703 == 7
	;クンニ強制
	CALL SAMEN_SHOOT_OLD_PUSSY
ELSEIF TFLAG:703 == 9
	;騎乗位する
	CALL SAMEN_SHOOT_OLD_RAPED
ENDIF


;---------------------------------------------------------
;射精処理関数その２
;実際に射精を行うため、汚れもここで判定。
;---------------------------------------------------------
;CFLAG:106 = ぶっかけ場所判別フラグ
@CHECK_CFLAG106
;髪
IF CFLAG:T:106 == 1
	IF TEQUIP:T:頭部装飾 >= 5
		STAIN:T:頭部装飾 |= 4
	ELSE
		STAIN:T:髪 |= 4
	ENDIF
;顔
ELSEIF CFLAG:T:106 == 2
	SIF TEQUIP:T:特殊３
		STAIN:T:目装飾 |= 4
;胸、腹、腋
ELSEIF CFLAG:T:106 == 3 || CFLAG:T:106 == 4 || CFLAG:T:106 == 5
	;胸の場合、着衣はまくらない。前開きの服にはつかない。
	IF CFLAG:T:106 == 3
		IF TOPS(T)
			STAIN:T:全身上着 |= 4
		ELSEIF TEQUIP:T:上衣
			STAIN:T:上衣 |= 4
		ELSEIF TEQUIP:T:下着（上）
			STAIN:T:下着（上） |= 4
		ELSE
			STAIN:T:胸 |= 4
		ENDIF
	;腋の場合、全身上着、上衣はまくらない。下着に精液汚れは付着しない。
	ELSEIF CFLAG:T:106 == 5 
		IF TEQUIP:T:全身上着
			STAIN:T:全身上着 |= 4
		ELSEIF TEQUIP:T:上衣
			STAIN:T:上衣 |= 4
		ENDIF
	ENDIF
;太もも、割れ目、尻
ELSEIF CFLAG:T:106 == 6 || CFLAG:T:106 == 7 || CFLAG:T:106 == 8
	;着衣の場合（スカート系はまくるので先に排除しておく）
	IF BOTTOMS(T) >= 4 || TEQUIP:T:下着（下）
		;太ももの場合、ハーフパンツ系、ショートパンツ系、下着なら地肌に射精できる。
		IF CFLAG:T:106 == 6 && (BOTTOMS(T) == 4 || TEQUIP:T:下着（下）)
		;スカート以外の着衣は、着衣に汚れが付く
		ELSEIF TEQUIP:T:下衣
			STAIN:T:下衣 |= 4
		;下着に関して
		ELSEIF TEQUIP:T:下着（下）
			;尻の場合、貞操帯は関与しない
			IF CFLAG:T:106 == 8 && TEQUIP:T:貞操帯
			;それ以外は全て下着に汚れが付く
			ELSE
				STAIN:T:下着（下） |= 4
			ENDIF
		ENDIF
	ENDIF
;足
ELSEIF CFLAG:T:106 == 10
	IF TEQUIP:T:靴
		STAIN:T:靴 |= 4
	ELSEIF TEQUIP:T:特殊２
		STAIN:T:足装飾 |= 4
	ELSE
		STAIN:T:足 |= 4
	ENDIF
ENDIF

IF CFLAG:T:106 == 0
	CALL COMMAND_CHECK
ELSEIF CFLAG:T:106 <= 2
	CALL SAMEN_SHOOT_OLD_FINGER
ELSEIF CFLAG:T:106 <= 5
	CALL SAMEN_SHOOT_OLD_ORAL
ELSEIF CFLAG:T:106 <= 8
	CALL SAMEN_SHOOT_OLD_SEX3
ELSEIF CFLAG:T:106 == 9
	CALL IN_VAGINA_CHECK
ELSEIF CFLAG:T:106 == 10
	CALL SAMEN_SHOOT_OLD_ORAL
ENDIF

;調教者以外の射精のときは射精箇所をTFLAG:43に記録してフラグを戻す
IF TFLAG:6
	TFLAG:43 = CFLAG:T:106
	CFLAG:T:106 = F
ENDIF


;---------------------------------------------------------
@KARA_SHOOT_CHECK
;---------------------------------------------------------
;調教者以外の射精は別フラグ
IF TFLAG:6
	TFLAG:43 = 21
;コマンドによる処理ターゲットの確認と分岐
ELSEIF TFLAG:703 == 1
	;膣内射精
	TFLAG:164 = 1
ELSEIF TFLAG:703 == 2
	;アナル射精
	TFLAG:164 = 2
ELSEIF TFLAG:703 == 3
	;手淫
	TFLAG:164 = 3
ELSEIF TFLAG:703 == 4
	;フェラ・パイズリ
	TFLAG:164 = 4
ELSEIF TFLAG:703 == 5
	;手と口以外（素股など）
	TFLAG:164 = 5
ELSEIF TFLAG:703 == 6
	;助手射精
	TFLAG:164 = 6
ELSEIF TFLAG:703 == 7
	;クンニ強制
	TFLAG:164 = 7
ELSEIF TFLAG:703 == 9
	;騎乗位する
	TFLAG:164 = 8
ENDIF
;子供の場合射精できない
CALL SAMEN_SHOOT_KARA



;---------------------------------------------------------
;プレイや及び助手の各コマンドでの中田氏チェック
;---------------------------------------------------------
@IN_VAGINA_CHECK
;各コマンドへの影響を小さくするのが目的（関数を呼ぶ、もしくはオミットするだけで良い）
;TFLAG:2（中田氏）とTEQUIP:コンドーム（コンドーム）を条件に膣射・腸射を増やす処理を持つ

;コマンドごとの分岐
;まずコンドームで分岐
IF TEQUIP:P:コンドーム == 1
	IF !TFLAG:6
		CFLAG:T:106 = 9
	ELSE
		TFLAG:43 = 9
	ENDIF
	IF NO:P == 0
		PRINT 主人
	ELSE
		PRINTFORM %CALLNAME:P%
	ENDIF
	PRINTL 　コンドーム射精
	CALL SAMEN_SHOOT_CONDOM
;コンドームをしてないかつＶ系なら中出し
ELSEIF TFLAG:703 == 1 || TFLAG:703 == 6
	SIF P == MASTER
		A = 102
	SIF P == ASSI
		A = 103
	;セックスで射精、助手が射精、助手を犯すで射精
	IF TFLAG:2 == 2 || TFLAG:6 == 2 || TFLAG:7 == 2
		IF NO:P == 0
			PRINT 主人
		ELSE
			PRINTFORM %CALLNAME:P%
		ENDIF
		PRINTL 　膣内大量射精
		TFLAG:38 = 2
		IF CFLAG:T:108
			PRINTL <避妊結界>
		ELSE
			PRINTFORML %CALLNAME:T%の膣射経験+２
			EXP:T:膣射経験 += 2
			CFLAG:T:A += 2
			;中田氏時の恐怖判定等
			SIF T == TARGET
				CALL SAMEN_SHOOT_SOURCE
		ENDIF
	;セックスで射精、助手が射精、助手を犯すで射精
	ELSEIF TFLAG:2 == 1 || TFLAG:6 == 1 || TFLAG:7 == 1
		IF NO:P == 0
			PRINT 主人
		ELSE
			PRINTFORM %CALLNAME:P%
		ENDIF
		PRINTL 　膣内射精
		TFLAG:38 = 1
		IF CFLAG:T:108
			PRINTL <避妊結界>
		ELSE
			PRINTFORML %CALLNAME:T%の膣射経験+１
			EXP:T:膣射経験 += 1
			CFLAG:T:A += 1
			;中田氏時の恐怖判定等
			SIF T == TARGET
				CALL SAMEN_SHOOT_SOURCE
		ENDIF
	ENDIF
;コンドームしてないかつＡ系なら腸射
ELSEIF TFLAG:703 == 2 || TFLAG:703 == 6
	SIF P == MASTER
		A = 702
	SIF P == ASSI
		A = 703
	;セックスで射精、助手が射精。助手のＡを犯すで射精
	IF TFLAG:2 == 2 || TFLAG:6 == 2 || TFLAG:7 == 2
		IF NO:P == 0
			PRINT 主人
		ELSE
			PRINTFORM %CALLNAME:P%
		ENDIF
		PRINTL 腸内大量射精
		TFLAG:138 = 2
		IF CFLAG:T:108
			PRINTL 〈避妊結界〉
		ELSE
			PRINTFORML %CALLNAME:T%の腸射経験＋２
			EXP:T:腸射経験 += 2
			CFLAG:T:A += 2
			;中田氏時の恐怖判定等
			SIF T == TARGET
				CALL SAMEN_SHOOT_SOURCE
		ENDIF
	
	;セックスで射精、助手が射精。助手のＡを犯すで射精
	ELSEIF TFLAG:2 == 1 || TFLAG:6 == 1 || TFLAG:7 == 1
		IF NO:P == 0
			PRINT 主人
		ELSE
			PRINTFORM %CALLNAME:P%
		ENDIF
		PRINTL 腸内射精
		TFLAG:138 = 1
		IF CFLAG:T:108
			PRINTL 〈避妊結界〉
		ELSE
			PRINTFORML %CALLNAME:T%の腸射経験＋１
			EXP:T:腸射経験 += 1
			CFLAG:T:A += 1
			;中田氏時の恐怖判定等
			SIF T == TARGET
				CALL SAMEN_SHOOT_SOURCE
		ENDIF
	ENDIF
ENDIF

@IN_VAGINA_CHECK_T
;奴隷の各コマンドでの中田氏チェック
;各コマンドへの影響を小さくするのが目的（関数を呼ぶ、もしくはオミットするだけで良い）

;奴隷攻め系コマンド（助手を犯させる、逆セックス系）の場合
IF (SELECTCOM == 430 || SELECTCOM == 431) || SEXPLAY(TARGET) >= 3
	;射精した人物の設定
	P = TARGET

	;射精する対象の設定
	IF (SELECTCOM == 430 || SELECTCOM == 431)
		T = ASSI
	ELSE
		T = PLAYER
	ENDIF

	;Ｖ系フラグ
	V = 0
	SIF ((SELECTCOM == 430 || SELECTCOM == 431) && TFLAG:705 == 0) || SEXPLAY(TARGET) == 3
		V = 1
	
	;まずコンドームで分岐
	IF TEQUIP:P:コンドーム == 1
		PRINTFORML %CALLNAME:P%　コンドーム射精
		CALL SAMEN_SHOOT_CONDOM
	;コンドームをしてないかつＶ系なら中出し
	ELSEIF V
		IF E == 2
			PRINTFORML %CALLNAME:P%　膣内大量射精
			IF CFLAG:T:108
				PRINTL <避妊結界>
			ELSE
				PRINTFORML %CALLNAME:T%の膣射経験+２
				EXP:T:膣射経験 += 2
				CFLAG:T:104 += 2
			ENDIF
		ELSEIF E == 1
			PRINTFORML %CALLNAME:P%　膣内射精
			IF CFLAG:T:108
				PRINTL <避妊結界>
			ELSE
				PRINTFORML %CALLNAME:T%の膣射経験+１
				EXP:T:膣射経験 += 1
				CFLAG:T:104 += 1
			ENDIF
		ENDIF
	;コンドームをしてないかつＡ系なら腸中出し
	ELSEIF V == 0
		IF E == 2
			PRINTFORML %CALLNAME:P%　腸内大量射精
			IF CFLAG:T:108
				PRINTL <避妊結界>
			ELSE
				PRINTFORML %CALLNAME:T%の腸射経験+２
				EXP:T:腸射経験 += 2
				CFLAG:T:704 += 2
			ENDIF
		ELSEIF E == 1
			PRINTFORML %CALLNAME:P%　腸内射精
			IF CFLAG:T:108
				PRINTL <避妊結界>
			ELSE
				PRINTFORML %CALLNAME:T%の腸射経験+１
				EXP:T:腸射経験 += 1
				CFLAG:T:704 += 1
			ENDIF
		ENDIF
	ENDIF
ENDIF


;---------------------------------------------------------
;各射精タイプごとの処理関数
;---------------------------------------------------------
;今後はCOMF内では処理せず、必要に応じてここから呼び出す。
;汚れ移動等の処理は各COMF内でやってくれるので移動の必要無し

@SAMEN_SHOOT_CONDOM
;ゴム射精の処理
;ほとんど他の射精と一緒だが、TFLAGを立てず（膣射・腸射阻止）、精液経験も増やさない
;コンドーム関連のフラグもできるだけここで扱う。

CALL SAMEN_SHOOT_CALC

;コンドームが脱げる
TEQUIP:P:コンドーム = 0

SIF P == MASTER
	S = 4
SIF P == ASSI
	S = 2
SIF P == TARGET
	S = 1
;コンドームに精液溜まる(誰の精液かビットで格納)
CFLAG:107 |= S

;コンドーム自動装着処理
IF FLAG:98 & S && ITEM:45 <= 0
	PRINT <コンドームが無くなったので、自動装着を解除します>
	FLAG:98 = 0
ELSEIF FLAG:98 & S
		ITEM:45 -= 1
		TEQUIP:P:コンドーム = 1
ENDIF

;反感が多少減少
SOURCE:反発 -= 500


@SAMEN_SHOOT_OLD_SEX
;これまでCOMF内で使用されていたセックス射精の処理

CALL SAMEN_SHOOT_CALC

SIF T != TARGET
	RETURN 0

;大量射精
IF E == 2
	EXP:精液経験 += 1
	PRINTL 精液経験＋１

	;セックスで射精フラグ
	IF !TFLAG:6
		TFLAG:2 = 2
	ELSE
		TFLAG:43 = 18
	ENDIF
;通常の射精
ELSEIF E == 1
	;セックスで射精フラグ
	IF !TFLAG:6
		TFLAG:2 = 1
	ELSE
		TFLAG:43 = 18
	ENDIF
ENDIF

@SAMEN_SHOOT_OLD_SEX2
;これまでCOMF内で使用されていたセックス射精の処理を改造。
;アナルセックス系コマンドに使用する。

CALL SAMEN_SHOOT_CALC

SIF T != TARGET 
	RETURN 0

;大量射精
IF E == 2
	EXP:精液経験 += 2
	PRINTL 精液経験＋２
	;セックスで射精フラグ
	IF !TFLAG:6
		TFLAG:2 = 2
	ELSE
		TFLAG:43 = 17
	ENDIF
ELSEIF E == 1
	IF SELECTCOM == 116
		EXP:精液経験 += 1
		PRINTL 精液経験＋１
	ENDIF
	;セックスで射精フラグ
	IF !TFLAG:6
		TFLAG:2 = 1
	ELSE
		TFLAG:43 = 17
	ENDIF
ENDIF

@SAMEN_SHOOT_OLD_SEX3
;これまでCOMF内で使用されていたセックス射精の処理を改造。
;太もも、割れ目、尻に使用する。
;精液経験は増えるがセックス射精のフラグは立てない（立てると中田氏口上が出たり膣射経験・腸射経験が増えてしまう）

CALL SAMEN_SHOOT_CALC

SIF T != TARGET 
	RETURN 0

;大量射精
IF E == 2
	IF SELECTCOM == 116
		EXP:精液経験 += 2
		PRINTL 精液経験＋２
	ELSE
		EXP:精液経験 += 1
		PRINTL 精液経験＋１
	ENDIF
ELSEIF E == 1
	IF SELECTCOM == 116
		EXP:精液経験 += 1
		PRINTL 精液経験＋１
	ENDIF
ENDIF

;着衣だと汚れ追加
IF E > 0
	IF TEQUIP:下衣
		STAIN:下衣 |= 4
	ELSEIF TEQUIP:下着（下）
		STAIN:下着（下） |= 4
	ENDIF
ENDIF

@SAMEN_SHOOT_OLD_FINGER
;これまでCOMF内で使用されていた手コキ射精の処理

CALL SAMEN_SHOOT_CALC

SIF T != TARGET 
	RETURN 0

;大量射精
IF E == 2
	EXP:精液経験 += 3
	PRINTL 精液経験＋３
	;手で射精させたフラグ
	TFLAG:1 = 2
;通常の射精
ELSEIF E == 1
	EXP:精液経験 += 1
	PRINTL 精液経験＋１
	;手で射精させたフラグ
	TFLAG:1 = 1
ENDIF

;着衣だと汚れ追加
IF E > 0
	IF TEQUIP:特殊１ == 1
		STAIN:手袋 |= 4
	ENDIF
ENDIF

@SAMEN_SHOOT_OLD_ORAL
;これまでCOMF内で使用されていたフェラ・パイズリ射精の処理

CALL SAMEN_SHOOT_CALC

SIF T != TARGET 
	RETURN 0

;大量射精
IF E == 2
	EXP:精液経験 += 9
	PRINTL 精液経験＋９
	IF SELECTCOM == 317 || SELECTCOM == 74
		;胸で射精させたフラグ
		TFLAG:34 = 2
	ELSE
		;口で射精させたフラグ
		IF !TFLAG:6
			TFLAG:0 = 2
		ELSE
			TFLAG:43 = 13
		ENDIF
	ENDIF
;通常の射精
ELSEIF E == 1
	EXP:精液経験 += 3
	PRINTL 精液経験＋３
	IF SELECTCOM == 317 || SELECTCOM == 74
		;胸で射精させたフラグ
		TFLAG:34 = 1
	ELSE
		;口で射精させたフラグ
		IF !TFLAG:6
			TFLAG:0 = 1
		ELSE
			TFLAG:43 = 13
		ENDIF
	ENDIF
ENDIF

@SAMEN_SHOOT_OLD_SUMATA
;これまでCOMF内で使用されていた手と口以外（素股など）射精の処理

CALL SAMEN_SHOOT_CALC

SIF T != TARGET 
	RETURN 0

;大量射精
IF E == 2
	EXP:精液経験 += 2
	PRINTL 精液経験＋２
	IF SELECTCOM == 80
		;足で射精させたフラグ
		TFLAG:16 = 2
	ELSE
		;手と口以外（素股など）で射精させたフラグ
		TFLAG:9 = 2
	ENDIF
;通常の射精
ELSEIF E == 1
	EXP:精液経験 += 1
	PRINTL 精液経験＋１
	IF SELECTCOM == 80
		;足で射精させたフラグ
		TFLAG:16 = 1
	ELSEIF SELECTCOM == 73 || SELECTCOM == 74 || SELECTCOM == 421 || SELECTCOM == 504
		;胸で射精
		TFLAG:34 = 1
	ELSE
		;手と口以外（素股など）で射精させたフラグ
		TFLAG:9 = 1
	ENDIF
ENDIF

@SAMEN_SHOOT_OLD_ASSISTANT
;これまでCOMF内で使用されていた助手の射精の処理

CALL SAMEN_SHOOT_CALC

IF E
	;助手に犯させるコマンドのとき主人に精液経験
	IF SELECTCOM == 270 || SELECTCOM == 271
		EXP:MASTER:精液経験 += 1
		PRINTFORML %CALLNAME:MASTER%に精液経験＋１
		TFLAG:43 = 12
	ELSE
		EXP:精液経験 += 1
		PRINTL 精液経験＋１
		;助手を犯す
		IF SELECTCOM == 440 || SELECTCOM == 441
			TFLAG:43 = 12
		;助手を犯させる
		ELSEIF SELECTCOM == 430 || SELECTCOM == 431
			TFLAG:43 = 20
		ENDIF
	ENDIF
ENDIF

@SAMEN_SHOOT_OLD_PUSSY
;これまでCOMF内で使用されていたクンニ強制時の射精の処理

CALL SAMEN_SHOOT_CALC

SIF T != TARGET 
	RETURN 0

;大量射精
IF E == 2
	EXP:精液経験 += 4
	PRINTL 精液経験＋４
	;クンニで射精させたフラグ
	TFLAG:5 = 2
;通常の射精
ELSEIF E == 1
	EXP:精液経験 += 1
	PRINTL 精液経験＋１
	;クンニで射精させたフラグ
	TFLAG:5 = 1
ENDIF

@SAMEN_SHOOT_OLD_RAPED
;これまでCOMF内で使用されていた騎乗位するで射精の時の処理

CALL SAMEN_SHOOT_CALC

IF E == 2
	EXP:T:精液経験 += 1
	PRINTL 精液経験＋１
	;騎乗位するで射精させたフラグ
	TFLAG:12 = 2
;通常の射精
ELSEIF E == 1
	;騎乗位するで射精させたフラグ
	TFLAG:12 = 1
ENDIF

@SAMEN_SHOOT_CALC
;射精時の共通処理
;精液汚れの設定
STAIN:P:ペニス |= 4

;大量射精
IF E == 2
	IF NO:P == 0
		PRINT 主人
	ELSE
		PRINTFORM %CALLNAME:P%
	ENDIF
	PRINTL 　大量射精

	EXP:P:射精経験 += 2
	BASE:P:射精 -= EJAC*2
	SIF BASE:P:射精 >= EJAC
		BASE:P:射精 = EJAC-1
;通常の射精
ELSEIF E == 1
	IF NO:P == 0
		PRINT 主人
	ELSE
		PRINTFORM %CALLNAME:P%
	ENDIF
	PRINTL 　射精

	EXP:P:射精経験 += 1
	BASE:P:射精 -= EJAC
	SIF BASE:P:射精 >= EJAC
		BASE:P:射精 = EJAC-1
ENDIF

;射精時に中毒充足のソースを追加
;通常射精時
SIF E == 1
	A = 200
;大量射精時
SIF E == 2
	A = 500

IF E > 0
	;精液中毒を見る
	IF ABL:精液中毒 == 1
		TIMES A , 0.00
	ELSEIF ABL:精液中毒 == 2
		TIMES A , 0.30
	ELSEIF ABL:精液中毒 == 3
		TIMES A , 0.60
	ELSEIF ABL:精液中毒 == 4
		TIMES A , 0.90
	ELSEIF ABL:精液中毒 == 5
		TIMES A , 1.20
	ELSEIF ABL:精液中毒 == 6
		TIMES A , 1.50
	ELSEIF ABL:精液中毒 == 7
		TIMES A , 1.80
	ELSEIF ABL:精液中毒 == 8
		TIMES A , 2.30
	ELSEIF ABL:精液中毒 == 9
		TIMES A , 3.00
	ELSEIF ABL:精液中毒 == 10
		TIMES A , 5.00
	ENDIF
ENDIF

;ゴム射フラグがあったら中毒充足しない
SIF CFLAG:T:106 != 9
	SOURCE:中毒充足 += A

;射精後は再装填の時間を設ける
;通常射精時
SIF E == 1
	CFLAG:P:9 = 3
;大量射精時
SIF E == 2
	CFLAG:P:9 = 5
CFLAG:P:11 -= 1


;-------------------------------------------------
;空射精
;-------------------------------------------------
@SAMEN_SHOOT_KARA

;強空射精
IF E == 2
	IF NO:P == 0
		PRINT 主人
	ELSE
		PRINTFORM %CALLNAME:P%
	ENDIF
	PRINTL 　強空射精

	BASE:P:射精 -= EJAC*2
	SIF BASE:P:射精 >= EJAC
		BASE:P:射精 = EJAC-1
;空射精
ELSEIF E == 1
	IF NO:P == 0
		PRINT 主人
	ELSE
		PRINTFORM %CALLNAME:P%
	ENDIF
	PRINTL 　空射精

	BASE:P:射精 -= EJAC
	SIF BASE:P:射精 >= EJAC
		BASE:P:射精 = EJAC-1
ENDIF



;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
@SAMEN_SHOOT_SOURCE
;性転換している場合に備えて元の性別を見る
;男性なら１　女性なら０
LOCAL = MALE2(TARGET)

;元が女の場合
IF LOCAL == 0
	;未陥落の場合反発、恐怖
	IF !KANRAKU(TARGET)
		SOURCE:反発 += 1000
		;妊娠してないかつ孕む可能性がある（腸）中出し
		SIF TALENT:妊娠 == 0 && ((TFLAG:38 && MALE(TARGET) == 0) || (TFLAG:138 && TALENT:オメガ))
			SOURCE:恐れ += 1000
	;恋慕ルートは達成感のソースが増加する
	ELSEIF KANRAKU(TARGET) == 1
		SOURCE:達成感 += 1500
	;恋慕以外の陥落は恭順
	ELSE
		SOURCE:恭順 += 500
	ENDIF
;元がオトコの場合
ELSE
	;未陥落の場合反発、恐怖
	IF !KANRAKU(TARGET)
		SOURCE:反発 += 2000
		;妊娠してないかつ孕む可能性がある（腸）中出し
		SIF TALENT:妊娠 == 0 && ((TFLAG:38 && MALE(TARGET) == 0) || (TFLAG:138 && TALENT:オメガ))
			SOURCE:恐れ += 2000
	;恋慕ルートは恭順、恐怖
	ELSEIF KANRAKU(TARGET) == 1
		SOURCE:恭順 += 1000
		;妊娠してないかつ孕む可能性がある（腸）中出し
		SIF TALENT:妊娠 == 0 && ((TFLAG:38 && MALE(TARGET) == 0) || (TFLAG:138 && TALENT:オメガ))
			SOURCE:恐れ += 500
	;恋慕以外の陥落は恭順＋屈従＋恐怖
	ELSE
		SOURCE:恭順 += 300
		;妊娠してないかつ孕む可能性がある（腸）中出し
		IF TALENT:妊娠 == 0 && ((TFLAG:38 && MALE(TARGET) == 0) || (TFLAG:138 && TALENT:オメガ))
			SOURCE:恐れ += 1000
			SOURCE:屈従 += 300
		ENDIF
	ENDIF
ENDIF


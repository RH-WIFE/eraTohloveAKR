;フリーミッション終了判定
@EVENTTURNEND
#LATER
SIF FLAG:5 == 8
	CALL FREE_MISSION_CHECK
RETURN

;オープニング
@OPENING_START
DRAWLINE

MASTER = 0

;主人公によって分岐するオープニング口上１
CALL KOJO_PRE_OPENING_STORY

;上からRETURN 1すると、デフォのオープニングはカット
SIF RESULT
	RETURN 1


REPEAT 62
	A = COUNT+500
	PRINTFORMSW STR:A
REND

PRINTFORMS STR:562
A = FLAG:4+562
PRINTFORMS STR:A
PRINTFORMSW STR:566

REPEAT 8
	A = COUNT+567
	PRINTFORMSW STR:A
REND
PRINTL 

;主人公によって分岐するオープニング口上2
CALL KOJO_POST_OPENING_STORY

RETURN 1



;チュートリアル
@TUTORIAL

PRINTFORML 
PRINTFORML ―――チュートリアル―――
PRINTFORMW 
PRINTFORMW eraToLoveAにようこそ。
PRINTFORMW ここでは、eraToLoveAを攻略する上でのヒントを説明していきます。
PRINTL 
$INPUT_LOOP
PRINTFORML 表示するチュートリアルを選択してください。
PRINTFORML [ 0] ゲームの「目的」について
PRINTFORML [ 1] 「難易度」って？
PRINTFORML [ 2] お金を稼ぐには？
PRINTFORML [ 3] 奴隷を「陥落」させるには？
PRINTFORML [ 4] 「恋慕」系陥落について
PRINTFORML [ 5] 「淫乱」系陥落について
PRINTFORML [ 6] 「服従」系陥落について
PRINTFORML [ 7] その他注意点
PRINTFORML [99] チュートリアルを終了する
INPUT
IF RESULT == 0
	PRINTFORMW 「eraToLoveA」は「調教SLG(ｼﾐｭﾚｰｼｮﾝｹﾞｰﾑ)」です。
	PRINTFORMW ゲームのプレイヤーは、もちろんあなた。
	PRINTFORMW あなたはここで「主」として、「性奴隷」を育てることになります。
	IF FLAG:5 == 9
		PRINTFORMW 今回選択された「EXTRAモード」はいわゆる「おまけモード」であり、
		PRINTFORMW ゲームとしての「目的」は設定されていません。
		PRINTFORMW しかし、だからこそ、できることはたくさんあります。
		PRINTFORMW 是非、途中で投げ出すことなく、
		PRINTFORMW 「eraToLoveA」の世界を遊び尽くしてください。
	ELSE
		PRINTFORMW 育てた奴隷は、あなたの欲望を満たすだけではありません。
		PRINTFORMW 「労役」に出して働かせたり、「売却」したり……
		PRINTFORMW つまり、お金の元になるのです。
		PRINTFORMW 「あなた」の目的は、そうやってお金を稼ぐこと。
		PRINTFORMW 奴隷を使って得たお金で、さらに資金を溜めていき、
		PRINTFORMW 最終的には、「100日」以内に「100万朱」を稼ぐことが目的です。
	ENDIF
ELSEIF RESULT == 1
	PRINTFORMW 「START」モードには、難易度が設定されています。
	PRINTFORMW 難易度によって調教のしやすさや、労役を行った場合の収益、
	PRINTFORMW 維持費の保証期間等、様々な要素が変化します。
	PRINTFORMW たとえば維持費に関して、それぞれに設定された期間中は、
	PRINTFORMW 維持費としてのお金がかかることはありませんが、
	PRINTFORMW その期間を過ぎると、奴隷の維持費等で、毎朝所持甲州金が減っていきます。
	PRINTFORMW 所持甲州金が底を尽きるとゲームオーバーになりますので、
	PRINTFORMW まずはしっかりと、そこまでの資金繰りをしていきましょう。
	PRINTFORMW 維持費の保証期間はそれぞれ、
	PRINTFORMW 「EASY」20日、「NORMAL」15日、「HARD」10日となります。
	PRINTFORMW 「EXTRAモード」では、何日過ごしても維持費がかかることはありません。
ELSEIF RESULT == 2
	PRINTFORMW お金を稼ぐ方法として最も手っ取り早いのは、奴隷の「売却」です。
	PRINTFORMW ただし「売却」した奴隷は他人の手に渡ることになり、
	PRINTFORMW 基本的には、また手元に戻ってくるということはありません。
	PRINTFORMW 奴隷を手放したくないという場合は、
	PRINTFORMW 育てた奴隷に「労役」を行わせるのが良いでしょう。
	PRINTFORMW 「労役」は、ある程度調教の進んだ奴隷に行わせることができ、
	PRINTFORMW 奴隷を「売却」するよりも、効率良くお金を稼ぐことができます。
ELSEIF RESULT == 3
	PRINTFORMW 調教を繰り返していく内、奴隷が主であるあなたに対して、
	PRINTFORMW 特殊な感情を抱く場合があります。
	PRINTFORMW それは「陥落素質」と呼ばれ、奴隷のランクアップでもあります。
	PRINTFORMW 主に「陥落」した奴隷は、より素直に主の命令を聞くようになり、
	PRINTFORMW 様々な状況で有利に働くようになるでしょう。
	PRINTFORMW 一口に「調教」とはいっても、その内容は様々であり、
	PRINTFORMW どんな調教を施すかによって、奴隷が主に抱く感情も変わってきます。
	PRINTFORMW 奴隷に「合わせる」も「合わさせる」もあなた次第ですが、
	PRINTFORMW 奴隷の状況をよく観察し、適切な調教を行うことが大事です。
	PRINTFORMW また、奴隷の陥落にはさらに先の段階があります。
	PRINTFORMW すでに陥落した奴隷に、調教を重ねることによって得られますので、
	PRINTFORMW 陥落した奴隷でも放置せず、さらなる高みを目指してください。
ELSEIF RESULT == 4
	PRINTFORMW 奴隷が主人に対して、微かな恋心を抱いている状態です。
	PRINTFORMW 主人を慕い、調教にも悦びを感じるようになっています。
	PRINTFORMW これを得るためには、当然、奴隷に優しくしてあげることが大事です。
	PRINTFORMW ハードな調教や、他人の手を借りる調教は、
	PRINTFORMW 奴隷の不信感を高めることになりますので、あまりお勧めしません。
	PRINTFORMW 初めは愛撫等の愛撫系コマンドを多めにして、ある程度育ったら、
	PRINTFORMW 手淫やフェラチオ等、奉仕系コマンドで依存度を上げていきましょう。
ELSEIF RESULT == 5
	PRINTFORMW 常に淫らな事をしていないと収まらなくなった状態です。
	PRINTFORMW 淫らなことは積極的に行おうとする心理が働くため、
	PRINTFORMW 主人の命令には、従う意思を示すようになります。
	PRINTFORMW これを得るためには、まずはとにかく、感度を上げてやることです。
	PRINTFORMW アブノーマルなプレイや、同性同士での性経験など、
	PRINTFORMW 様々な経験を積ませ、いかなる快楽も享受できるようすべきですが、
	PRINTFORMW 反発を抱かれることも多いため、最初からやり過ぎには注意。
ELSEIF RESULT == 6
	PRINTFORMW 主人を「主人」として認め、服従を誓った状態です。
	PRINTFORMW 反抗する意思が弱くなるので、主人の命令にはまず逆らいません。
	PRINTFORMW これを得るためには、主人としての威厳を示す必要があります。
	PRINTFORMW そのために縄やボールギャグ等で拘束し、自由を奪ったり、
	PRINTFORMW スパンキングや鞭等のSM系コマンドで、苦痛を与える等、
	PRINTFORMW ハードな調教で依存度をマイナスに下げていくことが大事です。
	PRINTFORMW アブノーマルなプレイも積極的に行い、経験を積ませると良いでしょう。
ELSEIF RESULT == 7
	PRINTFORMW ゲーム内の「万屋」では、「素質」や「体力」を変化させる等、
	PRINTFORMW 便利なアイテムが買えることがあります。
	PRINTFORMW ただし、それらのアイテムは奴隷に多大なストレスを与えるため、
	PRINTFORMW 奴隷を壊したくないのであれば、頻繁な使用は避けましょう。
	PRINTFORMW また、調教開始時には、奴隷は基本的に「服」を着ています。
	PRINTFORMW 服を着たままでは行えないコマンドも多いですので、
	PRINTFORMW コマンドが少ないな、と思ったらまずは服を脱がせてみてください。
	PRINTFORMW いちいち脱がせるのが面倒だという場合には、
	PRINTFORMW オプションから、着衣設定を変更することもできます。
ELSEIF RESULT == 99
	PRINTFORML チュートリアルを終了します。
	PRINTFORMW ご健闘を祈ります。
	RETURN 0
ENDIF
PRINTL 
GOTO INPUT_LOOP



;===============================
;エンディングチェック
;===============================
@ENDING_CHECK
CALL MASTER_CHANGE_OFF
PRINTL 納金の手段を選択してください（エンディング内容が変化します）
PRINTL [0] - 直接納める（通常エンド)
A = 0
B = -1
C = -1
D = -1
REPEAT CHARANUM
	SIF TALENT:COUNT:恋慕 || TALENT:COUNT:淫乱 || TALENT:COUNT:服従 || TALENT:COUNT:親愛 || TALENT:COUNT:娼婦 || TALENT:COUNT:隷属
		A += 1
	SIF TALENT:COUNT:親愛
		B = COUNT
	SIF TALENT:COUNT:娼婦
		C = COUNT
	SIF TALENT:COUNT:隷属
		D = COUNT
REND

;恋慕エンド
SIF B >= 0 && FLAG:30 == 1
	PRINTFORML [1] - %CALLNAME:B%への指輪を買う（恋慕エンド）
;淫乱エンド
SIF C >= 0 && FLAG:31 == 1
	PRINTFORML [2] - %CALLNAME:C%へのバイブを買う（淫乱エンド）
;服従エンド
SIF D >= 0 && FLAG:32 == 1
	PRINTFORML [3] - %CALLNAME:D%への首輪を買う（服従エンド）
;ハーレムエンド
SIF (B >= 0 || C >= 0 || D >= 0) && A >= 5
	PRINTFORML [4] - 屋敷の権利書を買う（ハーレムエンド）


;特殊ハーレム判定
CALL ENDING_JUDGE

PRINTL [100] やめる


$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 0
	MONEY -= 1000000
	CALL ENDING_1
ELSEIF RESULT == 1 && B >= 0 && FLAG:30 == 1
	MONEY -= 1000000
	TARGET = B
	;クリア回数+１
	FLAG:41 += 1
	CALL ENDING_2
ELSEIF RESULT == 2 && C >= 0 && FLAG:31 == 1
	MONEY -= 1000000
	TARGET = C
	;クリア回数+１
	FLAG:41 += 1
	CALL ENDING_3
ELSEIF RESULT == 3 && D >= 0 && FLAG:32 == 1
	MONEY -= 1000000
	TARGET = D
	;クリア回数+１
	FLAG:41 += 1
	CALL ENDING_4
ELSEIF RESULT == 4 && (B >= 0 || C >= 0 || D >= 0) && A >= 5
	MONEY -= 1000000
	;クリア回数+２
	FLAG:41 += 2
	CALL ENDING_5


;特殊ハーレム処理

;王を名乗る
ELSEIF RESULT == 10 && (E:500 & 1)
	MONEY -= 1000000
	FLAG:41 += 3
	CALL ENDING_HAREM10
	CALL GAME_CONTINUE
ELSEIF RESULT == 11 && (E:500 & 2)
	MONEY -= 1000000
	FLAG:41 += 3
	CALL ENDING_HAREM11
	CALL GAME_CONTINUE
ELSEIF RESULT == 12 && (E:500 & 4)
	MONEY -= 1000000
	FLAG:41 += 3
	CALL ENDING_HAREM12
	CALL GAME_CONTINUE

ELSE
	GOTO INPUT_LOOP
ENDIF

;バッドエンド
@BADENDING_1
;主人公ごとのバッドエンド口上１
CALL KOJO_BAD1_ENDING_STORY

SIF RESULT
	GOTO BADENDING1

DRAWLINE
REPEAT 19
	A = COUNT+1000
	PRINTFORMSW STR:A
REND
$BADENDING1
QUIT

@BADENDING_2
;主人公ごとのバッドエンド口上2
CALL KOJO_BAD2_ENDING_STORY

SIF RESULT
	GOTO BADENDING2

DRAWLINE
REPEAT 28
	A = COUNT+1020
	PRINTFORMSW STR:A
REND
$BADENDING2
QUIT

;グッドエンド
;ノーマルエンド
@ENDING_1
;主人公ごとのノーマルエンド口上
CALL KOJO_NORMAL_ENDING_STORY

SIF RESULT
	GOTO NORMALENDING_CONTINUE

DRAWLINE
REPEAT 34
	A = COUNT+1100
	PRINTFORMSW STR:A
REND
$NORMALENDING_CONTINUE
CALL GAME_CONTINUE

;恋慕単体エンド
@ENDING_2
SIF CFLAG:21 == 1
	CALL KOJO_MESSAGE_ENDING
SIF FLAG:5 == 9
	RETURN 0

DRAWLINE
CALL ENDING_M_2
CALL GAME_CONTINUE

;淫乱単体エンド
@ENDING_3
SIF CFLAG:21 == 1
	CALL KOJO_MESSAGE_ENDING
SIF FLAG:5 == 9
	RETURN 0

DRAWLINE
CALL ENDING_M_3
CALL GAME_CONTINUE

;服従単体エンド
@ENDING_4
SIF CFLAG:21 == 1
	CALL KOJO_MESSAGE_ENDING
SIF FLAG:5 == 9
	RETURN 0

DRAWLINE
CALL ENDING_M_4
CALL GAME_CONTINUE

;ハーレムエンド
@ENDING_5
REPEAT CHARANUM
	IF CFLAG:COUNT:21 == 2
		TARGET = COUNT
		CALL KOJO_MESSAGE_ENDING
	ENDIF
	SIF FLAG:5 == 9
		BREAK
REND
SIF FLAG:5 == 9
	RETURN 0

DRAWLINE
CALL ENDING_M_5
REPEAT 8
	A = COUNT+1200
	PRINTFORMSW STR:A
REND
;妊娠奴隷
IF ABL:Ｖ感覚 >= 5
	B = 1220
;アナル奴隷
ELSEIF ABL:Ａ感覚 >= 5 && TALENT:処女
	B = 1230
;それ以外
ELSE
	B = 1210
ENDIF
REPEAT 6
	A = COUNT+B
	PRINTFORMSW STR:A
REND
PRINTFORMSW STR:1207
PRINTFORMSW STR:1208
CALL GAME_CONTINUE

;？？？エンド
@ENDING_0
DRAWLINE
REPEAT 75
	A = COUNT+2000
	PRINTFORMSW STR:A
REND
$INPUT_LOOP
PRINTFORMSL STR:2075
PRINTFORMSL STR:2076
INPUT
IF RESULT == 0
	REPEAT 28
		A = COUNT+2077
		PRINTFORMSW STR:A
	REND
	QUIT
ELSEIF RESULT == 1
	REPEAT 10
		A = COUNT+2105
		PRINTFORMSW STR:A
	REND
	ABL:MASTER:技巧 = 10
	ABL:MASTER:サドっ気 = 10
	ABL:MASTER:撮影技能 = 10
	FLAG:5 = 9
	NOITEM = 1
	MONEY += 10000000
	TALENT:MASTER:舌使い = 1
	TALENT:MASTER:針さばき = 1
	TALENT:MASTER:調合知識 = 1
	TALENT:MASTER:汚れ無視 = 1
	TALENT:MASTER:サド = 1
	TALENT:MASTER:小悪魔 = 1
	TALENT:MASTER:謎の魅力 = 1
	TALENT:MASTER:狂気 = 1
	TALENT:MASTER:禁断の知識 = 1
	TALENT:MASTER:人気 = 1
	TALENT:MASTER:絶倫 = 1
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF

@GAME_CONTINUE
;クリアフラグを立てる
FLAG:40 = 10
FLAG:41 += 3
PRINTL ゲームを継続しますか？
PRINTC [0]終わる
PRINTC [1]続ける
PRINTL  

$INPUT_LOOP
INPUT

IF RESULT == 0
	QUIT
ELSEIF RESULT == 1
	FLAG:5 = 9
ELSE
	GOTO INPUT_LOOP
ENDIF
RETURN 1




;=============================================================================
;エンディングチェック処理関連
;長くなったのと今後の拡張性の問題で関数として独立宣言してみる
;EXTRAや周回要素を前提とした大人数エンドはここでは処理しない
;A～Dは通常エンド判定で使用するため使用不可
;=============================================================================
@ENDING_JUDGE



REPEAT CHARANUM
	SIF COUNT == MASTER
		CONTINUE
	;特殊ハーレムエンド判定
	
REND

;特殊ハーレムエンド判定




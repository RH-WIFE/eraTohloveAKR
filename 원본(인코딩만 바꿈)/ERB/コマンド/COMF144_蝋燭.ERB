;-------------------------------------------------
;蝋燭責め
;ＳＭ系コマンド
;-------------------------------------------------
@COM144

PRINTL 蝋燭責め
STR:0 = 蝋燭責め
CALL KOJO_MESSAGE_COM

;使ったアイテム減らす
ITEM:46 -= 1


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;性器
IF TFLAG:56 == 1
	IF TEQUIP:地下室
		;消費体力、気力
		LOSEBASE:体力 += 100
		LOSEBASE:気力 += 125
		
		SOURCE:痛み += 4000
	ELSE
		;消費体力、気力
		LOSEBASE:体力 += 75
		LOSEBASE:気力 += 100
		
		SOURCE:痛み += 2500
	ENDIF
	SOURCE:快Ｃ += 100
;舌
ELSEIF TFLAG:56 == 3
	IF TEQUIP:地下室
		;消費体力、気力
		LOSEBASE:体力 += 75
		LOSEBASE:気力 += 100
		
		SOURCE:痛み += 2500
	ELSE
		;消費体力、気力
		LOSEBASE:体力 += 50
		LOSEBASE:気力 += 80
		
		SOURCE:痛み += 2000
	ENDIF
;乳首
ELSEIF TFLAG:56 == 2
	IF TEQUIP:地下室
		;消費体力、気力
		LOSEBASE:体力 += 50
		LOSEBASE:気力 += 80
		
		SOURCE:痛み += 2500
	ELSE
		;消費体力、気力
		LOSEBASE:体力 += 30
		LOSEBASE:気力 += 50
		
		SOURCE:痛み += 1000
	ENDIF
	SOURCE:快Ｂ += 100
ELSE
	IF TEQUIP:地下室
		;消費体力、気力
		LOSEBASE:体力 += 30
		LOSEBASE:気力 += 50
		
		SOURCE:痛み += 1000
	ELSE
		;消費体力、気力
		LOSEBASE:体力 += 10
		LOSEBASE:気力 += 30
		
		SOURCE:痛み += 700
	ENDIF
ENDIF

SOURCE:恐れ += 700
;地下室にいると恐れのソース追加
SIF TEQUIP:地下室
	TIMES SOURCE:恐れ , 1.50

SOURCE:逸脱 += 150
SOURCE:反発 += 700

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;苦痛のパラメータによる苦痛のソース増加は先に処理しておく
IF PALAM:苦痛 < PALAMLV:1
	TIMES SOURCE:痛み , 1.00
ELSEIF PALAM:苦痛 < PALAMLV:2
	TIMES SOURCE:痛み , 1.50
ELSEIF PALAM:苦痛 < PALAMLV:3
	TIMES SOURCE:痛み , 2.00
ELSEIF PALAM:苦痛 < PALAMLV:4
	TIMES SOURCE:痛み , 2.50
ELSEIF PALAM:苦痛 >= PALAMLV:4
	TIMES SOURCE:痛み , 3.00
ENDIF


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;ゲイ経験とか加算
CALL COM_EXP(2)

;依存度ベクトル
TFLAG:33 = 3

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:隷属 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF


RETURN 1




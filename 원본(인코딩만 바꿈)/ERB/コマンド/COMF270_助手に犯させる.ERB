;-------------------------------------------------
;助手に犯させる
;主人受け系コマンド
;-------------------------------------------------
@COM270

;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)
	;奴隷が助手を犯していると「連結（主人←助手←奴隷）」
	IF PREVCOM == 430 || PREVCOM == 431
		CALL COM_ABLE519
		IF RESULT == 1
			TFLAG:40 = 0
			IF PREVCOM == 430
				TFLAG:49 = 11
			ELSE
				TFLAG:49 = 12
			ENDIF
			TFLAG:41 = 6
			JUMP COM519
		ENDIF
	;主人が奴隷を犯していると「連結（奴隷←主人←助手）」
	ELSEIF PREVCOM == 100 || PREVCOM == 101 || PREVCOM == 104 || PREVCOM == 105 || PREVCOM == 110 || PREVCOM == 111 || PREVCOM == 114 || PREVCOM == 115
		CALL COM_ABLE519
		IF RESULT == 1
			IF PREVCOM == 100 || PREVCOM == 101 || PREVCOM == 104 || PREVCOM == 105
				TFLAG:40 = 1
			ELSE
				TFLAG:40 = 2
			ENDIF
			TFLAG:41 = 6
			TFLAG:49 = 0
			JUMP COM519
		ENDIF
	;両方Ｖ挿入していると「二輪挿しさせる」
	ELSEIF PREVCOM == 262
		CALL COM_ABLE522
		IF RESULT == 1
			TFLAG:40 = 0
			TFLAG:41 = 6
			TFLAG:49 = 6
			JUMP COM522
		ENDIF
	;奴隷を口奉仕していると「サンドイッチさせる」
	ELSEIF PREVCOM == 31 || PREVCOM == 40 || PREVCOM == 280
		CALL COM_ABLE520
		IF RESULT == 1
			IF PREVCOM == 31
				TFLAG:40 = 3
			ELSEIF PREVCOM == 40
				TFLAG:40 = 4
			ELSEIF PREVCOM == 280
				TFLAG:40 = 5
			ENDIF
			TFLAG:41 = 6
			TFLAG:49 = 0
			JUMP COM520
		ENDIF
	;前回コマンドも３Ｐ系
	ELSEIF PREVCOM == 519 || PREVCOM == 520 || PREVCOM == 522
		;奴隷に口奉仕してれば「サンドイッチさせる」
		IF TFLAG:40 == 3 || TFLAG:40 == 4 || TFLAG:40 == 5
			CALL COM_ABLE520
			IF RESULT == 1
				TFLAG:41 = 6
				TFLAG:49 = 0
				JUMP COM520
			ENDIF
		;主人が奴隷を犯していると「連結（奴隷←主人←助手）」
		ELSEIF TFLAG:40 == 1 || TFLAG:40 == 2
			CALL COM_ABLE519
			IF RESULT == 1
				TFLAG:41 = 6
				TFLAG:49 = 0
				JUMP COM519
			ENDIF
		;両方Ｖ挿入していると「二輪挿しさせる」
		ELSEIF TFLAG:49 == 6
			CALL COM_ABLE522
			IF RESULT == 1
				TFLAG:40 = 0
				TFLAG:41 = 6
				JUMP COM522
			ENDIF
		;奴隷が助手を犯していると「連結（主人←助手←奴隷）」
		ELSEIF TFLAG:49 == 11 || TFLAG:49 == 12
			CALL COM_ABLE519
			IF RESULT == 1
				TFLAG:40 = 0
				TFLAG:41 = 6
				JUMP COM519
			ENDIF
		ENDIF
	ENDIF
ENDIF

;何も読まずにこっちに来たときはフラグ解除
TFLAG:40 = 0
TFLAG:41 = 0
TFLAG:49 = 0
TFLAG:42 = 0

PRINTL 助手に犯させる
STR:0 = 助手に犯させる

CALL KOJO_MESSAGE_COM

;調教者が処女だった場合の喪失フラグ(処女膜再生対応)
TFLAG:19 |= 2

;助手が童貞だった場合の喪失フラグ
SIF TALENT:ASSI:童貞
	TFLAG:160 |= 3

;挿入個所判定フラグ
TFLAG:705 = 0


;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
B = 1000

CALL SHOOT_GAUGE_CHECK_MA1

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:射精 += B

;-------------------------------------------------
;射精ゲージチェック(助手)
;-------------------------------------------------
B = 1350

CALL SHOOT_GAUGE_CHECK_MA2

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:ASSI:9 > 0
	B /= 20

SIF PENIS(ASSI)
	BASE:ASSI:射精 += B

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 0
LOSEBASE:気力 += 80

SOURCE:液体 += 100
SOURCE:屈従 += 1500
SOURCE:逸脱 += 700
SOURCE:反発 += 700

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 700

;助手と奴隷の相性によって、全体のソースが変化
T = NO:TARGET
A = NO:ASSI
SOURCE:液体 *= RELATION:A
SOURCE:屈従 *= RELATION:A
SOURCE:鬱屈 *= RELATION:A
SOURCE:逸脱 *= RELATION:A
SOURCE:反発 *= RELATION:A
SOURCE:液体 /= 100
SOURCE:屈従 /= 100
SOURCE:鬱屈 /= 100
SOURCE:逸脱 /= 100
SOURCE:反発 /= 100


;-------------------------------------------------
;射精チェック（主人）
;-------------------------------------------------
CALL SAMEN_CHECK(MASTER,1)

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	TFLAG:703 = 9
	;射精先選択の可・不可
	TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;助手に犯させるで主人が射精したフラグ
IF E == 2
	TFLAG:7 = 4
ELSEIF E == 1
	TFLAG:7 = 3
ENDIF

;-------------------------------------------------
;射精チェック（助手）
;-------------------------------------------------
CALL SAMEN_CHECK(ASSI)
IF E >= 1
	;射精先タイプの設定
	TFLAG:703 = 6
	;射精先選択の可・不可
	TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;助手のＣ⇔主人のＶと膣内の汚れが移動
;ペニスバンドを使う場合、汚れは移動しない
SIF PENIS(ASSI)
	CALL COM_STAIN

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;主人が処女の場合、主人と奴隷に異常経験付加。ただし一度きり
IF TALENT:PLAYER:処女 == 1
	PRINTFORML ＜%CALLNAME:PLAYER%が処女喪失＞
	TALENT:PLAYER:処女 = 0
	EXP:PLAYER:異常経験 += 1
	;異常な状態での処女喪失で異常経験を加算する
	;処女喪失の相手がにょたかちびの自分
		;にょたちびサーチ
		R = NO:PLAYER
		L = NO:ASSI
		CALL GET_JIBUN
		IF A
			PRINTFORML %CALLNAME:ASSI%相手に処女喪失で異常経験＋１
			EXP:ASSI:異常経験 += 1
		ENDIF
	;処女喪失を撮影された
	IF TEQUIP:ビデオカメラ
		PRINTL ビデオ撮影中に処女喪失で異常経験＋１
		EXP:PLAYER:異常経験 += 1
	ENDIF
	IF (CFLAG:99 & 2) == 0
		IF RELATION:(NO:PLAYER) > 100
			EXP:異常経験 += 1
			PRINTFORML %CALLNAME%に異常経験＋１
		ENDIF
		CFLAG:99 |= 2
	ENDIF
	;へたくしょん用面識判定の更新
	G = (2099 + NO:ASSI)
	H = (2099 + NO:PLAYER)
	CFLAG:ASSI:H |= 64
	CFLAG:PLAYER:G |= 128
	;処女喪失相手の記録
	CSTR:MASTER:10 = %CALLNAME:ASSI%
ENDIF

EXP:PLAYER:Ｖ経験 += 1

;主人が小人体型で助手が馬並みだった場合
IF TALENT:PLAYER:小人体型 && TALENT:ASSI:馬並み
	IF EXP:PLAYER:Ｖ拡張経験 == 0
		EXP:PLAYER:異常経験 += 1
		PRINTFORML %CALLNAME:PLAYER%の異常経験＋１
	ENDIF
	EXP:PLAYER:Ｖ拡張経験 += 1
	PRINTFORML %CALLNAME:PLAYER%のＶ拡張経験＋１
ENDIF

;性交経験
EXP:ASSI:性交経験 += 1
EXP:MASTER:性交経験 += 1
PRINTS EXPNAME:4
PRINTL +1

;挿入経験
EXP:ASSI:挿入経験 += 1

;ゲイ経験とか加算
CALL COM_EXP(10)

;依存度ベクトル
TFLAG:33 = 5

T = NO:TARGET
A = NO:ASSI
;依存度補正値(助手との相性100↑かつ淫乱で1、娼婦で2）
IF TALENT:淫乱 && RELATION:A > 100
	TFLAG:30 += 1
ELSEIF TALENT:娼婦 && RELATION:A > 100
	TFLAG:30 += 2
ENDIF


RETURN 1



;-------------------------------------------------
;パイズリフェラする
;奉仕する系コマンド
;-------------------------------------------------
@COM241

PRINTL パイズリフェラする
STR:0 = パイズリフェラする
CALL KOJO_MESSAGE_COM

;ストッキングびりびりフラグ
IF TEQUIP:特殊２ == 5 || TEQUIP:特殊２ == 6
	TFLAG:55 = 1
ENDIF


;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 50

CALL SHOOT_GAUGE_CHECK_MT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:射精 += B


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 5
LOSEBASE:気力 += 50

SOURCE:快Ｃ += 150

SOURCE:情愛 += 150

;スキンシップ好きだと情愛のソース追加
SIF TALENT:スキンシップ好き
	SOURCE:情愛 += 50
;プレイヤーが巨乳もしくは爆乳だと情愛のソース追加
SIF TALENT:PLAYER:巨乳 || TALENT:PLAYER:爆乳
	TIMES SOURCE:情愛 , 1.20


SOURCE:露出 += 30
SOURCE:逸脱 += 30
SOURCE:反発 += 30

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 50


;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
CALL COM_STAIN


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;ゲイ経験とか加算
CALL COM_EXP(6)

;依存度ベクトル
TFLAG:33 = 1

;依存度補正値(淫乱で1、娼婦で2）
IF TALENT:淫乱 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:娼婦 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
IF TALENT:PLAYER:猫舌
	EXP:PLAYER:嗜虐快楽経験 += 1
	PRINTFORML %CALLNAME:PLAYER%の%EXPNAME:32%＋１
ENDIF

RETURN 1


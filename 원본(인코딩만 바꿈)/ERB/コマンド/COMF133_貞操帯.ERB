;-------------------------------------------------
;貞操帯
;ＳＭ-拘束系コマンド
;-------------------------------------------------
@COM133

PRINTL 貞操帯
STR:0 = 貞操帯
CALL KOJO_MESSAGE_COM


;着脱の処理
IF TEQUIP:下着（下） != 5
	TEQUIP:貞操帯 = 1
	TEQUIP:下着（下） = 5
ELSE
	TEQUIP:貞操帯 = 0
	TEQUIP:下着（下） = 0
ENDIF

IF BOTTOMS(TARGET) >= 3
	SIF TEQUIP:下衣 >= 1 && TEQUIP:下衣 <= 10
		TEQUIP:上衣 = 0
	TEQUIP:下衣 = 0
ENDIF
IF TEQUIP:特殊２ == 5 || TEQUIP:特殊２ == 6
	TEQUIP:特殊２ = 0
	TEQUIP:靴 = 0
	IF TFLAG:55 == 0
		SIF TEQUIP:特殊２ == 5
			ITEM:36 += 1
		SIF TEQUIP:特殊２ == 6
			ITEM:37 += 1
	ENDIF
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 0
LOSEBASE:気力 += 50

SOURCE:快Ｃ += 20

SOURCE:恐れ += 20
SOURCE:露出 += 20
SOURCE:屈従 += 60
SOURCE:逸脱 += 200
SOURCE:反発 += 400

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	SOURCE:欲情 += 100
ELSEIF PALAM:欲情 < PALAMLV:2
	SOURCE:欲情 += 110
ELSEIF PALAM:欲情 < PALAMLV:3
	SOURCE:欲情 += 120
ELSEIF PALAM:欲情 < PALAMLV:4
	SOURCE:欲情 += 130
ELSE
	SOURCE:欲情 += 140
ENDIF

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES SOURCE:欲情 , 0.80
ELSEIF ABL:マゾっ気 == 1
	TIMES SOURCE:欲情 , 1.00
ELSEIF ABL:マゾっ気 == 2
	TIMES SOURCE:欲情 , 1.30
ELSEIF ABL:マゾっ気 == 3
	TIMES SOURCE:欲情 , 1.60
ELSEIF ABL:マゾっ気 == 4
	TIMES SOURCE:欲情 , 2.00
ELSE
	TIMES SOURCE:欲情 , 3.00
ENDIF

;倒錯的
SIF TALENT:倒錯的
	TIMES SOURCE:欲情 , 2.00
;調教者がサド、サドっ気3以上
SIF TALENT:PLAYER:サド || ABL:PLAYER:10 > 2
	TIMES SOURCE:反発 , 1.20


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;依存度ベクトル
TFLAG:33 = 3

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:隷属 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

RETURN 1



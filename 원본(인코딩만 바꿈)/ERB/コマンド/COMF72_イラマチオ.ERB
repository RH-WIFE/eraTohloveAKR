;-------------------------------------------------
;イラマチオ
;強制奉仕系コマンド
;-------------------------------------------------
@COM72

;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)
	;助手挿入中、前回コマンドが助手挿入のとき＝「サンドイッチ」
	IF PREVCOM == 405 || PREVCOM == 406
		CALL COM_ABLE515
		IF RESULT == 1
			TFLAG:40 = 0
			IF PREVCOM == 405
				TFLAG:41 = 1
			ELSE
				TFLAG:41 = 2
			ENDIF
			TFLAG:49 = 10
			JUMP COM515
		ENDIF
	;前回コマンドが助手に犯させるのとき、「サンドイッチさせる」
	ELSEIF PREVCOM == 270 || PREVCOM == 271
		CALL COM_ABLE520
		IF RESULT == 1
			TFLAG:40 = 0
			IF PREVCOM == 270
				TFLAG:41 = 6
			ELSE
				TFLAG:41 = 7
			ENDIF
			TFLAG:49 = 8
			JUMP COM520
		ENDIF
	;前回コマンドがサンドイッチのとき
	ELSEIF PREVCOM == 515
		;助手が奴隷に挿入してれば再び「サンドイッチ」に
		IF TFLAG:41 == 1 || TFLAG:41 == 2
			CALL COM_ABLE515
			IF RESULT == 1
				TFLAG:40 = 0
				TFLAG:49 = 10
				JUMP COM515
			ENDIF
		;助手が主人に挿入してると「サンドイッチさせる」に
		ELSEIF TFLAG:41 == 6 || TFLAG:41 == 7
			CALL COM_ABLE520
			IF RESULT == 1
				TFLAG:40 = 0
				TFLAG:49 = 10
				JUMP COM520
			ENDIF
		ENDIF
	ENDIF
ENDIF

;何も読まずにこっちに来たときはフラグ解除
TFLAG:40 = 0
TFLAG:41 = 0
TFLAG:49 = 0
TFLAG:42 = 0

PRINTL イラマチオ
STR:0 = イラマチオ
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:28 = 1


;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 1300

CALL SHOOT_GAUGE_CHECK_MT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:射精 += B

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 100

SOURCE:性行動 += 500
SOURCE:痛み += 200
SOURCE:屈従 += 1500
SOURCE:逸脱 += 700
SOURCE:反発 += 700

;上のほうで計算した汚れデータ
SOURCE:不潔 += Y*40 + 100


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 700


;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK(PLAYER,1)

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	TFLAG:703 = 4
	;射精先選択の可・不可
	TFLAG:704 = 1
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

SIF E >= 1
	TFLAG:8 = 1

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
CALL COM_STAIN

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;フェラ経験
PRINTL フェラ経験＋１
EXP:フェラ経験 += 1

;ゲイ経験とか加算
CALL COM_EXP(7)

;愛情経験(マゾっ気5以上限定）
IF (TALENT:恋慕 == 1 || TALENT:親愛 == 1) && ASSIPLAY == 0 && ABL:マゾっ気 >= 5
	EXP:愛情経験 += 1
	PRINTL 愛情経験+１
ENDIF

;依存度ベクトル
TFLAG:33 = 3

;依存度補正値(主人かつ恋慕、淫乱、服従で1。親愛、娼婦、隷属で2）
IF (TALENT:恋慕 || TALENT:淫乱 || TALENT:服従) && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF (TALENT:親愛 || TALENT:娼婦 || TALENT:隷属) && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

;奉仕快楽フラグ
TFLAG:100 = 1


RETURN 1



;-------------------------------------------------
;サンドイッチ
;派生系コマンド
;-------------------------------------------------
@COM515

PRINTL サンドイッチ
STR:0 = サンドイッチ

SELECTCOM = 515
CALL KOJO_MESSAGE_COM


;処女喪失フラグ
SIF (TALENT:処女 || TALENT:再生処女) && (TFLAG:40 == 1 || TFLAG:41 == 1)
	TFLAG:19 |= 1
;主人による処女喪失
SIF TALENT:処女 && (TALENT:恋慕 || TALENT:親愛) && ASSIPLAY == 0 && TFLAG:40 == 1
	TFLAG:20 = 1

;処女喪失フラグ
SIF TALENT:バックバージン && (TFLAG:40 == 2 || TFLAG:41 == 2)
	TFLAG:161 |= 1
;主人による処女喪失
SIF TALENT:バックバージン && (TALENT:恋慕 || TALENT:親愛) && ASSIPLAY == 0 && TFLAG:40 == 2
	TFLAG:163 = 1

;調教者が童貞だった場合の喪失フラグ
SIF TALENT:PLAYER:童貞 && (TFLAG:40 == 1 || TFLAG:40 == 2)
	TFLAG:160 |= 2

;調教者が童貞だった場合の喪失フラグ
SIF TALENT:ASSI:童貞 && (TFLAG:41 == 1 || TFLAG:41 == 2)
	TFLAG:160 |= 3


;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
B = 1000

CALL SHOOT_GAUGE_CHECK_3PM

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:MASTER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:MASTER:射精 += B

;-------------------------------------------------
;射精ゲージチェック（助手）
;-------------------------------------------------
B = 1000

CALL SHOOT_GAUGE_CHECK_3PA

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:ASSI:9 > 0
	B /= 20

SIF PENIS(ASSI)
	BASE:ASSI:射精 += B

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 160
LOSEBASE:気力 += 350

SOURCE:情愛 += 100

;スキンシップ好きだと情愛のソース追加
SIF TALENT:スキンシップ好き
	SOURCE:情愛 += 50

SOURCE:性行動 += 100
SOURCE:中毒充足 += 500
SOURCE:屈従 += 500
SOURCE:逸脱 += 500
SOURCE:反発 += 500

;挿入している部位によって、ソースの数値が変化する
;Vに挿入している場合
IF TFLAG:40 == 1 || TFLAG:41 == 1
	SOURCE:快Ｖ += 200
	SOURCE:性行動 += 100
	SOURCE:痛み += 500
ENDIF
;Aに挿入している場合
IF TFLAG:40 == 2 || TFLAG:41 == 2
	SOURCE:快Ａ += 200
	SOURCE:性行動 += 100
	SOURCE:痛み += 800
ENDIF
;口に挿入している場合
IF TFLAG:49 == 8 || TFLAG:49 == 9 || TFLAG:49 == 10 || TFLAG:49 == 13
	SOURCE:性行動 += 420
	SOURCE:達成感 += 150
ENDIF
;Ｃ愛撫されてる場合
IF TFLAG:40 == 3 || TFLAG:40 == 4 || TFLAG:40 == 5
	SOURCE:快Ｃ += 150
	
	;調教者のTALENT:舌使いをみる
	IF TALENT:MASTER:舌使い
		TIMES SOURCE:快Ｃ , 1.50
	ELSEIF TALENT:MASTER:猫舌
		TIMES SOURCE:快Ｃ , 0.75
	ENDIF
ENDIF


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && (TALENT:恋慕 == 0 && TALENT:親愛 == 0)
	SOURCE:反発 += 1000

;Vに挿入している場合
IF TFLAG:40 == 1 || TFLAG:41 == 1
	;処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:処女 == 1
		SOURCE:痛み += 5500
		SOURCE:反発 += 3000
	ENDIF

	;再生処女だった場合は、苦痛のソースと反発のソースを追加する
	IF TALENT:再生処女 == 1
		SOURCE:痛み += 5500
		SOURCE:反発 += 1000
	ENDIF
	
	;小人体型で調教者が巨根か馬並みの場合、苦痛と恐怖のソースを追加
	IF TALENT:小人体型
		IF (TALENT:MASTER:巨根 && TFLAG:40 == 1) || (TALENT:ASSI:巨根 && TFLAG:41 == 1)
			SOURCE:痛み += 200
			SOURCE:恐れ += 500
		ELSEIF (TALENT:MASTER:馬並み && TFLAG:40 == 1) || (TALENT:ASSI:馬並み && TFLAG:41 == 1)
			SOURCE:痛み += 500
			SOURCE:恐れ += 1000
		ENDIF
	ENDIF

	;調教者が馬並みの場合
	IF (TALENT:MASTER:馬並み && TFLAG:40 == 1) || (TALENT:ASSI:馬並み && TFLAG:41 == 1)
		;Ｖ拡張経験一定以上で中毒充足のソースを追加
		IF EXP:Ｖ拡張経験 >= 20
			SOURCE:中毒充足 += 500
		;Ｖ拡張経験一定以下で苦痛と恐怖のソースを追加
		ELSE
			SOURCE:痛み += 500
			SOURCE:恐れ += 1000
		ENDIF
	ENDIF
	
	;Vへの苦痛は先に処理しておく
	;EXP:Ｖ経験をみる（処女の反感は別途処理済みなので省略）
	IF EXP:Ｖ経験 < EXPLV:1
		TIMES SOURCE:痛み , 2.50
	ELSEIF EXP:Ｖ経験 < EXPLV:2
		TIMES SOURCE:痛み , 0.80
		TIMES SOURCE:反発 , 1.20
	ELSEIF EXP:Ｖ経験 < EXPLV:3
		TIMES SOURCE:痛み , 0.40
	ELSEIF EXP:Ｖ経験 < EXPLV:4
		TIMES SOURCE:痛み , 0.10
	ELSEIF EXP:Ｖ経験 < EXPLV:5
		TIMES SOURCE:痛み , 0.00
	ELSEIF EXP:Ｖ経験 >= EXPLV:5
		TIMES SOURCE:痛み , 0.00
	ENDIF

	;PALAM:潤滑をみる
	IF PALAM:潤滑 < PALAMLV:1
		TIMES SOURCE:痛み , 1.20
		TIMES SOURCE:反発 , 1.50
	ELSEIF PALAM:潤滑 < PALAMLV:2
		TIMES SOURCE:痛み , 0.70
		TIMES SOURCE:反発 , 1.20
	ELSEIF PALAM:潤滑 < PALAMLV:3
		TIMES SOURCE:痛み , 0.40
	ELSEIF PALAM:潤滑 < PALAMLV:4
		TIMES SOURCE:痛み , 0.10
	ELSEIF PALAM:潤滑 >= PALAMLV:4
		TIMES SOURCE:痛み , 0.10
	ENDIF
ENDIF

;Aに挿入している場合
IF TFLAG:40 == 2 || TFLAG:41 == 2
	
	;小人体型で調教者が巨根か馬並みの場合、苦痛と恐怖のソースを追加
	IF TALENT:小人体型
		IF (TALENT:MASTER:巨根 && TFLAG:40 == 2) || (TALENT:ASSI:巨根 && TFLAG:41 == 2)
			SOURCE:痛み += 200
			SOURCE:恐れ += 500
		ELSEIF (TALENT:MASTER:馬並み && TFLAG:40 == 2) || (TALENT:ASSI:馬並み && TFLAG:41 == 2)
			SOURCE:痛み += 500
			SOURCE:恐れ += 1000
		ENDIF
	ENDIF

	;調教者が馬並みの場合
	IF (TALENT:MASTER:馬並み && TFLAG:40 == 2) || (TALENT:ASSI:馬並み && TFLAG:41 == 2)
		;Ａ拡張経験一定以上で中毒充足のソースを追加
		IF EXP:Ａ拡張経験 >= 20
			SOURCE:中毒充足 += 500
		;Ａ拡張経験一定以下で苦痛と恐怖のソースを追加
		ELSE
			SOURCE:痛み += 500
			SOURCE:恐れ += 1000
		ENDIF
	ENDIF
	
	;Aへの苦痛は先に処理しておく
	;EXP:Ａ経験をみる
	IF EXP:Ａ経験 < EXPLV:1
		TIMES SOURCE:痛み , 3.00
		TIMES SOURCE:反発 , 3.00
	ELSEIF EXP:Ａ経験 < EXPLV:2
		TIMES SOURCE:痛み , 1.20
		TIMES SOURCE:反発 , 2.00
	ELSEIF EXP:Ａ経験 < EXPLV:3
		TIMES SOURCE:痛み , 0.80
		TIMES SOURCE:反発 , 1.50
	ELSEIF EXP:Ａ経験 < EXPLV:4
		TIMES SOURCE:痛み , 0.60
		TIMES SOURCE:反発 , 1.20
	ELSEIF EXP:Ａ経験 < EXPLV:5
		TIMES SOURCE:痛み , 0.40
	ELSEIF EXP:Ａ経験 >= EXPLV:5
		TIMES SOURCE:痛み , 0.20
	ENDIF

	;PALAM:潤滑をみる
	IF PALAM:潤滑 < PALAMLV:1
		TIMES SOURCE:痛み , 1.20
		TIMES SOURCE:反発 , 2.00
	ELSEIF PALAM:潤滑 < PALAMLV:2
		TIMES SOURCE:痛み , 1.00
		TIMES SOURCE:反発 , 1.50
	ELSEIF PALAM:潤滑 < PALAMLV:3
		TIMES SOURCE:痛み , 0.60
	ELSEIF PALAM:潤滑 < PALAMLV:4
		TIMES SOURCE:痛み , 0.30
	ELSEIF PALAM:潤滑 >= PALAMLV:4
		TIMES SOURCE:痛み , 0.20
	ENDIF
ENDIF

;セックス中毒を見る
IF ABL:セックス中毒 == 1
	TIMES SOURCE:中毒充足 , 0.00
ELSEIF ABL:セックス中毒 == 2
	TIMES SOURCE:中毒充足 , 0.30
ELSEIF ABL:セックス中毒 == 3
	TIMES SOURCE:中毒充足 , 0.60
ELSEIF ABL:セックス中毒 == 4
	TIMES SOURCE:中毒充足 , 0.90
ELSEIF ABL:セックス中毒 == 5
	TIMES SOURCE:中毒充足 , 1.20
ELSEIF ABL:セックス中毒 == 6
	TIMES SOURCE:中毒充足 , 1.50
ELSEIF ABL:セックス中毒 == 7
	TIMES SOURCE:中毒充足 , 1.80
ELSEIF ABL:セックス中毒 == 8
	TIMES SOURCE:中毒充足 , 2.30
ELSEIF ABL:セックス中毒 == 9
	TIMES SOURCE:中毒充足 , 3.00
ELSEIF ABL:セックス中毒 >= 10
	TIMES SOURCE:中毒充足 , 5.00
ENDIF


;-------------------------------------------------
;射精チェック１（主人）
;-------------------------------------------------
CALL SAMEN_CHECK(MASTER,1)

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	IF TFLAG:40 == 1
		TFLAG:703 = 1
	ELSEIF TFLAG:40 == 2
		TFLAG:703 = 2
	ELSE
		TFLAG:703 = 4
	ENDIF
	;射精先選択の可・不可
	TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;-------------------------------------------------
;射精チェック２（助手）
;-------------------------------------------------
CALL SAMEN_CHECK(ASSI)

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	IF TFLAG:41 == 1
		TFLAG:703 = 1
	ELSEIF TFLAG:41 == 2
		TFLAG:703 = 2
	ELSE
		TFLAG:703 = 4
	ENDIF
	;射精先選択の可・不可
	TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
IF TFLAG:40 == 1 || TFLAG:41 == 1
	;処女の場合、Ｖと膣内に破瓜の血の汚れを付加
	IF TALENT:処女 == 1 || TALENT:再生処女 == 1
		STAIN:膣内 |= 64
		STAIN:ヴァギナ |= 64
	ENDIF
ENDIF

;奴隷のＶ⇔主人のペニスの汚れが移動
IF TFLAG:40 == 1 && PENIS(PLAYER)
	STAIN:膣内 |= STAIN:MASTER:ペニス
	STAIN:MASTER:ペニス |= STAIN:膣内
	STAIN:ヴァギナ |= STAIN:MASTER:ペニス
	STAIN:MASTER:ペニス |= STAIN:ヴァギナ
;奴隷のＡ⇔主人のペニスの汚れが移動
ELSEIF TFLAG:40 == 2 && PENIS(PLAYER)
	STAIN:腸内 |= STAIN:MASTER:ペニス
	STAIN:MASTER:ペニス |= STAIN:腸内
	STAIN:アナル |= STAIN:MASTER:ペニス
	STAIN:MASTER:ペニス |= STAIN:アナル
ENDIF

;奴隷のＶ⇔助手のペニスの汚れが移動
IF TFLAG:41 == 1 && PENIS(PLAYER)
	STAIN:ヴァギナ |= STAIN:ASSI:ペニス
	STAIN:ASSI:ペニス |= STAIN:ヴァギナ
	STAIN:膣内 |= STAIN:ASSI:ペニス
	STAIN:ASSI:ペニス |= STAIN:膣内
;奴隷のＡ⇔助手のペニスの汚れが移動
ELSEIF TFLAG:41 == 2 && PENIS(PLAYER)
	STAIN:アナル |= STAIN:ASSI:ペニス
	STAIN:ASSI:ペニス |= STAIN:アナル
	STAIN:腸内 |= STAIN:ASSI:ペニス
	STAIN:ASSI:ペニス |= STAIN:腸内
ENDIF

;奴隷の口⇔主人のペニスの汚れが移動
IF TFLAG:49 == 8 || TFLAG:49 == 10
	STAIN:口 |= STAIN:MASTER:ペニス
	STAIN:MASTER:ペニス = 2
ELSEIF TFLAG:49 == 9
	STAIN:口 |= STAIN:MASTER:ヴァギナ
	STAIN:MASTER:ヴァギナ = 1
;奴隷の口⇔助手のペニスの汚れが移動
ELSEIF TFLAG:49 == 13
	STAIN:口 |= STAIN:ASSI:ペニス
	STAIN:ASSI:ペニス = 2
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;Vに挿入している場合
IF TFLAG:40 == 1 || TFLAG:41 == 1
	;調教者が馬並みで奴隷が小人体型だった場合、Ｖ拡張経験
	IF TALENT:小人体型 && TALENT:MASTER:馬並み && TFLAG:40 == 1
		IF EXP:Ｖ拡張経験 == 0
			EXP:異常経験 += 1
			PRINTL Ｖ拡張初経験で異常経験＋１
		ENDIF
		EXP:Ｖ拡張経験 += 1
		PRINTL Ｖ拡張経験＋１
	ENDIF
	IF TALENT:小人体型 && TALENT:ASSI:馬並み && TFLAG:41 == 1
		IF EXP:Ｖ拡張経験 == 0
			EXP:異常経験 += 1
			PRINTL Ｖ拡張初経験で異常経験＋１
		ENDIF
		EXP:Ｖ拡張経験 += 1
		PRINTL Ｖ拡張経験＋１
	ENDIF
	EXP:Ｖ経験 += 1
	PRINTL Ｖ経験＋１
ENDIF

;Aに挿入している場合
IF TFLAG:40 == 2 || TFLAG:41 == 2
	;調教者が馬並みで奴隷が小人体型だった場合、Ａ拡張経験
	IF TALENT:小人体型 && TALENT:MASTER:馬並み && TFLAG:40 == 2
		IF EXP:Ａ拡張経験 == 0
			EXP:異常経験 += 1
			PRINTL Ａ拡張初経験で異常経験＋１
		ENDIF
		EXP:Ａ拡張経験 += 1
		PRINTL Ａ拡張経験＋１
	ENDIF
	IF TALENT:小人体型 && TALENT:ASSI:馬並み && TFLAG:41 == 2
		IF EXP:Ａ拡張経験 == 0
			EXP:異常経験 += 1
			PRINTL Ａ拡張初経験で異常経験＋１
		ENDIF
		EXP:Ａ拡張経験 += 1
		PRINTL Ａ拡張経験＋１
	ENDIF
	EXP:Ａ経験 += 2
	PRINTL Ａ経験＋２
ENDIF

;挿入してるか、されてれば性交経験
IF TFLAG:40 == 1 || TFLAG:40 == 2 || TFLAG:41 == 1 || TFLAG:41 == 2
	EXP:性交経験 += 1
	PRINTFORML %CALLNAME%の性交経験＋1
ENDIF
IF TFLAG:41 == 1 || TFLAG:41 == 2
	EXP:ASSI:性交経験 += 1
	PRINTFORML %CALLNAME:ASSI%の性交経験＋1
	
	EXP:ASSI:挿入経験 += 1
ENDIF
IF TFLAG:40 == 1 || TFLAG:40 == 2
	EXP:MASTER:性交経験 += 1
	PRINTFORML %CALLNAME:PLAYER%の性交経験＋1
	
	EXP:MASTER:挿入経験 += 1
ENDIF

;奉仕系
IF TFLAG:49 == 8 || TFLAG:49 == 9 || TFLAG:49 == 10 || TFLAG:49 == 13
	;奉仕快楽フラグ
	TFLAG:100 = 1
	
	;フェラ経験
	IF TFLAG:49 == 8 || TFLAG:49 == 10 || TFLAG:49 == 13
		EXP:フェラ経験 += 2
		PRINTFORML %EXPNAME:22%＋２
	ENDIF

	;奴隷が猫舌持ちの場合は、奴隷に嗜虐快楽経験を追加
	IF TALENT:TARGET:猫舌
		EXP:嗜虐快楽経験 += 1
		PRINTFORML %EXPNAME:32%＋１
	ENDIF
ENDIF

;ゲイ経験とか加算
CALL COM_EXP(7)

;依存度ベクトル
TFLAG:33 = 1


RETURN 1



;-------------------------------------------------
;排便
;スカ系コマンド
;-------------------------------------------------
@COM183

PRINTL 排便させる
STR:0 = 排便させる

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;実行判定操作
CALL KOJO_DENIAL_PLUS

;合計を表示(45以上で実行)
PRINT  = 
PRINTV A

;難易度上昇
V = 45
;ズボン系の下衣類を着てる
SIF TEQUIP:下着（下） && TEQUIP:下着（下） != 5
	V += 5
SIF BOTTOMS(TARGET) >= 3
	V += 5

;お風呂場
SIF TEQUIP:お風呂場
	V -= 2

SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

;実行不可口上/実行判定操作
SIF A < V
	CALL KOJO_DENIAL

WAIT

;実行できない
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 5
LOSEBASE:気力 += 100

SOURCE:快Ａ += 20

SOURCE:痛み += 200

SOURCE:露出 += 1200
SOURCE:屈従 += 800
SOURCE:不潔 += 500
SOURCE:逸脱 += 1000
SOURCE:反発 += 1000

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 700

;場所によって逸脱のソースが上下
;野外、録画中
SIF TEQUIP:野外プレイ || TEQUIP:ビデオカメラ
	TIMES SOURCE:逸脱 , 1.50
;鏡の前（羞恥プレイ）
IF TEQUIP:羞恥プレイ
	TIMES SOURCE:逸脱 , 2.00
;風呂場
ELSEIF TEQUIP:お風呂場
	TIMES SOURCE:逸脱 , 0.70
ENDIF

;排便時の基本的なソース変動はTRACHECKに入ってるので呼びだして終わり。
CALL DEFECATION_CHECK2


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;ゲイ経験とか加算
CALL COM_EXP(1)

;依存度ベクトル
TFLAG:33 = 5

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:隷属 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF


RETURN 1





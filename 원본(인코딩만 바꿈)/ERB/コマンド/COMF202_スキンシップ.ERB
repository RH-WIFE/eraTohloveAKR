;-------------------------------------------------
;スキンシップ
;コミュニケーション系コマンド
;-------------------------------------------------
@COM202

PRINTL スキンシップ
STR:0 = スキンシップ
CALL KOJO_MESSAGE_COM


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 10

IF TALENT:親愛 || TALENT:娼婦 || TALENT:隷属
	SOURCE:恭順 += 1000
ELSEIF TALENT:恋慕 || TALENT:淫乱 || TALENT:服従
	SOURCE:恭順 += 500
ELSE
	SOURCE:恭順 += 200
ENDIF

SOURCE:情愛 += 500
SOURCE:性行動 += 500
SOURCE:逸脱 += 100

;スキンシップ好きか幼児持ちだと情愛ソース追加
IF TALENT:スキンシップ好き || TALENT:幼児／幼児退行 || TALENT:幼稚
	TIMES SOURCE:情愛 , 1.50
;スキンシップ苦手だと情愛のソース減少、鬱屈のソース追加
ELSEIF TALENT:スキンシップ苦手
	TIMES SOURCE:情愛 , 0.75
	SOURCE:鬱屈 += 200
ENDIF



;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;愛情経験
IF (TALENT:恋慕 == 1 || TALENT:親愛 == 1) && ASSIPLAY == 0
	PRINTS EXPNAME:23
	PRINTL +1
	EXP:愛情経験 += 1
ENDIF

;依存度ベクトル
TFLAG:33 = 4

;依存度補正値(恋慕で1、親愛で2）
IF TALENT:恋慕 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:親愛 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF


RETURN 1




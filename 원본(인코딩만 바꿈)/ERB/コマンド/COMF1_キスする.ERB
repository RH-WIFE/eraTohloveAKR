;-------------------------------------------------
;キスする
;基本系コマンド
;-------------------------------------------------
@COM1

PRINTL キスする
STR:0 = キスする

CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:28 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 50

CALL SHOOT_GAUGE_CHECK_MT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:射精 += B


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 0
LOSEBASE:気力 += 30

SOURCE:情愛 += 100

;スキンシップ好きだと情愛のソース追加
SIF TALENT:スキンシップ好き
	SOURCE:情愛 += 50
;恋慕系陥落済みだと情愛のソース追加
SIF TALENT:恋慕 || TALENT:親愛
	TIMES SOURCE:情愛 , 1.50

SOURCE:性行動 += 10
SOURCE:屈従 += 10
SOURCE:逸脱 += 10
SOURCE:反発 += 10

;上のほうで計算した汚れデータ
SOURCE:不潔 += Y*20 + 10


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 10


;気絶から回復させたときは体力気力ボーナス
IF TALENT:気絶
	BASE:体力 += 100
	BASE:気力 += 100
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
CALL COM_STAIN


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;ゲイ経験とか加算
CALL COM_EXP(3)

;愛情経験
IF (TALENT:恋慕 == 1 || TALENT:親愛 == 1) && ASSIPLAY == 0
	EXP:愛情経験 += 1
	PRINTL EXP:愛情経験+１
ENDIF

;奴隷が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
IF TALENT:TARGET:猫舌
	EXP:嗜虐快楽経験 += 1
	PRINTL 嗜虐快楽経験+１
ENDIF

;奉仕快楽フラグ
TFLAG:100 = 1

;依存度ベクトル
TFLAG:33 = 2

;依存度補正値(恋慕で1、親愛で2）
IF TALENT:恋慕 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:親愛 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF


RETURN 1




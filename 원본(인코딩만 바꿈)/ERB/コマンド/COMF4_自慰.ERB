;-------------------------------------------------
;自慰
;自慰系コマンド
;-------------------------------------------------
@COM4

;バイブ、ビデオ撮影の時に内容変化
;ビデオ撮影中なら公開オナニー
;野外プレイ中なら野外オナニー
;シャワー使用中ならシャワーオナニー
;バイブ装備でバイブオナニー
;オナホ装備でオナホオナニー
LOCALS = 

SIF TEQUIP:ビデオカメラ
	LOCALS = 公開
SIF TEQUIP:野外プレイ
	LOCALS = 野外

IF TEQUIP:バイブ
	LOCALS = %LOCALS%バイブ
	TFLAG:56 = 1
ELSEIF TEQUIP:ローター挿入
	LOCALS = %LOCALS%ローター
	TFLAG:56 = 1
ELSEIF TEQUIP:電動オナホール
	LOCALS = %LOCALS%オナホ
	TFLAG:56 = 0
ELSEIF TEQUIP:シャワー
	LOCALS =  %LOCALS%シャワー
	TFLAG:56 = 0
ELSE
	;ペニスがあって、元がオトコならペニスを優先して使う
	IF PENIS(TARGET) && MALE2(TARGET)
		TFLAG:56 = 0
	;ヴァギナが使える場合は指を入れる
	ELSEIF (!TALENT:処女 && VAGINA(TARGET)) || EXP:Ｖ経験 >= 20
		TFLAG:56 = 1
	ELSE
		TFLAG:56 = 0
	ENDIF
ENDIF

IF TEQUIP:ビデオカメラ || TEQUIP:野外プレイ || TEQUIP:バイブ || TEQUIP:ローター挿入 || TEQUIP:電動オナホール || TEQUIP:シャワー
	LOCALS = %LOCALS%オナニー
ELSE
	LOCALS = %LOCALS%自慰
ENDIF

STR:0 = %LOCALS%
PRINTFORML %STR:0%

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;実行判定操作
CALL KOJO_DENIAL_PLUS

;合計を表示(33以上で実行)
PRINT  = 
PRINTV A

;難易度上昇
;公開で+10、バイブで+5、シャワーで＋3
V = 33
SIF TEQUIP:ビデオカメラ
	V += 10
SIF TEQUIP:野外プレイ
	V += 10

SIF TEQUIP:バイブ
	V += 5
SIF TEQUIP:ローター挿入
	V += 3
SIF TEQUIP:電動オナホール
	V += 3
SIF TEQUIP:シャワー
	V += 3

SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

;実行不可口上/実行判定操作
SIF A < V
	CALL KOJO_DENIAL

WAIT

;実行できない
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM


;実行者が奴隷であるフラグ
TFLAG:28 = 1

;ストッキングびりびりフラグ
IF TEQUIP:特殊２ == 5 || TEQUIP:特殊２ == 6
	TFLAG:55 = 1
ENDIF


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 50

;基本の快感
SOURCE:快Ｃ += 80
SIF TFLAG:56 == 1
	SOURCE:快Ｖ += 80

IF TEQUIP:バイブ || TEQUIP:ローター挿入
	TIMES SOURCE:快Ｃ , 0.70

	SIF TEQUIP:バイブ
		TIMES SOURCE:快Ｖ , 1.50
	SIF TEQUIP:ローター挿入
		TIMES SOURCE:快Ｖ , 1.20

	SIF TEQUIP:バイブ
		SOURCE:痛み += 50
	SIF TEQUIP:ローター挿入
		SOURCE:痛み += 20

	;Vへの苦痛は先に処理しておく
	;EXP:Ｖ経験をみる（処女の反感は別途処理済みなので省略）
	IF EXP:Ｖ経験 < EXPLV:1
		TIMES SOURCE:痛み , 2.50
	ELSEIF EXP:Ｖ経験 < EXPLV:2
		TIMES SOURCE:痛み , 0.80
		TIMES SOURCE:反発 , 1.20
	ELSEIF EXP:Ｖ経験 < EXPLV:3
		TIMES SOURCE:痛み , 0.40
	ELSEIF EXP:Ｖ経験 < EXPLV:4
		TIMES SOURCE:痛み , 0.10
	ELSEIF EXP:Ｖ経験 < EXPLV:5
		TIMES SOURCE:痛み , 0.00
	ELSEIF EXP:Ｖ経験 >= EXPLV:5
		TIMES SOURCE:痛み , 0.00
	ENDIF
	
	;PALAM:潤滑をみる
	IF PALAM:潤滑 < PALAMLV:1
		TIMES SOURCE:痛み , 1.20
		TIMES SOURCE:反発 , 1.50
	ELSEIF PALAM:潤滑 < PALAMLV:2
		TIMES SOURCE:痛み , 0.70
		TIMES SOURCE:反発 , 1.20
	ELSEIF PALAM:潤滑 < PALAMLV:3
		TIMES SOURCE:痛み , 0.40
	ELSEIF PALAM:潤滑 < PALAMLV:4
		TIMES SOURCE:痛み , 0.10
	ELSEIF PALAM:潤滑 >= PALAMLV:4
		TIMES SOURCE:痛み , 0.10
	ENDIF

	SOURCE:屈従 += 100
	SOURCE:反発 += 100
ELSEIF TEQUIP:電動オナホール
	TIMES SOURCE:快Ｃ , 1.50
ELSEIF TEQUIP:シャワー
	TIMES SOURCE:快Ｃ , 1.20
ENDIF

SOURCE:性行動 += 100
SOURCE:恐れ += 200
SOURCE:中毒充足 += 200
SOURCE:露出 += 400
SOURCE:屈従 += 500
SOURCE:逸脱 += 300
SOURCE:反発 += 250

;ビデオor野外
IF TEQUIP:ビデオカメラ || TEQUIP:野外プレイ
	SOURCE:液体 += 50
	SOURCE:欲情 += 100
	SOURCE:反発 += 200
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;中毒による中毒充足のソースは先に処理しておく
;自慰中毒を見る
IF ABL:自慰中毒 == 1
	TIMES SOURCE:快Ｃ , 1.00
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:中毒充足 , 0.00
ELSEIF ABL:自慰中毒 == 2
	TIMES SOURCE:快Ｃ , 1.10
	TIMES SOURCE:快Ｖ , 1.10
	TIMES SOURCE:中毒充足 , 0.30
ELSEIF ABL:自慰中毒 == 3
	TIMES SOURCE:快Ｃ , 1.20
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:中毒充足 , 0.60
ELSEIF ABL:自慰中毒 == 4
	TIMES SOURCE:快Ｃ , 1.30
	TIMES SOURCE:快Ｖ , 1.30
	TIMES SOURCE:中毒充足 , 0.90
ELSEIF ABL:自慰中毒 == 5
	TIMES SOURCE:快Ｃ , 1.50
	TIMES SOURCE:快Ｖ , 1.50
	TIMES SOURCE:中毒充足 , 1.20
ELSEIF ABL:自慰中毒 == 6
	TIMES SOURCE:快Ｃ , 1.70
	TIMES SOURCE:快Ｖ , 1.70
	TIMES SOURCE:中毒充足 , 1.50
ELSEIF ABL:自慰中毒 == 7
	TIMES SOURCE:快Ｃ , 2.00
	TIMES SOURCE:快Ｖ , 2.00
	TIMES SOURCE:中毒充足 , 1.80
ELSEIF ABL:自慰中毒 == 8
	TIMES SOURCE:快Ｃ , 2.30
	TIMES SOURCE:快Ｖ , 2.30
	TIMES SOURCE:中毒充足 , 2.30
ELSEIF ABL:自慰中毒 == 9
	TIMES SOURCE:快Ｃ , 2.50
	TIMES SOURCE:快Ｖ , 2.50
	TIMES SOURCE:中毒充足 , 3.00
ELSEIF ABL:自慰中毒 >= 10
	TIMES SOURCE:快Ｃ , 3.50
	TIMES SOURCE:快Ｖ , 3.50
	TIMES SOURCE:中毒充足 , 5.00
ENDIF

;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 400

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の手（手袋）⇔奴隷のＣorＶor膣内
;バイブ、ローター、オナホールを使っているときは汚れはどこにも移動しない
IF TEQUIP:バイブ || TEQUIP:ローター挿入 || TEQUIP:電動オナホール
;シャワーオナニーの場合汚れをリセット、潤滑更に半分。
ELSEIF TEQUIP:シャワー
	STAIN:手 = 0
	STAIN:ペニス = 2
	STAIN:ヴァギナ = 1
	STAIN:膣内 = 1
	STAIN:アナル = 8
	STAIN:足 = 0
	STAIN:手袋 = 256
	STAIN:全身上着 = 256
	STAIN:下衣 = 256
	STAIN:下着（下） = 256
	STAIN:足装飾 = 256
	STAIN:靴 = 256
	PALAM:潤滑 /= 2
ELSE
	CALL COM_STAIN
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
IF TFLAG:56 == 1
	EXP:Ｖ経験 += 1
	PRINTL Ｖ経験＋１
ENDIF

;ビデオ撮影時は経験にプラス
IF TEQUIP:ビデオカメラ
	EXP:自慰経験 += 2
	PRINTL 自慰経験＋２
	EXP:調教自慰経験 += 2
	PRINTL 調教自慰経験＋２
	
	IF !EXP:公開オナニー && !TALENT:淫乱 && !TALENT:娼婦
		EXP:異常経験 += 1
		PRINTL 公開オナニー初経験で異常経験＋１
	ENDIF
	EXP:公開オナニー += 1
	PRINTL 公開オナニー経験＋１
ELSE
	EXP:自慰経験 += 1
	PRINTL 自慰経験＋１
	EXP:調教自慰経験 += 1
	PRINTL 調教自慰経験＋１
ENDIF

;撮影時の調教内容チェック
IF TEQUIP:バイブ || TEQUIP:ローター挿入
	TFLAG:90 = 991
ELSEIF TEQUIP:シャワー && !PENIS(TARGET)
	TFLAG:90 = 994
ELSE
	TFLAG:90 = 0
ENDIF

;ゲイ経験とか加算
CALL COM_EXP(3)

;依存度ベクトル
TFLAG:33 = 3

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:隷属 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF


TFLAG:56 = 0

RETURN 1



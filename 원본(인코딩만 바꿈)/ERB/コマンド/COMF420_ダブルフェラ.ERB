;-------------------------------------------------
;ダブルフェラ
;主人×（助手＋奴隷）系コマンド
;-------------------------------------------------
@COM420

PRINTL ダブルフェラ
STR:0 = ダブルフェラ

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;実行判定操作
CALL KOJO_DENIAL_PLUS

;合計を表示(34以上で実行)
PRINT  = 
PRINTV A

V = 34
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

;実行不可口上/実行判定操作
SIF A < V
	CALL KOJO_DENIAL

WAIT

;実行できない
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM


;実行者が奴隷であるフラグ
TFLAG:28 = 1


;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 1300

CALL SHOOT_GAUGE_CHECK_3PM

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:MASTER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:MASTER:射精 += B

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 80

SOURCE:情愛 += 100

;スキンシップ好きだと情愛のソース追加
SIF TALENT:スキンシップ好き
	SOURCE:情愛 += 50

SOURCE:性行動 += 400
SOURCE:達成感 += 200
SOURCE:屈従 += 600
SOURCE:逸脱 += 200
SOURCE:反発 += 200

;上のほうで計算した汚れデータ
SOURCE:不潔 += Y*40 + 100

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 400


;-------------------------------------------------
;射精チェック
;-------------------------------------------------
;射精するのは主人なので専用の関数を呼ぶ
CALL SAMEN_CHECK(MASTER,1)

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	TFLAG:703 = 4
	;射精先選択の可・不可
	TFLAG:704 = 1
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;調教者のＰの汚れが奴隷、助手の口に移動
STAIN:口 |= STAIN:MASTER:ペニス
STAIN:ASSI:口 |= STAIN:MASTER:ペニス

STAIN:MASTER:ペニス = 2

SIF E >= 1
	TFLAG:8 = 2

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
PRINTFORML %EXPNAME:22%＋１
EXP:フェラ経験 += 1

;レズ経験
A = 7
B = NO:MASTER + 1099
C = NO:ASSI + 1099
D = 10

IF MALE(TARGET) == 0 && MALE(MASTER) == 0 && MALE(ASSI) == 0
	PRINTFORML %EXPNAME:40%+{D}
	EXP:レズ経験 += D
ELSEIF MALE(TARGET) == 0 && MALE(MASTER) == 0
	PRINTFORML %EXPNAME:40%+{A}
	EXP:レズ経験 += A
ELSEIF MALE(TARGET) == 0 && MALE(ASSI) == 0
	PRINTFORML %EXPNAME:40%+{A}
	EXP:レズ経験 += A
ELSEIF MALE(TARGET) && MALE(MASTER) && MALE(ASSI)
	PRINTFORML %EXPNAME:41%+{D}
	EXP:ゲイ経験 += D
ELSEIF MALE(TARGET) && MALE(MASTER)
	PRINTFORML %EXPNAME:41%+{A}
	EXP:ゲイ経験 += A
ELSEIF MALE(TARGET) && MALE(ASSI)
	PRINTFORML %EXPNAME:41%+{A}
	EXP:ゲイ経験 += A
ENDIF
;調教者を記録
CFLAG:B += A
CFLAG:C += A

;奴隷が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
IF TALENT:猫舌
	EXP:嗜虐快楽経験 += 1
	PRINTFORML %EXPNAME:32%＋１
ENDIF

;依存度ベクトル
P = PLAYER
IF RELATION:P > 100
	TFLAG:33 = 2
ELSEIF RELATION:P < 100
	TFLAG:33 = 3
ELSE
	TFLAG:33 = 1
ENDIF

;依存度補正値(相性100↑かつ淫乱で1、娼婦で2）
P = PLAYER
IF TALENT:淫乱 && RELATION:P > 100
	TFLAG:30 += 1
ELSEIF TALENT:娼婦 && RELATION:P > 100
	TFLAG:30 += 2
ENDIF

TFLAG:100 = 1


RETURN 1



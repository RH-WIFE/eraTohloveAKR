;-------------------------------------------------
;助手を犯させる
;奴隷×助手系コマンド
;-------------------------------------------------
@COM430

;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)
	;両方挿入してると「助手を二輪挿し」
	IF PREVCOM == 440
		CALL COM_ABLE524
		IF RESULT == 1
			TFLAG:40 = 11
			TFLAG:41 = 0
			TFLAG:49 = 11
			JUMP COM524
		ENDIF
	;主人が助手Ａ挿入してると「助手をサンドイッチ（二穴挿入）」
	ELSEIF PREVCOM == 441
		CALL COM_ABLE521
		IF RESULT == 1
			TFLAG:40 = 12
			TFLAG:41 = 0
			TFLAG:49 = 11
			JUMP COM521
		ENDIF
	;助手が主人に挿入してると「連結（主人←助手←奴隷）」
	ELSEIF PREVCOM == 270 || PREVCOM == 271
		CALL COM_ABLE519
		IF RESULT == 1
			TFLAG:40 = 0
			IF PREVCOM == 270
				TFLAG:41 = 6
			ELSE
				TFLAG:41 = 7
			ENDIF
			TFLAG:49 = 11
			JUMP COM519
		ENDIF
	;主人が奴隷に挿入してると「連結（助手←奴隷←主人）」
	ELSEIF PREVCOM == 101 || PREVCOM == 111 || PREVCOM == 105 || PREVCOM == 115
		CALL COM_ABLE519
		IF RESULT == 1
			IF PREVCOM == 101 || PREVCOM == 105
				TFLAG:40 = 1
			ELSE
				TFLAG:40 = 2
			ENDIF
			TFLAG:41 = 0
			TFLAG:49 = 11
			JUMP COM519
		ENDIF
	;前回コマンドが「助手を二輪挿し」「助手をサンドイッチ」「連結」
	ELSEIF PREVCOM == 524 || PREVCOM == 521 || PREVCOM == 519
		;両方挿入してると「助手を二輪挿し」
		IF TFLAG:40 == 11
			CALL COM_ABLE524
			IF RESULT == 1
				TFLAG:41 = 0
				TFLAG:49 = 11
				JUMP COM524
			ENDIF
		;主人が助手Ａ挿入してると「助手をサンドイッチ（二穴挿入）」
		ELSEIF TFLAG:40 == 12
			CALL COM_ABLE521
			IF RESULT == 1
				TFLAG:41 = 0
				TFLAG:49 = 11
				JUMP COM521
			ENDIF
		;主人が奴隷に挿入してると「連結（助手←奴隷←主人）」
		ELSEIF TFLAG:40 == 1 || TFLAG:40 == 2
			CALL COM_ABLE519
			IF RESULT == 1
				TFLAG:41 = 0
				TFLAG:49 = 11
				JUMP COM519
			ENDIF
		;助手が主人に挿入してると「連結（主人←助手←奴隷）」
		ELSEIF TFLAG:41 == 6 || TFLAG:41 == 7
			CALL COM_ABLE519
			IF RESULT == 1
				TFLAG:40 = 0
				TFLAG:49 = 11
				JUMP COM519
			ENDIF
		ENDIF
	ENDIF
ENDIF

;何も読まずにこっちに来たときはフラグ解除
TFLAG:40 = 0
TFLAG:41 = 0
TFLAG:49 = 0
TFLAG:42 = 0

PRINTL 助手を犯させる
STR:0 = 助手を犯させる

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;実行判定操作
CALL KOJO_DENIAL_PLUS

;合計を表示(40以上で実行)
PRINT  = 
PRINTV A

V = 40
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

;実行不可口上/実行判定操作
SIF A < V
	CALL KOJO_DENIAL

WAIT

;実行できない
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM

;ストッキング脱がせる
IF TEQUIP:特殊２ == 5 || TEQUIP:特殊２ == 6
	TEQUIP:特殊２ = 0
	TEQUIP:特殊２ = 0
ENDIF

;処女喪失フラグ
SIF TALENT:ASSI:処女 || TALENT:ASSI:再生処女
	TFLAG:19 |= 3

;調教者が童貞だった場合の喪失フラグ
SIF TALENT:童貞
	TFLAG:160 |= 1

;挿入個所判定フラグ
TFLAG:705 = 0

;-------------------------------------------------
;射精ゲージチェック（助手）
;-------------------------------------------------
B = 1000

CALL SHOOT_GAUGE_CHECK_AT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:ASSI:9 > 0
	B /= 20

SIF PENIS(ASSI)
	BASE:ASSI:射精 += B

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 100
LOSEBASE:気力 += 80

SIF PENIS(TARGET)
	SOURCE:快Ｃ += 300

SOURCE:性行動 += 500
SOURCE:達成感 += 400
SOURCE:屈従 += 700
SOURCE:逸脱 += 500
SOURCE:反発 += 500


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500


;-------------------------------------------------
;射精チェック（助手）
;-------------------------------------------------
CALL SAMEN_CHECK(ASSI)

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	TFLAG:703 = 6
	;射精先選択の可・不可
	TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF


;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のＰ⇔調教者のＶの汚れが移動
;ペニスバンドを使う場合、汚れは移動しない
IF PENIS(TARGET)
	CALL COM_STAIN
ENDIF


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;動くか分からないけど相性で異常経験を加算
IF TALENT:ASSI:処女
	IF RELATION:(NO:ASSI) > 100
		EXP:異常経験 += 1
		PRINTFORML %CALLNAME%に%EXPNAME:50%＋１
	ENDIF

	IF RELATION:ASSI:(NO:TARGET) > 100
		EXP:ASSI:異常経験 += 1
		PRINTFORML %CALLNAME:ASSI%に%EXPNAME:50%＋１
	ENDIF
ENDIF

;助手にＶ経験
EXP:ASSI:Ｖ経験 += 1

;助手が小人体型で奴隷が馬並みだった場合
IF TALENT:ASSI:小人体型 && TALENT:馬並み
	IF EXP:ASSI:Ｖ拡張経験 == 0
		EXP:異常経験 += 1
		PRINTL Ｖ拡張初経験で異常経験＋１
		EXP:ASSI:異常経験 += 1
		PRINTFORML %CALLNAME:ASSI%の異常経験＋１
	ENDIF
	EXP:ASSI:Ｖ拡張経験 += 1
	PRINTFORML %CALLNAME:ASSI%のＶ拡張経験＋１
ENDIF

;性交経験
EXP:性交経験 += 1
EXP:ASSI:性交経験 += 1
PRINTS EXPNAME:4
PRINTL +1

;挿入経験
EXP:挿入経験 += 1
PRINTS EXPNAME:17
PRINTL +1

;ゲイ経験とか加算
CALL COM_EXP(10)

;依存度ベクトル
P = PLAYER
IF RELATION:P > 100
	TFLAG:33 = 2
ELSEIF RELATION:P < 100
	TFLAG:33 = 3
ELSE
	TFLAG:33 = 1
ENDIF

;依存度補正値(相性100↑かつ淫乱で1、娼婦で2）
P = PLAYER
IF TALENT:淫乱 && RELATION:P > 100
	TFLAG:30 += 1
ELSEIF TALENT:娼婦 && RELATION:P > 100
	TFLAG:30 += 2
ENDIF


RETURN 1





;-------------------------------------------------
;肉便器
;スカ系コマンド
;-------------------------------------------------
@COM181

PRINTL 肉便器
STR:0 = 肉便器

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;拘束されてなくて、体格差がある（主人が小さい）ときのみ実行判定を行う
CALL BUILD
IF !TEQUIP:縄 && S < 0
	;すべての命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER

	;実行判定操作
	CALL KOJO_DENIAL_PLUS

	;合計を表示(32以上で実行)
	PRINT  = 
	PRINTV A

	V = 32
	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V

	;実行不可口上/実行判定操作
	SIF A < V
		CALL KOJO_DENIAL

	WAIT

	;実行できない
	SIF A < V
		RETURN 0
ENDIF

CALL KOJO_MESSAGE_COM

;ストッキングびりびりフラグ
IF TEQUIP:特殊２ == 5 || TEQUIP:特殊２ == 6
	TFLAG:55 = 1
ENDIF

;調教対象がバックバージンだった場合の喪失フラグ
SIF TALENT:バックバージン
	TFLAG:161 |= 1
;主人のセックスによるバックバージン喪失
SIF TALENT:バックバージン && (TALENT:恋慕 || TALENT:親愛) && ASSIPLAY == 0
	TFLAG:163 = 1

;調教者が童貞だった場合の喪失フラグ
SIF TALENT:PLAYER:童貞
	TFLAG:160 |= 2


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 100

SOURCE:快Ａ += 200

SOURCE:情愛 += 50
;スキンシップ好きだと情愛のソース追加
SIF TALENT:スキンシップ好き
	SOURCE:情愛 += 50
;服従系陥落済みだと情愛のソース追加
SIF TALENT:服従 || TALENT:隷属
	TIMES SOURCE:情愛 , 1.50

SOURCE:痛み += 350
SOURCE:恐れ += 200

SOURCE:露出 += 200
SOURCE:屈従 += 400
SOURCE:不潔 += 700
SOURCE:逸脱 += 500
SOURCE:反発 += 500


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

;Aへの苦痛は先に処理しておく
;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES SOURCE:痛み , 3.00
	TIMES SOURCE:反発 , 3.00
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES SOURCE:痛み , 1.20
	TIMES SOURCE:反発 , 2.00
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES SOURCE:痛み , 0.80
	TIMES SOURCE:反発 , 1.50
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES SOURCE:痛み , 0.60
	TIMES SOURCE:反発 , 1.20
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES SOURCE:痛み , 0.40
ELSEIF EXP:Ａ経験 >= EXPLV:5
	TIMES SOURCE:痛み , 0.20
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:痛み , 1.20
	TIMES SOURCE:反発 , 2.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:痛み , 1.00
	TIMES SOURCE:反発 , 1.50
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:痛み , 0.60
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:痛み , 0.30
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:痛み , 0.20
ENDIF


;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
CALL COM_STAIN

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;Ａ経験
EXP:Ａ経験 += 5
PRINTL Ａ経験＋5

;スカ経験を伴う調教による異常経験
IF EXP:スカ経験 == 0
	EXP:異常経験 += 1
	PRINTL スカトロ初経験で異常経験＋１
ENDIF
EXP:スカ経験 += 1
PRINTL スカ経験＋１

;調教者にスカ経験
IF EXP:PLAYER:スカ経験 == 0
	EXP:PLAYER:異常経験 += 1
	PRINTFORML スカトロ初経験で%CALLNAME:PLAYER%に異常経験＋１
ENDIF
EXP:PLAYER:スカ経験 += 1
PRINTFORML %CALLNAME:PLAYER%にスカ経験＋１

;ゲイ経験とか加算
CALL COM_EXP(1)

;便意ゲージに追加
IF BASE:便意 >= 60
	BASE:便意 += 30
ELSE
	BASE:便意 += 60
ENDIF

;依存度ベクトル
TFLAG:33 = 5

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:隷属 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF


RETURN 1




;-------------------------------------------------
;騎乗位乳房自慰
;派生系コマンド
;-------------------------------------------------
@COM509

LOCALS = 
SIF TEQUIP:ビデオカメラ
	LOCALS = 公開
SIF TEQUIP:野外プレイ
	LOCALS =  %LOCALS%野外

;騎乗位からの派生の場合
IF PREVCOM == 106 || TFLAG:705 == 0
	TFLAG:705 = 0
	LOCALS = %LOCALS%騎乗乳房自慰
;騎乗位アナルからの派生の場合(直接コマンドを指定した場合もこっち)
ELSEIF PREVCOM == 116 || TFLAG:705 == 1
	TFLAG:705 = 1
	LOCALS = %LOCALS%アナル騎乗乳房自慰
ENDIF

STR:0 = %LOCALS%
PRINTFORML %STR:0%

SELECTCOM = 509
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:28 = 1

;ストッキングびりびりフラグ
IF TEQUIP:特殊２ == 5 || TEQUIP:特殊２ == 6
	TFLAG:55 = 1
ENDIF


;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 600

CALL SHOOT_GAUGE_CHECK_MT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:射精 += B

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 120
LOSEBASE:気力 += 80

SOURCE:快Ｂ += 80
SIF TEQUIP:ニプルキャップ || TEQUIP:乳首クリップ || TEQUIP:搾乳機
	TIMES SOURCE:快Ｂ , 1.50

SOURCE:情愛 += 400

;スキンシップ好きだと情愛のソース追加
SIF TALENT:スキンシップ好き
	SOURCE:情愛 += 50
SOURCE:性行動 += 300
SOURCE:達成感 += 300
SOURCE:中毒充足 += 400
SOURCE:露出 += 200
SOURCE:逸脱 += 700
SOURCE:反発 += 900
;上のほうで計算した汚れデータ
SOURCE:不潔 += Y*10 + 60
;騎乗位からの派生の場合
IF TFLAG:705 == 0
	SOURCE:快Ｖ = 350
	SOURCE:快Ｃ = 80
	SOURCE:痛み = 500
;騎乗位アナルからの派生の場合
ELSE
	SOURCE:快Ａ = 350
	SOURCE:痛み = 800
	SOURCE:逸脱 += 100
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 900

;騎乗位からの派生の場合
IF TFLAG:705 == 0
	;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
	SIF TALENT:貞操観念 && !TALENT:恋慕 && !TALENT:親愛
		SOURCE:反発 += 1000
	
	;Vへの苦痛は先に処理しておく
	;EXP:Ｖ経験をみる（処女の反感は別途処理済みなので省略）
	IF EXP:Ｖ経験 < EXPLV:1
		TIMES SOURCE:痛み , 2.50
	ELSEIF EXP:Ｖ経験 < EXPLV:2
		TIMES SOURCE:痛み , 0.80
		TIMES SOURCE:反発 , 1.20
	ELSEIF EXP:Ｖ経験 < EXPLV:3
		TIMES SOURCE:痛み , 0.40
	ELSEIF EXP:Ｖ経験 < EXPLV:4
		TIMES SOURCE:痛み , 0.10
	ELSEIF EXP:Ｖ経験 < EXPLV:5
		TIMES SOURCE:痛み , 0.00
	ELSEIF EXP:Ｖ経験 >= EXPLV:5
		TIMES SOURCE:痛み , 0.00
	ENDIF

	;PALAM:潤滑をみる
	IF PALAM:潤滑 < PALAMLV:1
		TIMES SOURCE:痛み , 1.20
		TIMES SOURCE:反発 , 1.50
	ELSEIF PALAM:潤滑 < PALAMLV:2
		TIMES SOURCE:痛み , 0.70
		TIMES SOURCE:反発 , 1.20
	ELSEIF PALAM:潤滑 < PALAMLV:3
		TIMES SOURCE:痛み , 0.40
	ELSEIF PALAM:潤滑 < PALAMLV:4
		TIMES SOURCE:痛み , 0.10
	ELSEIF PALAM:潤滑 >= PALAMLV:4
		TIMES SOURCE:痛み , 0.10
	ENDIF
;騎乗位アナルからの派生の場合
ELSE
	;Aへの苦痛は先に処理しておく
	;EXP:Ａ経験をみる
	IF EXP:Ａ経験 < EXPLV:1
		TIMES SOURCE:痛み , 3.00
		TIMES SOURCE:反発 , 3.00
	ELSEIF EXP:Ａ経験 < EXPLV:2
		TIMES SOURCE:痛み , 1.20
		TIMES SOURCE:反発 , 2.00
	ELSEIF EXP:Ａ経験 < EXPLV:3
		TIMES SOURCE:痛み , 0.80
		TIMES SOURCE:反発 , 1.50
	ELSEIF EXP:Ａ経験 < EXPLV:4
		TIMES SOURCE:痛み , 0.60
		TIMES SOURCE:反発 , 1.20
	ELSEIF EXP:Ａ経験 < EXPLV:5
		TIMES SOURCE:痛み , 0.40
	ELSEIF EXP:Ａ経験 >= EXPLV:5
		TIMES SOURCE:痛み , 0.20
	ENDIF

	;PALAM:潤滑をみる
	IF PALAM:潤滑 < PALAMLV:1
		TIMES SOURCE:痛み , 1.20
		TIMES SOURCE:反発 , 2.00
	ELSEIF PALAM:潤滑 < PALAMLV:2
		TIMES SOURCE:痛み , 1.00
		TIMES SOURCE:反発 , 1.50
	ELSEIF PALAM:潤滑 < PALAMLV:3
		TIMES SOURCE:痛み , 0.60
	ELSEIF PALAM:潤滑 < PALAMLV:4
		TIMES SOURCE:痛み , 0.30
	ELSEIF PALAM:潤滑 >= PALAMLV:4
		TIMES SOURCE:痛み , 0.20
	ENDIF
ENDIF

;中毒による中毒充足のソースは先に処理しておく
;セックス中毒を見る
IF ABL:セックス中毒 == 1
	TIMES SOURCE:中毒充足 , 0.00
ELSEIF ABL:セックス中毒 == 2
	TIMES SOURCE:中毒充足 , 0.30
ELSEIF ABL:セックス中毒 == 3
	TIMES SOURCE:中毒充足 , 0.60
ELSEIF ABL:セックス中毒 == 4
	TIMES SOURCE:中毒充足 , 0.90
ELSEIF ABL:セックス中毒 == 5
	TIMES SOURCE:中毒充足 , 1.20
ELSEIF ABL:セックス中毒 == 6
	TIMES SOURCE:中毒充足 , 1.50
ELSEIF ABL:セックス中毒 == 7
	TIMES SOURCE:中毒充足 , 1.80
ELSEIF ABL:セックス中毒 == 8
	TIMES SOURCE:中毒充足 , 2.30
ELSEIF ABL:セックス中毒 == 9
	TIMES SOURCE:中毒充足 , 3.00
ELSEIF ABL:セックス中毒 >= 10
	TIMES SOURCE:中毒充足 , 5.00
ENDIF

;自慰中毒を見る
IF ABL:自慰中毒 == 1
	TIMES SOURCE:中毒充足 , 0.00
ELSEIF ABL:自慰中毒 == 2
	TIMES SOURCE:中毒充足 , 0.30
ELSEIF ABL:自慰中毒 == 3
	TIMES SOURCE:中毒充足 , 0.60
ELSEIF ABL:自慰中毒 == 4
	TIMES SOURCE:中毒充足 , 0.90
ELSEIF ABL:自慰中毒 == 5
	TIMES SOURCE:中毒充足 , 1.20
ELSEIF ABL:自慰中毒 == 6
	TIMES SOURCE:中毒充足 , 1.50
ELSEIF ABL:自慰中毒 == 7
	TIMES SOURCE:中毒充足 , 1.80
ELSEIF ABL:自慰中毒 == 8
	TIMES SOURCE:中毒充足 , 2.30
ELSEIF ABL:自慰中毒 == 9
	TIMES SOURCE:中毒充足 , 3.00
ELSEIF ABL:自慰中毒 >= 10
	TIMES SOURCE:中毒充足 , 5.00
ENDIF

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK(PLAYER,1)

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	IF TFLAG:705 == 0
		TFLAG:703 = 1
	ELSE
		TFLAG:703 = 2
	ENDIF
	;射精先選択の可・不可
	TFLAG:704 = 1
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の手（手袋）⇔奴隷のＢの汚れが移動
;道具がついてると汚れは移動しない
IF TEQUIP:ニプルキャップ || TEQUIP:乳首クリップ || TEQUIP:搾乳機
ELSE
	;手袋をしているとき
	IF TEQUIP:特殊１ == 1
		STAIN:手袋 |= STAIN:胸
		STAIN:胸 |= STAIN:手袋
	ELSE
		;奴隷の指⇔奴隷のＢの汚れが移動
		STAIN:手 |= STAIN:胸
		STAIN:胸 |= STAIN:手
	ENDIF
ENDIF

;ペニスバンドを使う場合、汚れは移動しない
IF !PENIS(PLAYER)

;騎乗位からの派生の場合
;奴隷のＶと膣内⇔調教者のＣの汚れが移動
ELSEIF TFLAG:705 == 0
	STAIN:膣内 |= STAIN:PLAYER:ペニス
	STAIN:PLAYER:ペニス |= STAIN:膣内
	STAIN:ヴァギナ |= STAIN:PLAYER:ペニス
	STAIN:PLAYER:ペニス |= STAIN:ヴァギナ

;騎乗位アナルからの派生の場合
;奴隷のＡと腸内⇔調教者のＣの汚れが移動
ELSE
	STAIN:腸内 |= STAIN:PLAYER:ペニス
	STAIN:PLAYER:ペニス |= STAIN:腸内
	STAIN:アナル |= STAIN:PLAYER:ペニス
	STAIN:PLAYER:ペニス |= STAIN:アナル
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;ビデオ撮影時は経験にプラス
IF TEQUIP:ビデオカメラ
	EXP:自慰経験 += 2
	PRINTL 自慰経験＋２
	EXP:調教自慰経験 += 2
	PRINTL 調教自慰経験＋２
	
	IF !EXP:公開オナニー && !TALENT:淫乱 && !TALENT:娼婦
		EXP:異常経験 += 1
		PRINTL 公開オナニー初経験で異常経験＋１
	ENDIF
	EXP:公開オナニー += 1
	PRINTL 公開オナニー経験＋１
ELSE
	EXP:自慰経験 += 1
	PRINTL 自慰経験＋１
	EXP:調教自慰経験 += 1
	PRINTL 調教自慰経験＋１
ENDIF

;騎乗位からの派生の場合
;奴隷のＶと膣内⇔調教者のＣの汚れが移動
IF TFLAG:705 == 0
	;V経験
	EXP:Ｖ経験 += 3
	PRINTL Ｖ経験＋３
	
	;調教者が馬並みで奴隷が小人体型だった場合、Ｖ拡張経験
	IF TALENT:小人体型 && TALENT:PLAYER:馬並み
		IF EXP:Ｖ拡張経験 == 0
			EXP:異常経験 += 1
			PRINTL Ｖ拡張初経験で異常経験＋１
		ENDIF
		EXP:Ｖ拡張経験 += 1
		PRINTL Ｖ拡張経験＋１
	ENDIF
;騎乗位アナルからの派生の場合
ELSE
	;A経験
	EXP:Ａ経験 += 3
	PRINTL Ａ経験＋３
	
	;調教者が馬並みで奴隷が小人体型だった場合、Ａ拡張経験
	IF TALENT:小人体型 && TALENT:PLAYER:馬並み
		IF EXP:Ａ拡張経験 == 0
			EXP:異常経験 += 1
			PRINTL Ａ拡張初経験で異常経験＋１
		ENDIF
		EXP:Ａ拡張経験 += 1
		PRINTL Ａ拡張経験＋１
	ENDIF
ENDIF

;性交経験
EXP:PLAYER:性交経験 += 1
EXP:性交経験 += 1
PRINTL 性交経験＋１

;挿入経験
EXP:PLAYER:挿入経験 += 1

;ゲイ経験とか加算
CALL COM_EXP(7)

;依存度ベクトル
TFLAG:33 = 1

;依存度補正値(恋慕で2、親愛で4）
IF TALENT:恋慕 && ASSIPLAY == 0
	TFLAG:30 += 2
ELSEIF TALENT:親愛 && ASSIPLAY == 0
	TFLAG:30 += 4
ENDIF

;奉仕快楽フラグ
TFLAG:100 = 1

RETURN 1



;-------------------------------------------------
;フェラチオ
;奉仕系コマンド
;-------------------------------------------------
@COM71

;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)
	;助手口辱中、前回コマンドが助手口辱か二本フェラ＝「二本フェラ」
	IF PREVCOM == 407 || PREVCOM == 516
		CALL COM_ABLE516
		IF RESULT == 1
			TFLAG:40 = 0
			TFLAG:41 = 0
			TFLAG:49 = 20
			JUMP COM516
		ENDIF
	;助手挿入中、前回コマンドが助手挿入のとき＝「サンドイッチ」
	ELSEIF PREVCOM == 405 || PREVCOM == 406
		CALL COM_ABLE515
		IF RESULT == 1
			TFLAG:40 = 0
			IF PREVCOM == 405
				TFLAG:41 = 1
			ELSE
				TFLAG:41 = 2
			ENDIF
			TFLAG:49 = 8
			JUMP COM515
		ENDIF
	;前回コマンドが助手に犯させるのとき、「サンドイッチさせる」
	ELSEIF PREVCOM == 270 || PREVCOM == 271
		CALL COM_ABLE520
		IF RESULT == 1
			TFLAG:40 = 0
			IF PREVCOM == 270
				TFLAG:41 = 6
			ELSE
				TFLAG:41 = 7
			ENDIF
			TFLAG:49 = 8
			JUMP COM520
		ENDIF
	;前回コマンドがサンドイッチのとき
	ELSEIF PREVCOM == 515
		;助手が奴隷に挿入してれば再び「サンドイッチ」に
		IF TFLAG:41 == 1 || TFLAG:41 == 2
			CALL COM_ABLE515
			IF RESULT == 1
				TFLAG:40 = 0
				TFLAG:49 = 8
				JUMP COM515
			ENDIF
		;助手が主人に挿入してると「サンドイッチさせる」に
		ELSEIF TFLAG:41 == 6 || TFLAG:41 == 7
			CALL COM_ABLE520
			IF RESULT == 1
				TFLAG:40 = 0
				TFLAG:49 = 8
				JUMP COM520
			ENDIF
		ENDIF
	;前回の調教がフェラする、クンニ、シックスナインのとき、シックスナインに
	ELSEIF PREVCOM == 31 || PREVCOM == 40 || PREVCOM == 503
		CALL COM_ABLE503
		SIF RESULT == 1
			JUMP COM503
	ENDIF
ENDIF

;何も読まずにこっちに来たときはフラグ解除
TFLAG:40 = 0
TFLAG:41 = 0
TFLAG:49 = 0
TFLAG:42 = 0

PRINTL フェラチオ
STR:0 = フェラチオ


;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;実行判定操作
CALL KOJO_DENIAL_PLUS

;合計を表示(24以上で実行)
PRINT  = 
PRINTV A

V = 24
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

;実行不可口上/実行判定操作
SIF A < V
	CALL KOJO_DENIAL

WAIT

;実行できない
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM


;実行者が奴隷であるフラグ
TFLAG:28 = 1


;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 950

CALL SHOOT_GAUGE_CHECK_MT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:射精 += B

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 50

SOURCE:情愛 += 100

;スキンシップ好きだと情愛のソース追加
SIF TALENT:スキンシップ好き
	SOURCE:情愛 += 50

SOURCE:性行動 += 300
SOURCE:達成感 += 150
SOURCE:屈従 += 700
SOURCE:逸脱 += 200
SOURCE:反発 += 200

;上のほうで計算した汚れデータ
SOURCE:不潔 += Y*10 + 60


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 400

;調教者が馬並みの場合
IF TALENT:PLAYER:馬並み
	;恐怖と屈従のソースを追加
	SOURCE:恐れ += 200
	SOURCE:屈従 += 200
ENDIF


;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK(PLAYER,1)

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	TFLAG:703 = 4
	;射精先選択の可・不可
	TFLAG:704 = 1
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

SIF E >= 1
	TFLAG:8 = 1

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
CALL COM_STAIN

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;フェラ経験
PRINTL フェラ経験＋１
EXP:フェラ経験 += 1

;ゲイ経験とか加算
CALL COM_EXP(7)

;愛情経験
IF (TALENT:恋慕 == 1 || TALENT:親愛 == 1) && ASSIPLAY == 0
	EXP:愛情経験 += 1
	PRINTL 愛情経験+１
ENDIF

;奴隷が猫舌持ちの場合は、奴隷に嗜虐快楽経験を追加
IF TALENT:TARGET:猫舌
	EXP:嗜虐快楽経験 += 1
	PRINTL 嗜虐経験＋１
ENDIF

;依存度ベクトル
TFLAG:33 = 1

;依存度補正値(主人かつ恋慕、淫乱、服従で1。親愛、娼婦、隷属で2）
IF (TALENT:恋慕 || TALENT:淫乱 || TALENT:服従) && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF (TALENT:親愛 || TALENT:娼婦 || TALENT:隷属) && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

;奉仕快楽フラグ
TFLAG:100 = 1

RETURN 1




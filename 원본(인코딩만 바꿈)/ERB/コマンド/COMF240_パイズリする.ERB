;-------------------------------------------------
;パイズリする
;奉仕する系コマンド
;-------------------------------------------------
@COM240

;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)
	;前回の調教がパイズリ、６９パイズリのとき、６９パイズリに
	IF PREVCOM == 73 || PREVCOM == 504
		CALL COM_ABLE504
		SIF RESULT == 1
			JUMP COM504
	ENDIF
ENDIF

PRINTL パイズリする
STR:0 = パイズリする
CALL KOJO_MESSAGE_COM

;ストッキングびりびりフラグ
IF TEQUIP:特殊２ == 5 || TEQUIP:特殊２ == 6
	TFLAG:55 = 1
ENDIF


;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 50

CALL SHOOT_GAUGE_CHECK_MT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:射精 += B


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 5
LOSEBASE:気力 += 30

SOURCE:快Ｃ += 80

SOURCE:情愛 += 100

;スキンシップ好きだと情愛のソース追加
SIF TALENT:スキンシップ好き
	SOURCE:情愛 += 50
;プレイヤーが巨乳もしくは爆乳だと情愛のソース追加
SIF TALENT:PLAYER:巨乳 || TALENT:PLAYER:爆乳
	TIMES SOURCE:情愛 , 1.20


SOURCE:欲情 += 50
SOURCE:露出 += 30
SOURCE:逸脱 += 20
SOURCE:反発 += 20

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 50


;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
CALL COM_STAIN


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;ゲイ経験とか加算
CALL COM_EXP(2)

;依存度ベクトル
TFLAG:33 = 1

;依存度補正値(淫乱で1、娼婦で2）
IF TALENT:淫乱 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:娼婦 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF


RETURN 1



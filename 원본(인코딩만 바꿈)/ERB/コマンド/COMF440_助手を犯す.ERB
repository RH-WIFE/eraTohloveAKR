;-------------------------------------------------
;助手を犯す
;主人×助手系コマンド
;-------------------------------------------------
@COM440

;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)
	;両方挿入してると「助手を二輪挿し」
	IF PREVCOM == 430
		CALL COM_ABLE524
		IF RESULT == 1
			TFLAG:40 = 11
			TFLAG:41 = 0
			TFLAG:49 = 11
			JUMP COM524
		ENDIF
	;奴隷が助手を犯しているとき「助手をサンドイッチ（二穴挿入）」
	ELSEIF PREVCOM == 431
		CALL COM_ABLE521
		IF RESULT == 1
			TFLAG:40 = 12
			TFLAG:41 = 0
			TFLAG:49 = 11
			JUMP COM521
		ENDIF
	;口辱中は「助手をサンドイッチ」
	ELSEIF PREVCOM == 407
		CALL COM_ABLE521
		IF RESULT == 1
			TFLAG:40 = 11
			TFLAG:41 = 0
			TFLAG:49 = 13
			JUMP COM521
		ENDIF
	;助手が奴隷を犯す場合「連結（奴隷←助手←主人）」
	ELSEIF PREVCOM == 405 || PREVCOM == 406
		CALL COM_ABLE519
		IF RESULT == 1
			IF PREVCOM == 405
				TFLAG:41 = 1
			ELSE
				TFLAG:41 = 2
			ENDIF
			TFLAG:40 = 11
			TFLAG:49 = 0
			JUMP COM519
		ENDIF
	;奴隷が主人を犯す場合「連結（助手←主人←奴隷）」
	ELSEIF PREVCOM == 261 || PREVCOM == 264
		CALL COM_ABLE519
		IF RESULT == 1
			IF PREVCOM == 261
				TFLAG:49 = 6
			ELSE
				TFLAG:49 = 7
			ENDIF
			TFLAG:40 = 11
			TFLAG:41 = 0
			JUMP COM519
		ENDIF
	;前回コマンドが「助手を二輪挿し」「助手をサンドイッチ」「連結」
	ELSEIF PREVCOM == 524 || PREVCOM == 521 || PREVCOM == 519
		;両方挿入してると「助手を二輪挿し」
		IF TFLAG:49 == 11
			CALL COM_ABLE524
			IF RESULT == 1
				TFLAG:40 = 11
				TFLAG:41 = 0
				JUMP COM524
			ENDIF
		;奴隷が助手を犯しているとき「助手をサンドイッチ（二穴挿入）」
		ELSEIF TFLAG:49 == 12
			CALL COM_ABLE521
			IF RESULT == 1
				TFLAG:40 = 11
				TFLAG:41 = 0
				JUMP COM521
			ENDIF
		;口辱中、口辱命令は「助手をサンドイッチ」
		ELSEIF TFLAG:49 == 13
			CALL COM_ABLE521
			IF RESULT == 1
				TFLAG:40 = 11
				TFLAG:41 = 0
				JUMP COM521
			ENDIF
		;奴隷が主人を犯す場合「連結（助手←主人←奴隷）」
		ELSEIF TFLAG:49 == 6 || TFLAG:49 == 7
			CALL COM_ABLE519
			IF RESULT == 1
				TFLAG:40 = 11
				TFLAG:41 = 0
				JUMP COM519
			ENDIF
		;助手が奴隷を犯す場合「連結（奴隷←助手←主人）」
		ELSEIF TFLAG:41 == 1 || TFLAG:41 == 2
			CALL COM_ABLE519
			IF RESULT == 1
				TFLAG:40 = 11
				TFLAG:49 = 0
				JUMP COM519
			ENDIF
		ENDIF
	ENDIF
ENDIF

;何も読まずにこっちに来たときはフラグ解除
TFLAG:40 = 0
TFLAG:41 = 0
TFLAG:49 = 0
TFLAG:42 = 0

PRINTL 助手を犯す
STR:0 = 助手を犯す

CALL KOJO_MESSAGE_COM

;処女喪失フラグ
SIF TALENT:ASSI:処女 || TALENT:ASSI:再生処女
	TFLAG:19 |= 3

;調教者が童貞だった場合の喪失フラグ
SIF TALENT:PLAYER:童貞
	TFLAG:160 |= 2

;挿入個所判定フラグ
TFLAG:705 = 0


;-------------------------------------------------
;射精ゲージチェック(主人)
;-------------------------------------------------
B = 1350

CALL SHOOT_GAUGE_CHECK_MA1

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:MASTER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:MASTER:射精 += B

;-------------------------------------------------
;射精ゲージチェック（助手）
;-------------------------------------------------
B = 1000

CALL SHOOT_GAUGE_CHECK_MA2

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:ASSI:9 > 0
	B /= 20

SIF PENIS(ASSI)
	BASE:ASSI:射精 += B

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 0
LOSEBASE:気力 += 80

SOURCE:液体 += 100
SOURCE:屈従 += 1500
SOURCE:逸脱 += 700
SOURCE:反発 += 700

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 700

;助手と奴隷の相性によって、全体のソースが変化
T = NO:TARGET
A = NO:ASSI
SOURCE:液体 *= RELATION:A
SOURCE:屈従 *= RELATION:A
SOURCE:鬱屈 *= RELATION:A
SOURCE:逸脱 *= RELATION:A
SOURCE:反発 *= RELATION:A
SOURCE:液体 /= 100
SOURCE:屈従 /= 100
SOURCE:鬱屈 /= 100
SOURCE:逸脱 /= 100
SOURCE:反発 /= 100


;-------------------------------------------------
;射精チェック（主人）
;-------------------------------------------------
CALL SAMEN_CHECK(MASTER,1)
IF E >= 1
	;射精先タイプの設定
	TFLAG:703 = 1
	;射精先選択の可・不可
	TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;助手を犯すで射精したフラグ
SIF E == 2
	TFLAG:7 = 2
SIF E == 1
	TFLAG:7 = 1

;-------------------------------------------------
;射精チェック（助手）
;-------------------------------------------------
CALL SAMEN_CHECK(ASSI)

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	TFLAG:703 = 6
	;射精先選択の可・不可
	TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF


;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のＰ⇔調教者のＶの汚れが移動
;ペニスバンドを使う場合、汚れは移動しない
IF PENIS(TARGET)
	CALL COM_STAIN
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;助手が処女の場合、助手と奴隷に異常経験付加。ただし一度きり
IF TALENT:ASSI:処女 == 1 && TFLAG:705 == 0
	PRINTFORML ＜%CALLNAME:ASSI%が処女喪失＞
	TALENT:ASSI:処女 = 0
	EXP:ASSI:異常経験 += 1
	PRINTFORML %CALLNAME:ASSI%に%EXPNAME:50%＋１
	;異常な状態での処女喪失で異常経験を加算する
	;処女喪失の相手がにょたかちびの自分
		;にょたちびサーチ
		R = NO:ASSI
		L = NO:MASTER
		CALL GET_JIBUN
		IF A
			PRINTFORML %CALLNAME:MASTER%相手に処女喪失で%EXPNAME:50%+1
			EXP:ASSI:異常経験 += 1
		ENDIF
	;処女喪失を撮影された
	IF TEQUIP:ビデオカメラ
		PRINTFORML ビデオ撮影中に処女喪失で%EXPNAME:50%+1
		EXP:ASSI:異常経験 += 1
	ENDIF
	IF (CFLAG:99 & 2) == 0
		IF RELATION:(NO:ASSI) > 100
			EXP:異常経験 += 1
			PRINTFORML %CALLNAME%に%EXPNAME:50%＋１
		ENDIF
		CFLAG:99 |= 2
	ENDIF
	;へたくしょん用面識判定の更新
	G = (2099 + NO:MASTER)
	H = (2099 + NO:ASSI)
	CFLAG:MASTER:H |= 64
	CFLAG:ASSI:G |= 128
	;処女喪失相手の記録
	CSTR:ASSI:10 = %CALLNAME:MASTER%
ENDIF

;助手のＶ経験
EXP:ASSI:Ｖ経験 += 1

;助手が小人体型で調教者が馬並みだった場合
IF TALENT:ASSI:小人体型 && TALENT:MASTER:馬並み
	IF EXP:ASSI:Ｖ拡張経験 == 0
		EXP:異常経験 += 1
		PRINTL Ｖ拡張を強要されて異常経験＋１
		EXP:ASSI:異常経験 += 1
		PRINTFORML Ｖ拡張%CALLNAME:ASSI%の異常経験＋１
	ENDIF
	EXP:ASSI:Ｖ拡張経験 += 1
	PRINTFORML %CALLNAME:ASSI%のＶ拡張経験＋１
ENDIF

;性交経験
EXP:ASSI:性交経験 += 1
EXP:MASTER:性交経験 += 1
PRINTS EXPNAME:4
PRINTL +1

;挿入経験
EXP:MASTER:挿入経験 += 1

;ゲイ経験とか加算
CALL COM_EXP(10)

;依存度ベクトル
TFLAG:33 = 5

T = NO:TARGET
A = NO:ASSI
;依存度補正値(助手との相性100↑かつ淫乱で1、娼婦で2）
IF TALENT:淫乱 && RELATION:A > 100
	TFLAG:30 += 1
ELSEIF TALENT:娼婦 && RELATION:A > 100
	TFLAG:30 += 2
ENDIF


RETURN 1


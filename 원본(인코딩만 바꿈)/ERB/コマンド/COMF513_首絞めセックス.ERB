;-------------------------------------------------
;首絞めセックス
;派生系コマンド
;-------------------------------------------------
@COM513

PRINTL 首絞めセックス
STR:0 = 首絞めセックス
SELECTCOM = 513
CALL KOJO_MESSAGE_COM


;ストッキングびりびりフラグ
IF TEQUIP:特殊２ == 5 || TEQUIP:特殊２ == 6
	TFLAG:55 = 1
ENDIF

;処女喪失フラグ
SIF TALENT:処女 || TALENT:再生処女
	TFLAG:19 |= 1
;主人による処女喪失
SIF TALENT:処女 && (TALENT:恋慕 || TALENT:親愛) && ASSIPLAY == 0
	TFLAG:20 = 1

;調教者が童貞だった場合の喪失フラグ
SIF TALENT:PLAYER:童貞
	TFLAG:160 |= 2


;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 1500

CALL SHOOT_GAUGE_CHECK_MT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:射精 += B

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 150
LOSEBASE:気力 += 120

SOURCE:快Ｖ += 350

SOURCE:情愛 += 300

;スキンシップ好きだと情愛のソース追加
SIF TALENT:スキンシップ好き
	SOURCE:情愛 += 50
SOURCE:痛み += 1500
SOURCE:恐れ += 2000
SOURCE:中毒充足 += 200
SOURCE:露出 += 50
SOURCE:逸脱 += 600
SOURCE:反発 += 800

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300
;苦痛のパラメータによる苦痛のソース増加は先に処理しておく
IF PALAM:苦痛 < PALAMLV:1
	TIMES SOURCE:痛み , 1.00
ELSEIF PALAM:苦痛 < PALAMLV:2
	TIMES SOURCE:痛み , 1.50
ELSEIF PALAM:苦痛 < PALAMLV:3
	TIMES SOURCE:痛み , 2.00
ELSEIF PALAM:苦痛 < PALAMLV:4
	TIMES SOURCE:痛み , 2.50
ELSEIF PALAM:苦痛 >= PALAMLV:4
	TIMES SOURCE:痛み , 3.00
ENDIF

;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && (TALENT:恋慕 == 0 && TALENT:親愛 == 0)
	SOURCE:反発 += 1000

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女 == 1
	SOURCE:痛み += 2000
	SOURCE:反発 += 3000
ENDIF

;再生処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:再生処女 == 1
	SOURCE:痛み += 500
	SOURCE:反発 += 1000
ENDIF

;小人体型で調教者が巨根か馬並みの場合、苦痛と恐怖のソースを追加
IF TALENT:小人体型
	IF TALENT:PLAYER:巨根
		SOURCE:痛み += 200
		SOURCE:恐れ += 500
	ELSEIF TALENT:PLAYER:馬並み
		SOURCE:痛み += 500
		SOURCE:恐れ += 1000
	ENDIF
ENDIF

;調教者が馬並みの場合
IF TALENT:PLAYER:馬並み
	;Ｖ拡張経験一定以上で中毒充足のソースを追加
	IF EXP:Ｖ拡張経験 >= 20
		SOURCE:中毒充足 += 500
	;Ｖ拡張経験一定以下で苦痛と恐怖のソースを追加
	ELSE
		SOURCE:痛み += 500
		SOURCE:恐れ += 1000
	ENDIF
ENDIF

;Vへの苦痛は先に処理しておく
;EXP:Ｖ経験をみる（処女の反感は別途処理済みなので省略）
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:痛み , 2.50
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:痛み , 0.80
	TIMES SOURCE:反発 , 1.20
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:痛み , 0.40
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:痛み , 0.10
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:痛み , 0.00
ELSEIF EXP:Ｖ経験 >= EXPLV:5
	TIMES SOURCE:痛み , 0.00
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:痛み , 1.20
	TIMES SOURCE:反発 , 1.50
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:痛み , 0.70
	TIMES SOURCE:反発 , 1.20
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:痛み , 0.40
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:痛み , 0.10
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:痛み , 0.10
ENDIF

;中毒による中毒充足のソースは先に処理しておく
;セックス中毒を見る
IF ABL:セックス中毒 == 1
	TIMES SOURCE:中毒充足 , 0.00
ELSEIF ABL:セックス中毒 == 2
	TIMES SOURCE:中毒充足 , 0.30
ELSEIF ABL:セックス中毒 == 3
	TIMES SOURCE:中毒充足 , 0.60
ELSEIF ABL:セックス中毒 == 4
	TIMES SOURCE:中毒充足 , 0.90
ELSEIF ABL:セックス中毒 == 5
	TIMES SOURCE:中毒充足 , 1.20
ELSEIF ABL:セックス中毒 == 6
	TIMES SOURCE:中毒充足 , 1.50
ELSEIF ABL:セックス中毒 == 7
	TIMES SOURCE:中毒充足 , 1.80
ELSEIF ABL:セックス中毒 == 8
	TIMES SOURCE:中毒充足 , 2.30
ELSEIF ABL:セックス中毒 == 9
	TIMES SOURCE:中毒充足 , 3.00
ELSEIF ABL:セックス中毒 >= 10
	TIMES SOURCE:中毒充足 , 5.00
ENDIF

;マゾっ気を見る
IF ABL:マゾっ気 == 0
	TIMES SOURCE:屈従 , 0.80
	SOURCE:中毒充足 += 0
	SOURCE:欲情 += 0
ELSEIF ABL:マゾっ気 == 1
	TIMES SOURCE:屈従 , 1.00
	SOURCE:中毒充足 += 20
	SOURCE:欲情 += 30
ELSEIF ABL:マゾっ気 == 2
	TIMES SOURCE:屈従 , 1.20
	SOURCE:中毒充足 += 40
	SOURCE:欲情 += 70
ELSEIF ABL:マゾっ気 == 3
	TIMES SOURCE:屈従 , 1.40
	SOURCE:中毒充足 += 70
	SOURCE:欲情 += 120
ELSEIF ABL:マゾっ気 == 4
	TIMES SOURCE:屈従 , 1.50
	SOURCE:中毒充足 += 110
	SOURCE:欲情 += 180
ELSEIF ABL:マゾっ気 == 5
	TIMES SOURCE:屈従 , 1.70
	SOURCE:中毒充足 += 150
	SOURCE:欲情 += 250
ELSEIF ABL:マゾっ気 == 6
	TIMES SOURCE:屈従 , 1.90
	SOURCE:中毒充足 += 200
	SOURCE:欲情 += 300
ELSEIF ABL:マゾっ気 == 7
	TIMES SOURCE:屈従 , 2.10
	SOURCE:中毒充足 += 250
	SOURCE:欲情 += 350
ELSEIF ABL:マゾっ気 == 8
	TIMES SOURCE:屈従 , 2.30
	SOURCE:中毒充足 += 300
	SOURCE:欲情 += 400
ELSEIF ABL:マゾっ気 == 9
	TIMES SOURCE:屈従 , 2.50
	SOURCE:中毒充足 += 350
	SOURCE:欲情 += 450
ELSEIF ABL:マゾっ気 >= 10
	TIMES SOURCE:屈従 , 3.00
	SOURCE:中毒充足 += 500
	SOURCE:欲情 += 500
ENDIF

;マゾを見る
IF TALENT:マゾ
	TIMES SOURCE:欲情 , 1.50
	TIMES SOURCE:反発 , 0.80
ENDIF

;体格差により恐怖のソース追加
CALL BUILD
IF S == 3
	TIMES SOURCE:恐れ , 2.00
ELSEIF S == 2
	TIMES SOURCE:恐れ , 1.50
ELSEIF S == 1
	TIMES SOURCE:恐れ , 1.20
ELSEIF S == 0
	TIMES SOURCE:恐れ , 1.00
ELSEIF S == -3
	TIMES SOURCE:恐れ , 0.75
ELSE
	TIMES SOURCE:恐れ , 0.90
ENDIF


;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK(PLAYER,1)

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	TFLAG:703 = 1
	;射精先選択の可・不可
	TFLAG:704 = 1
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;処女の場合、Ｖと膣内に破瓜の血の汚れを付加
IF TALENT:処女 == 1 || TALENT:再生処女 == 1
	STAIN:膣内 |= 64
	STAIN:ヴァギナ |= 64
ENDIF

;奴隷のＶと膣内⇔調教者のＣの汚れが移動
;ペニスバンドを使う場合、汚れは移動しない
IF !PENIS(PLAYER)
ELSE
	STAIN:膣内 |= STAIN:PLAYER:ペニス
	STAIN:PLAYER:ペニス |= STAIN:膣内
	STAIN:ヴァギナ |= STAIN:PLAYER:ペニス
	STAIN:PLAYER:ペニス |= STAIN:ヴァギナ
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;V経験
EXP:Ｖ経験 += 2
PRINTL Ｖ経験＋２

;調教者が馬並みで奴隷が小人体型だった場合、Ｖ拡張経験
IF TALENT:小人体型 && TALENT:PLAYER:馬並み
	IF EXP:Ｖ拡張経験 == 0
		EXP:異常経験 += 1
		PRINTL Ｖ拡張初経験で異常経験＋１
	ENDIF
	EXP:Ｖ拡張経験 += 1
	PRINTL Ｖ拡張経験＋１
ENDIF

;性交経験
EXP:PLAYER:性交経験 += 1
EXP:性交経験 += 1
PRINTS EXPNAME:4
PRINTL +1

;挿入経験
EXP:PLAYER:挿入経験 += 1

;ゲイ経験とか加算
CALL COM_EXP(4)

;愛情経験
IF TALENT:恋慕 == 1 && ASSIPLAY == 0
	PRINTS EXPNAME:23
	PRINTL +2
	EXP:愛情経験 += 2
ELSEIF TALENT:親愛 == 1 && ASSIPLAY == 0
	PRINTS EXPNAME:23
	PRINTL +4
	EXP:愛情経験 += 4
ENDIF

;依存度ベクトル
TFLAG:33 = 3

;依存度補正値(恋慕で1、親愛で2）
IF TALENT:恋慕 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:親愛 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

RETURN 1



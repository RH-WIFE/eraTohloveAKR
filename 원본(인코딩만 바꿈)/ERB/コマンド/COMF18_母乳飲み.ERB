;-------------------------------------------------
;母乳飲み
;Ｂ系調教コマンド
;-------------------------------------------------
@COM18

;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回が手淫で奴隷が母乳体質の場合、授乳手コキに飛ぶ
IF PREVCOM == 70 && TALENT:母乳体質
	CALL COM_ABLE528
	SIF RESULT == 1
		JUMP COM528
ENDIF


PRINTL 母乳飲み
STR:0 = 母乳飲み
CALL KOJO_MESSAGE_COM



;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 80

SOURCE:快Ｂ += 100

SOURCE:情愛 += 500

;スキンシップ好きだと情愛のソース追加
SIF TALENT:スキンシップ好き
	SOURCE:情愛 += 50

;幼児／幼児退行もしくは小人体型だと情愛のソース追加
SIF TALENT:幼児／幼児退行 || TALENT:小人体型
	TIMES SOURCE:情愛 , 1.20

;母性持ちだと情愛のソース追加
SIF TALENT:母性
	TIMES SOURCE:情愛 , 2.00

SOURCE:達成感 += 1000
SOURCE:恭順 += 2000
SOURCE:屈従 += 1800
SOURCE:逸脱 += 100
SOURCE:反発 += 100


;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
CALL COM_STAIN



;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;ゲイ経験とか加算
CALL COM_EXP(5)

;愛情経験
IF (TALENT:恋慕 == 1 || TALENT:親愛 == 1) && ASSIPLAY == 0
	EXP:愛情経験 += 1
	PRINTL 愛情経験+１
ENDIF

;依存度ベクトル
TFLAG:33 = 4

;依存度補正値(主人かつ恋慕で1、親愛で2）
IF TALENT:恋慕 && ASSIPLAY == 0
	TFLAG:30 += 2
ELSEIF TALENT:親愛 && ASSIPLAY == 0
	TFLAG:30 += 4
ENDIF


RETURN 1




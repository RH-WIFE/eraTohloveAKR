;====================================================================
@COM_ORDER
;====================================================================
A = 0
S = 0
T = 0
Y = 0

;--------------------------------------------------------
;従順度XX
;(従順、恭順、屈服刻印、苦痛刻印、恐怖、素直、プライド低い、弱味、妄信、即落ち、ツンデレ)
L = 0
;--------------------------------------------------------
;ABL:従順
SIF ABL:従順
	L += ABL:従順 * 4

;PALAM:恭順
IF PALAM:恭順 >= PALAMLV:5
	L += 15
ELSEIF PALAM:恭順 >= PALAMLV:4
	L += 12
ELSEIF PALAM:恭順 >= PALAMLV:3
	L += 9
ELSEIF PALAM:恭順 >= PALAMLV:2
	L += 6
ELSEIF PALAM:恭順 >= PALAMLV:1
	L += 3
ENDIF

;MARK:屈服刻印
IF TALENT:プライド高い
	T = 1
ELSEIF TALENT:プライド低い
	T = 4
ELSE
	T = 2
ENDIF
SIF MARK:屈服刻印
	L += MARK:屈服刻印 * 3 * T

;MARK:苦痛刻印
SIF MARK:苦痛刻印
	L += MARK:苦痛刻印 * 5

;PALAM:恐怖
IF PALAM:恐怖 >= PALAMLV:5
	T = 5
ELSEIF PALAM:恐怖 >= PALAMLV:4
	T = 4
ELSEIF PALAM:恐怖 >= PALAMLV:3
	T = 3
ELSEIF PALAM:恐怖 >= PALAMLV:2
	T = 2
ELSEIF PALAM:恐怖 >= PALAMLV:1
	T = 1
ELSE
	T = 0
ENDIF
SIF T && TALENT:気丈
	T -= 1
SIF TALENT:臆病
	T += 1
SIF T
	L += T * 3

T = 0
;TALENT:素直
SIF TALENT:素直
	L += 5
;TALENT:プライド低い
SIF TALENT:プライド低い
	L += 5
;TALENT:弱味
SIF TALENT:弱味
	L += 12
;TALENT:妄信
SIF TALENT:妄信
	L += 10
;TALENT:即落ち
SIF TALENT:即落ち
	L += 20
;TALENT:ツンデレ
SIF TALENT:ツンデレ && KANRAKU(TARGET)
	L += 10

A += L
PRINTFORM 従順度({L})


;--------------------------------------------------------
;反発心XX
;(反発刻印、反感、反抗的、プライド高い、抵抗、気丈、ツンデレ)
;--------------------------------------------------------
L = 0

;反発刻印
IF TALENT:プライド高い
	T = 4
ELSEIF TALENT:プライド低い
	T = 1
ELSE
	T = 2
ENDIF
SIF MARK:反発刻印
	L += MARK:反発刻印 * 2 * T

;PALAM:反感
IF PALAM:反感 >= PALAMLV:5
	T = 5
ELSEIF PALAM:反感 >= PALAMLV:4
	T = 4
ELSEIF PALAM:反感 >= PALAMLV:3
	T = 3
ELSE
	T = 0
ENDIF
IF T
	SIF TALENT:反抗的
		T += 1
	SIF TALENT:生意気
		T += 1
	SIF TALENT:プライド高い
		T += 1
	L += T * 3
ENDIF

T = 0
;反抗的
SIF TALENT:反抗的
	L += 5
;プライド高い
SIF TALENT:プライド高い
	L += 15
;抵抗
SIF TALENT:抵抗
	L += 10
;気丈
SIF TALENT:気丈
	L += 5
;ツンデレ
SIF TALENT:ツンデレ && !KANRAKU(TARGET)
	L += 10

IF L
	;コマンド「罵ってもらう」のみ、この値がプラスに働く。
	IF SELECTCOM == 281
		PRINTFORM  + 
		A += L*2
		PRINTFORM 反発心({L*2})
	ELSE
		PRINTFORM  - 
		A -= L
		PRINTFORM 反発心({L})
	ENDIF
ENDIF


;--------------------------------------------------------
;奉仕精神XX
;--------------------------------------------------------
L = 0

SELECTCASE SELECTCOM
	CASE 3 TO 8,100 TO 105,110 TO 115,145,182 TO 185,405,406
		S = 1
	CASE 74,76,81,83,84,160,161,162,262,265,280 TO 284,422
		S = 2
	CASEELSE
		S = 4
ENDSELECT
SIF ABL:奉仕精神
	L += ABL:奉仕精神 * S

IF TALENT:献身的 && S == 4
	L += 6
ELSEIF TALENT:献身的 && S == 2
	L += 3
ELSEIF TALENT:献身的 && S == 1
	L += 1
ENDIF

IF L
	PRINTFORM  + 
	A += L
	PRINTFORM 奉仕精神({L})
ENDIF


;--------------------------------------------------------
;マゾっ気XX
;--------------------------------------------------------
L = 0

;マゾっ気
SIF ABL:マゾっ気
	L += ABL:マゾっ気 * 2

;マゾ
IF TALENT:マゾ == 2
	L += 10
ELSEIF TALENT:マゾ == 1
	L += 5
ENDIF

IF L
	PRINTFORM  + 
	A += L
	PRINTFORM マゾっ気({L})
ENDIF


;--------------------------------------------------------
;サドっ気XX
;--------------------------------------------------------
L = 0

;小悪魔
SELECTCASE SELECTCOM
	CASE 80,281,283,284,420,421,422
		S = 4
	CASE 77,223,225,243,244,245
		S = 2
	CASEELSE
ENDSELECT
SIF S && TALENT:小悪魔
	L += S

;サド
SELECTCASE SELECTCOM
	CASE 80,280 TO 284,
		SIF TALENT:サド
			L += 10
		SIF ABL:サドっ気
			L += ABL:サドっ気 * 2
	CASE 220 TO 225,260 TO 265,430,431
		SIF TALENT:サド
			L += 5
		SIF ABL:サドっ気
			L += ABL:サドっ気
ENDSELECT

IF L
	PRINTFORM  + 
	A += L
	PRINTFORM サドっ気({L})
ENDIF


;--------------------------------------------------------
;快楽肯定値(XX)
;--------------------------------------------------------
L = 0

SELECTCASE SELECTCOM
	CASE 3 TO 8,100 TO 106,110 TO 116,160 TO 162,181,260 TO 265,405,406,430,431
		S = 3
	CASE 76,182,183,243 TO 245,280,317,422
		S = 2
	CASEELSE
		S = 1
ENDSELECT
SIF ABL:欲望
	L += ABL:欲望 * S

;PALAM:欲情
IF PALAM:欲情 >= PALAMLV:5
	T = 5
ELSEIF PALAM:欲情 >= PALAMLV:4
	T = 4
ELSEIF PALAM:欲情 >= PALAMLV:3
	T = 3
ELSEIF PALAM:欲情 >= PALAMLV:2
	T = 2
ELSEIF PALAM:欲情 >= PALAMLV:1
	T = 1
ELSE
	T = 0
ENDIF
SIF T
	L += T * S

SIF MARK:快楽刻印
	L += MARK:快楽刻印 * S

IF TEQUIP:媚薬
	IF TALENT:薬毒耐性
		L += 4
	ELSE
		L += 8
	ENDIF
ENDIF

IF ZUIKI(TARGET)
	IF TALENT:薬毒耐性
		L += 4
	ELSE
		L += 8
	ENDIF
ENDIF

;快感に素直
SELECTCASE SELECTCOM
	CASE 3 TO 8,76,100 TO 106,110 TO 116,160 TO 162,181,243 TO 245,260 TO 265,317,405,406,422,430,431
		SIF TALENT:快感に素直
			L += 3
ENDSELECT

;セックス中毒
SELECTCASE SELECTCOM
	CASE 100 TO 106,110 TO 116,181,260 TO 265,405,406,430,431
		SIF ABL:セックス中毒
			L += ABL:セックス中毒
ENDSELECT

;自慰しやすい,自慰中毒,露出癖
SELECTCASE SELECTCOM
	CASE 3 TO 8
		SIF TALENT:自慰しやすい
			L += 5
		SIF ABL:自慰中毒
			L += ABL:自慰中毒 * 3
		SIF ABL:露出癖
			L += ABL:露出癖 * 4
ENDSELECT

;恥薄い
SELECTCASE SELECTCOM
	CASE 182,183
		S = 5
	CASE 3 TO 8,106,116,243 TO 245,280,281,317,422
		S = 3
	CASE 76,101,111,420,421
		S = 1
	CASEELSE
		S = 0
ENDSELECT
SIF S && TALENT:恥薄い
	L += S

;精液中毒
SELECTCASE SELECTCOM
	CASE 70,71,73,74,75,80,81,84,86,87,407,420,421,528
		S = 3
	CASE 76,82,83,100 TO 106,110 TO 116,181,220 TO 225,244,245,317,405,406,422
		S = 1
	CASEELSE
		S = 0
ENDSELECT
SIF S && ABL:精液中毒
	L += ABL:精液中毒 * S

;倒錯的
SELECTCASE SELECTCOM
	CASE 80 TO 82,85,145,150,160 TO 162,181 TO 185,280 TO 284
		S = 4
	CASE 3 TO 8,83,84,86,87,420 TO 422
		S = 2
	CASEELSE
		S = 0
ENDSELECT
SIF S && TALENT:倒錯的
	L += S

;目立ちたがり
SIF TALENT:目立ちたがり
	L += 2

;各性感帯感度
SELECTCASE SELECTCOM
	CASE 5,73,74,75,84,243,421,528
		S = ABL:Ｂ感覚
	CASE 3,76,244,245,260 TO 265,280,422,430,431
		S = ABL:Ｃ感覚
	CASE 100 TO 106,160,405
		S = ABL:Ｖ感覚
	CASE 6,8,110 TO 116,161,181,406
		S = ABL:Ａ感覚
	CASE 4
		IF TFLAG:56 == 1
			S = ABL:Ｖ感覚
		ELSE
			S = ABL:Ｃ感覚
		ENDIF
	CASE 7,162
		S = ABL:Ｖ感覚+ABL:Ａ感覚
	CASE 317
		S = ABL:Ｂ感覚+ABL:Ｃ感覚
	CASEELSE
		S = 0
ENDSELECT
SIF S
	L += S * 2

;スキンシップ好き
SELECTCASE SELECTCOM
	CASE 70,71,73 TO 77,80,85 TO 87,100 TO 106,110 TO 116,220 TO 225,243 TO 245,260 TO 265,317,420,421,422,528
		SIF TALENT:スキンシップ好き
			L += 2
	CASE 3 TO 8
		SIF TALENT:スキンシップ苦手
			L += 2
ENDSELECT

;発情期
SIF TALENT:発情期
	L += 8

IF L
	PRINTFORM  + 
	A += L
	PRINTFORM 欲望({L})
ENDIF


;--------------------------------------------------------
;快楽否定値XX
;--------------------------------------------------------
L = 0

;抑圧
SIF TALENT:抑圧
	L += 10
;失意
SIF TALENT:失意
	L += TALENT:失意

;快感の否定
SELECTCASE SELECTCOM
	CASE 3 TO 8,76,100 TO 106,110 TO 116,160 TO 162,181,243 TO 245,260 TO 265,317,405,406,422,430,431
		SIF TALENT:快感の否定
			L += 5
ENDSELECT

;スキンシップ好き
SELECTCASE SELECTCOM
	CASE 70,71,73 TO 77,80,85 TO 87,100 TO 106,110 TO 116,220 TO 225,243 TO 245,260 TO 265,317,420,421,422,528
		SIF TALENT:スキンシップ苦手
			L += 2
	CASE 3 TO 8
		SIF TALENT:スキンシップ好き
			L += 2
ENDSELECT

;自制心
SELECTCASE SELECTCOM
	CASE 3 TO 8
		SIF TALENT:自制心
			L += 5
ENDSELECT

;恥じらい
SELECTCASE SELECTCOM
	CASE 182,183
		S = 5
	CASE 3 TO 8,106,116,243 TO 245,280,281,317,422
		S = 3
	CASE 76,101,111,420,421
		S = 1
	CASEELSE
		S = 0
ENDSELECT
SIF S && TALENT:恥じらい
	L += S

IF L
	PRINTFORM  - 
	A -= L
	PRINTFORM 快楽の否定({L})
ENDIF


;--------------------------------------------------------
;処女、バックバージン、潤滑、セックス中毒XX
;--------------------------------------------------------
L = 0

S = 0
;処女、再生処女
SELECTCASE SELECTCOM
	CASE 3,4,7,100 TO 106,160,162,405
		IF TALENT:処女 || TALENT:再生処女
			IF TALENT:処女
				S += 40
			ELSEIF TALENT:再生処女
				S += 15
			ENDIF
			
			IF TALENT:貞操観念 && !TALENT:恋慕 && !TALENT:親愛
				S += 10
			ELSEIF TALENT:貞操観念 && (TALENT:恋慕 || TALENT:親愛)
				S -= 20
			ELSEIF TALENT:貞操無頓着 || TALENT:淫乱 || TALENT:娼婦
				S -= 10
			ENDIF
			SIF S < 0
				S = 0
			
			IF S
				PRINTFORM  - 
				A -= S
				IF TALENT:処女
					PRINTFORM 処女({S})
				ELSEIF TALENT:再生処女
					PRINTFORM 再生処女({S})
				ENDIF
			ENDIF
		ELSEIF EXP:Ｖ経験 < EXPLV:2 && VAGINA(TARGET)
			PRINTFORM  - 
			A -= 15
			PRINTFORM Ｖ経験不足(15)
		ENDIF
ENDSELECT

S = 0
;バックバージン
SELECTCASE SELECTCOM
	CASE 6,7,8,110 TO 116,161,162,181,406
		IF TALENT:バックバージン
			S += 20
			SIF (TALENT:貞操観念 && TALENT:処女) || TALENT:恋慕 || TALENT:親愛 || TALENT:淫乱 || TALENT:娼婦
				S -= 10
			
			PRINTFORM  - 
			A -= S
			SIF TALENT:バックバージン
				PRINTFORM バックバージン({S})
		ELSEIF EXP:Ａ経験 < EXPLV:2
			PRINTFORM  - 
			A -= 30
			PRINTFORM Ａ経験不足(30)
		ENDIF
ENDSELECT

;潤滑不足
SELECTCASE SELECTCOM
	CASE 3,4,6,7,8,76,100 TO 106,110 TO 116,160 TO 162,405,406,422
		IF PALAM:潤滑 < PALAMLV:3
			PRINTFORM  - 
			A -= 5
			PRINTFORM 潤滑不足(5)
		ENDIF
ENDSELECT


;--------------------------------------------------------
;スカ中毒/スカ経験XX
;--------------------------------------------------------
L = 0

SELECTCASE SELECTCOM
	;肉便器
	CASE 181
		S = 1
	;放尿、排便
	CASE 182,183
		S = 2
	;飲尿、食糞
	CASE 184,185
		S = 3
	CASEELSE
		S = 0
ENDSELECT

IF S && ABL:スカ中毒
	L += ABL:スカ中毒 * 2
	PRINTFORM  + 
	A += L
	PRINTFORM スカ中毒({L})
ELSEIF S && EXP:スカ経験 < 5
	L += S * 5
	PRINTFORM  - 
	A += L
	PRINTFORM スカ経験不足({L})
ENDIF

IF S == 2
	IF TALENT:おもらし癖
		PRINTFORM  + 
		A += 12
		PRINTFORM おもらし癖(12)
	ENDIF
	
	S = 0
	;放尿
	IF SELECTCOM == 182
		IF BASE:尿意 >= 100
			L = 5
		ELSEIF BASE:尿意 >= 80
			L = 3
		ELSEIF BASE:尿意 >= 60
			L = 2
		ELSE
			L = 0
		ENDIF
		IF TALENT:痛みに強い
			L -= 1
		ELSEIF TALENT:痛みに弱い
			L += L
		ENDIF
		IF L > 0
			PRINTFORM  + 
			A += L * 2
			PRINTFORM 尿意有り({L*2})
		ELSE
			PRINTFORM  - 
			A -= 5
			PRINTFORM 尿意無し({5})
		ENDIF
	;排便
	ELSEIF SELECTCOM == 183
		IF BASE:便意 >= 100
			L = 5
		ELSEIF BASE:便意 >= 80
			L = 2
		ELSEIF BASE:便意 >= 60
			L = 1
		ELSE
			L = 0
		ENDIF
		IF TALENT:痛みに強い
			L -= 1
		ELSEIF TALENT:痛みに弱い
			L += L
		ENDIF
		IF L > 0
			PRINTFORM  + 
			A += L
			PRINTFORM 便意有り({L*2})
		ELSE
			PRINTFORM  - 
			A -= 5
			PRINTFORM 便意無し({5})
		ENDIF
	ENDIF
ENDIF


;--------------------------------------------------------
;汚れ/汚臭感度XX
;--------------------------------------------------------
L = 0

SELECTCASE SELECTCOM
	;主人のＢ
	CASE 221,243
		S = STAIN:PLAYER:胸
	;主人のＰ
	CASE 70,71,73 TO 76,80 TO 84,86,87,100 TO 106,110 TO 116,181,245,420 TO 422,528
		S = STAIN:PLAYER:ペニス
	;主人のＶを触る
	CASE 222,223,244,260,261,262
		S = STAIN:PLAYER:ヴァギナ
	;主人のＡを舐める
	CASE 224,225,263,264,265
		S = STAIN:PLAYER:アナル
	;主人の口を触る
	CASE 280
		S = STAIN:PLAYER:口
	;主人の手を舐める
	CASE 77,160,161,162
		S = STAIN:PLAYER:手
	;主人の足を舐める
	CASE 85
		S = STAIN:PLAYER:足
	;助手のＰを触る
	CASE 405,406,407
		S = STAIN:ASSI:ペニス
	;助手のＶに入れる
	CASE 430
		S = STAIN:ASSI:ヴァギナ
	;助手のＡに入れる
	CASE 431
		S = STAIN:ASSI:アナル
	;汚物
	CASE 184
		SIF TFLAG:66 == 0
			S = STAIN:PLAYER:ペニス
	CASEELSE
		S = 0
ENDSELECT

IF S
	Y = 0
	;愛液
	SIF S & 1
		Y += 1
	;ペニス
	SIF S & 2
		Y += 2
	;精液
	SIF S & 4
		Y += 5
	;アナル
	SIF S & 8
		Y += 8
	;母乳
	SIF S & 16
		Y += 1
	;粘液
	SIF S & 32
		Y += 15
	;血
	SIF S & 64
		Y += 5
	;尿
	SIF S & 128
		Y += 12
	;便
	SIF S & 512
		Y += 20
	;吐瀉物
	SIF S & 2048
		Y += 20
ENDIF

IF TALENT:汚臭鈍感 || TALENT:汚れ無視
	PRINTFORM  + 
ELSE
	PRINTFORM  - 
ENDIF

;汚れ
IF Y
	PRINTFORM 汚れ有り
ELSE
	PRINTFORM 汚れ無し
ENDIF

SIF Y && (TALENT:汚臭鈍感 || TALENT:汚臭敏感 || TALENT:汚れ無視)
	PRINTFORM 、

SELECTCASE SELECTCOM
	CASE 182,184
		S = 3
	CASE 183,185
		S = 5
	CASE 220,317
		S = 1
ENDSELECT

SELECTCASE SELECTCOM
	CASE 70,73,74,76,80 TO 84,86,100 TO 106,110 TO 116,160,162,181,221,225,243 TO 245,260 TO 265,405,406,421,422,430,431,528
		SIF Y
			Y /= 3
ENDSELECT

IF Y == 0 && S
	IF TALENT:汚臭鈍感
		Y -= S
	ELSEIF TALENT:汚臭敏感
		Y += S * 3
	ELSEIF TALENT:汚れ無視
		Y = 0
	ELSE
		Y += S
	ENDIF
ELSEIF Y
	SIF TALENT:汚臭鈍感
		Y /= 3
	SIF TALENT:汚臭敏感
		Y *= 2
	SIF TALENT:汚れ無視
		Y = 0
ENDIF

SIF Y < 0
	Y = 0

IF Y
	A -= Y
	IF TALENT:汚臭鈍感
		PRINTFORM 汚臭鈍感
	ELSEIF TALENT:汚臭敏感
		PRINTFORM 汚臭敏感
	ENDIF
	PRINTFORM ({Y})
ENDIF


;--------------------------------------------------------
;陥落
;--------------------------------------------------------
L = 0

IF TALENT:失意
	PRINTFORM  - 
	A -= TALENT:失意
	PRINTFORM 失意({TALENT:失意})
ELSEIF TALENT:恋慕 || TALENT:親愛
	PRINTFORM  + 
	IF TALENT:恋慕
		A += 8
		PRINTFORM 恋慕(8)
	ELSE
		A += 15
		PRINTFORM 親愛(15)
	ENDIF
ELSEIF TALENT:淫乱 || TALENT:娼婦
	PRINTFORM  + 
	IF TALENT:淫乱
		A += 5
		PRINTFORM 淫乱(5)
	ELSE
		A += 10
		PRINTFORM 娼婦(10)
	ENDIF
ELSEIF TALENT:服従 || TALENT:隷属
	PRINTFORM  + 
	IF TALENT:服従
		A += 10
		PRINTFORM 服従(10)
	ELSE
		A += 20
		PRINTFORM 隷属(20)
	ENDIF
ENDIF



;--------------------------------------------------------
;調教者のサドっ気XX
;--------------------------------------------------------
L = 0

;ABL:サドっ気
SIF ABL:PLAYER:サドっ気
	L += ABL:PLAYER:サドっ気 * 2

;サド
IF TALENT:PLAYER:サド == 2
	L += 6
ELSEIF TALENT:PLAYER:サド == 1
	L += 3
ENDIF

IF L
	PRINTFORM  + 
	A += L
	PRINTFORM 調教者のサドっ気({L})
ENDIF


;--------------------------------------------------------
;調教者の魅力XX
;--------------------------------------------------------
L = 0

;調教者の能力、素質
;魅惑
SIF TALENT:PLAYER:魅惑
	L += 6
;謎の魅力
SIF TALENT:PLAYER:謎の魅力
	L += 8

IF L
	PRINTFORM  + 
	A += L
	PRINTFORM 調教者の魅力({L})
ENDIF


;--------------------------------------------------------
;調教者のサイズXX
;--------------------------------------------------------
SELECTCASE SELECTCOM
	CASE 100 TO 106,110 TO 116,181
		L = 1
	CASEELSE
		L = 0
ENDSELECT

IF L
	;調教者が子供サイズ
	IF TALENT:PLAYER:子供サイズ
		A += 2
		PRINTFORM  + 
		PRINTFORM %CALLNAME:PLAYER%が子供サイズ(2)
	ENDIF
	
	;調教者が短小
	IF TALENT:PLAYER:短小
		A += 1
		PRINTFORM  + 
		PRINTFORM %CALLNAME:PLAYER%が短小(1)
	ENDIF
	
	;調教者が巨根
	IF TALENT:PLAYER:巨根
		A -= 2
		PRINTFORM  - 
		PRINTFORM %CALLNAME:PLAYER%が巨根(2)
	ENDIF
	
	;調教者が馬並み
	IF TALENT:PLAYER:馬並み
		A -= 4
		PRINTFORM  - 
		PRINTFORM %CALLNAME:PLAYER%が馬並み(4)
	ENDIF
ENDIF

;--------------------------------------------------------
;調教者の体型XX
;--------------------------------------------------------
L = 0

IF TALENT:PLAYER:小柄体型
	L = 3
ELSEIF TALENT:PLAYER:小人体型
	L = 5
ENDIF

IF SELECTCOM == 150 && L
	A += L
	PRINTFORM  + 
	PRINTFORM %CALLNAME:PLAYER%が
	IF TALENT:PLAYER:小柄体型
		PRINTFORM 小柄体型
	ELSEIF TALENT:PLAYER:小人体型
		PRINTFORM 小人体型
	ENDIF
	PRINTFORM (L)
ENDIF


;--------------------------------------------------------
;調教者が処女XX
;--------------------------------------------------------
L = 0

SELECTCASE SELECTCOM
	CASE 260,261,262
		IF TALENT:PLAYER:処女
			PRINTFORM  - 
			A -= 15
			PRINTFORM %CALLNAME:PLAYER%が処女(15)
		ELSEIF TALENT:PLAYER:再生処女
			PRINTFORM  - 
			A -= 5
			PRINTFORM %CALLNAME:PLAYER%が再生処女(5)
		ENDIF
	CASE 263,264,265
		IF TALENT:PLAYER:バックバージン
			PRINTFORM  - 
			A -= 8
			PRINTFORM %CALLNAME:PLAYER%がバックバージン(15)
		ENDIF
ENDSELECT





;--------------------------------------------------------
;助手の素質
;--------------------------------------------------------
IF ASSI > -1
	IF TALENT:ASSI:鼓舞 && ASSIPLAY == 0
		L = 1
		A += 1
		
		PRINTFORM  + 
		PRINTFORM 助手が鼓舞({1})
	ENDIF
ENDIF


;--------------------------------------------------------
;助手のサイズXX
;--------------------------------------------------------
SELECTCASE SELECTCOM
	CASE 405,406,407
		L = 1
	CASEELSE
		L = 0
ENDSELECT

IF L
	;助手が子供サイズ
	IF TALENT:ASSI:子供サイズ
		A += 2
		PRINTFORM  + 
		PRINTFORM %CALLNAME:ASSI%が子供サイズ(2)
	ENDIF
	
	;助手が短小
	IF TALENT:ASSI:短小
		A += 1
		PRINTFORM  + 
		PRINTFORM %CALLNAME:ASSI%が短小(1)
	ENDIF
	
	;助手が巨根
	IF TALENT:ASSI:巨根
		A -= 2
		PRINTFORM  - 
		PRINTFORM %CALLNAME:ASSI%が巨根(2)
	ENDIF
	
	;助手が馬並み
	IF TALENT:ASSI:馬並み
		A -= 4
		PRINTFORM  - 
		PRINTFORM %CALLNAME:ASSI%が馬並み(4)
	ENDIF
ENDIF


;--------------------------------------------------------
;助手が処女XX
;--------------------------------------------------------
L = 0

SELECTCASE SELECTCOM
	CASE 430
		IF TALENT:ASSI:処女
			PRINTFORM  - 
			A -= 15
			PRINTFORM %CALLNAME:ASSI%が処女(15)
		ELSEIF TALENT:ASSI:再生処女
			PRINTFORM  - 
			A -= 5
			PRINTFORM %CALLNAME:ASSI%が再生処女(5)
		ENDIF
	CASE 431
		IF TALENT:ASSI:バックバージン
			PRINTFORM  - 
			A -= 8
			PRINTFORM %CALLNAME:ASSI%がバックバージン(15)
		ENDIF
ENDSELECT


;--------------------------------------------------------
;レズホモ調教XX
;--------------------------------------------------------
L = 0
S = 0

;レズ調教
IF (!MALE(PLAYER) && !MALE(TARGET)) || (MALE(PLAYER) && MALE(TARGET))
	IF !MALE(PLAYER) && !MALE(TARGET)
		;ABL:レズっ気
		IF ABL:レズっ気 > 3
			L += ABL:レズっ気
		ELSEIF ABL:レズっ気 < 3
			S += ABL:レズっ気
		ENDIF
		;ABL:レズ中毒
		SIF ABL:レズ中毒
			L += ABL:レズ中毒 * 3
	ELSE
		;ABL:ホモっ気
		IF ABL:ホモっ気 > 3
			L += ABL:ホモっ気
		ELSEIF ABL:ホモっ気 < 3
			S += ABL:ホモっ気
		ENDIF
		;ABL:ゲイ中毒
		SIF ABL:ゲイ中毒
			L += ABL:ゲイ中毒*3
	ENDIF
	
	;TALENT:両刀
	SIF TALENT:両刀
		L += 10
	;好奇心
	SIF TALENT:好奇心
		L += 7
	;保守的
	SIF TALENT:保守的
		S += 13
	
	IF L
		PRINTFORM  + 
		A += L
		IF !MALE(PLAYER) && !MALE(TARGET)
			PRINTFORM レズっ気有り({L})
		ELSE
			PRINTFORM ホモっ気有り({L})
		ENDIF
	ELSEIF S
		PRINTFORM  - 
		A -= S
		IF !MALE(PLAYER) && !MALE(TARGET)
			PRINTFORM レズっ気無し({S})
		ELSE
			PRINTFORM ホモっ気無し({S})
		ENDIF
	ENDIF
ELSE
	SIF ABL:レズ中毒
		S += ABL:レズ中毒*2
	SIF ABL:ゲイ中毒
		S += ABL:ゲイ中毒*2
	
	;好奇心
	SIF TALENT:好奇心
		L += 5
	;保守的
	SIF TALENT:保守的
		S += 10
	
	IF L
		A += L
		PRINTFORM  + 
		PRINTFORM 好奇心({L})
	ELSEIF S
		A -= S
		PRINTFORM  - 
		IF ABL:レズ中毒
			PRINTFORM レズ中毒({S})
		ELSEIF ABL:ゲイ中毒
			PRINTFORM ゲイ中毒({S})
		ELSEIF TALENT:保守的
			PRINTFORM 保守的({S})
		ENDIF
	ENDIF
ENDIF


;--------------------------------------------------------
;レズホモ調教XX
;--------------------------------------------------------
L = 0
;相性チェック
R = NO:PLAYER
;女好き、男好き、子供好き
IF (TALENT:女好き && MALE(PLAYER) == 0  && !TALENT:PLAYER:小人体型) || (TALENT:男好き && MALE(PLAYER) && !TALENT:PLAYER:小人体型) || (TALENT:子供好き && TALENT:PLAYER:小人体型)
	IF RELATION:R == 0
		C = 150
	ELSE
		C = RELATION:R + 50
		SIF C > 200
			C = 200
	ENDIF
;男嫌い、女嫌い(恋慕陥落で無効)
ELSEIF !TALENT:恋慕 && !TALENT:親愛 && ((TALENT:男嫌い && MALE(PLAYER)) || (TALENT:女嫌い && MALE(PLAYER) == 0))
	IF RELATION:R == 0
		C = 50
	ELSE
		C = RELATION:R - 50
		SIF C <= 0
		C = 0
	ENDIF
ELSE
	IF RELATION:R < 30
		C = 100
	ELSE
		C = RELATION:R
	ENDIF
ENDIF
IF C >= 170
	PRINTFORM  + 
	A += 10
	PRINTFORM 相性最高(10)
ELSEIF C >= 130
	PRINTFORM  + 
	A += 6
	PRINTFORM 相性良い(6)
ELSEIF C > 100
	PRINTFORM  + 
	A += 3
	PRINTFORM 相性やや良い(3)
ELSEIF C == 100
	PRINTFORM  + 
	PRINTFORM 相性普通(0)
ELSEIF C >= 70
	PRINTFORM  - 
	A -= 3
	PRINTFORM 相性いまいち(3)
ELSEIF C >= 30
	PRINTFORM  - 
	A -= 6
	PRINTFORM 相性悪い(6)
ELSE
	PRINTFORM  - 
	A -= 10
	PRINTFORM 相性最悪(10)
ENDIF



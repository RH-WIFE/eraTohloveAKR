;-------------------------------------------------
;叩いてもらう
;主人マゾ系コマンド
;-------------------------------------------------
@COM283

PRINTL 叩いてもらう
STR:0 = 叩いてもらう

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;実行判定操作
CALL KOJO_DENIAL_PLUS

;合計を表示(25以上で実行)
PRINT  = 
PRINTV A

V = 25
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

;実行不可口上/実行判定操作
SIF A < V
	CALL KOJO_DENIAL

WAIT

;実行できない
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM


;実行者が奴隷であるフラグ
TFLAG:28 = 1

;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
B = 200

CALL SHOOT_GAUGE_CHECK_MT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:射精 += B


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 50

SOURCE:性行動 += 300
SOURCE:達成感 += 400
SOURCE:恐れ += 400
SOURCE:屈従 += 200
SOURCE:逸脱 += 600
SOURCE:反発 += 400

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 600


;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK(PLAYER,1)

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	TFLAG:703 = 0
	;射精先選択の可・不可
	TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;ゲイ経験とか加算
CALL COM_EXP(2)

;サドっ気に応じて嗜虐快楽経験
E = 1
L = 0
IF ABL:サドっ気 == 0
	E += 0
	L += 0
ELSEIF ABL:サドっ気 == 1
	E += 1
	L += 1
ELSEIF ABL:サドっ気 == 2
	E += 3
	L += 2
ELSEIF ABL:サドっ気 == 3
	E += 5
	L += 3
ELSEIF ABL:サドっ気 == 4
	E += 7
	L += 4
ELSEIF ABL:サドっ気 >= 5
	E += 9
	L += 5
ENDIF
;調教者のマゾっ気で嗜虐快楽経験
IF ABL:PLAYER:9 == 0
	E += 0
	L += 0
ELSEIF ABL:PLAYER:9 == 1
	E += 1
	L += 1
ELSEIF ABL:PLAYER:9 == 2
	E += 2
	L += 3
ELSEIF ABL:PLAYER:9 == 3
	E += 3
	L += 5
ELSEIF ABL:PLAYER:9 == 4
	E += 4
	L += 7
ELSEIF ABL:PLAYER:9 >= 5
	E += 5
	L += 9
ENDIF
PRINT 嗜虐快楽経験+
PRINTVL E
EXP:嗜虐快楽経験 += E
IF L > 0
	PRINTFORM %CALLNAME:PLAYER%の
	PRINT 苦痛快楽経験+
	PRINTVL L
	EXP:PLAYER:苦痛快楽経験 += L
	TFLAG:27 = L
ENDIF

;依存度ベクトル
TFLAG:33 = 3

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:隷属 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

;奉仕快楽フラグ
TFLAG:100 = 1

RETURN 1


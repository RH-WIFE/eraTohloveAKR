;-------------------------------------------------
;アナル挿入命令
;助手命令系コマンド
;-------------------------------------------------
@COM406

;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)
	;両方挿入してると「二輪挿し」
	IF PREVCOM == 112 || PREVCOM == 113 || PREVCOM == 114 || PREVCOM == 115 || PREVCOM == 116
		CALL COM_ABLE518
		IF RESULT == 1
			TFLAG:40 = 2
			TFLAG:41 = 2
			TFLAG:49 = 0
			JUMP COM518
		ENDIF
	;主人が助手を犯してると「連結（奴隷←助手←主人）」
	ELSEIF PREVCOM == 440 || PREVCOM == 441
		CALL COM_ABLE519
		IF RESULT == 1
			IF PREVCOM == 440
				TFLAG:40 = 11
			ELSE
				TFLAG:40 = 12
			ENDIF
			TFLAG:41 = 2
			TFLAG:49 = 0
			JUMP COM519
		ENDIF
	;奴隷が主人を犯してると「連結（主人←奴隷←助手）」
	ELSEIF PREVCOM == 260 || PREVCOM == 261 || PREVCOM == 263 || PREVCOM == 264
		CALL COM_ABLE519
		IF RESULT == 1
			IF PREVCOM == 260 || PREVCOM == 261
				TFLAG:49 = 6
			ELSE
				TFLAG:49 = 7
			ENDIF 
			TFLAG:41 = 2
			TFLAG:40 = 0
			JUMP COM519
		ENDIF
	;主人が挿入してると「サンドイッチ（二穴挿入）」
	ELSEIF PREVCOM == 102 || PREVCOM == 104 || PREVCOM == 106
		CALL COM_ABLE515
		IF RESULT == 1
			TFLAG:40 = 1
			TFLAG:41 = 2
			TFLAG:49 = 0
			JUMP COM515
		ENDIF
	;主人が口奉仕してると「サンドイッチ」
	ELSEIF PREVCOM == 31 || PREVCOM == 40 || PREVCOM == 280
		CALL COM_ABLE515
		IF RESULT == 1
			IF PREVCOM == 31
				TFLAG:40 = 3
			ELSEIF PREVCOM == 40
				TFLAG:40 = 4
			ELSEIF PREVCOM == 280
				TFLAG:40 = 5
			ENDIF
			TFLAG:41 = 2
			TFLAG:49 = 0
			JUMP COM515
		ENDIF
	;奴隷が口奉仕させられてると「サンドイッチ」
	ELSEIF PREVCOM == 71 || PREVCOM == 72 || PREVCOM == 222
		CALL COM_ABLE515
		IF RESULT == 1
			TFLAG:40 = 0
			TFLAG:41 = 2
			IF PREVCOM == 71
				TFLAG:49 = 8
			ELSEIF PREVCOM == 72
				TFLAG:49 = 10
			ELSEIF PREVCOM == 222
				TFLAG:49 = 9
			ENDIF
			JUMP COM515
		ENDIF
	;前回コマンドが「サンドイッチ」「連結」「二輪挿しアナル」
	ELSEIF PREVCOM == 515 || PREVCOM == 519 || PREVCOM == 518
		;両方挿入してると「二輪挿し」
		IF TFLAG:40 == 2
			CALL COM_ABLE518
			IF RESULT == 1
				TFLAG:41 = 2
				TFLAG:49 = 0
				JUMP COM518
			ENDIF
		;主人が挿入してると「サンドイッチ（二穴挿入）」
		ELSEIF TFLAG:40 == 1
			CALL COM_ABLE515
			IF RESULT == 1
				TFLAG:41 = 2
				TFLAG:49 = 0
				JUMP COM515
			ENDIF
		;主人が助手を犯してると「連結（奴隷←助手←主人）」
		ELSEIF TFLAG:40 == 11 || TFLAG:40 == 12
			CALL COM_ABLE519
			IF RESULT == 1
				TFLAG:41 = 2
				TFLAG:49 = 0
				JUMP COM519
			ENDIF
		;主人が口奉仕してると「サンドイッチ」
		ELSEIF TFLAG:40 == 3 || TFLAG:40 == 4 || TFLAG:40 == 5
			CALL COM_ABLE515
			IF RESULT == 1
				TFLAG:41 = 2
				TFLAG:49 = 0
				JUMP COM515
			ENDIF
		;奴隷が主人を犯してると「連結（主人←奴隷←助手）」
		ELSEIF TFLAG:49 == 6 || TFLAG:49 == 7
			CALL COM_ABLE519
			IF RESULT == 1
				TFLAG:40 = 0
				TFLAG:41 = 2
				JUMP COM519
			ENDIF
		;奴隷が口奉仕させられてると「サンドイッチ」
		ELSEIF TFLAG:49 == 8 || TFLAG:49 == 9 || TFLAG:49 == 10
			CALL COM_ABLE515
			IF RESULT == 1
				TFLAG:40 = 0
				TFLAG:41 = 2
				JUMP COM515
			ENDIF
		ENDIF
	ENDIF
ENDIF

;何も読まずにこっちに来たときはフラグ解除
TFLAG:40 = 0
TFLAG:41 = 0
TFLAG:49 = 0
TFLAG:42 = 0

PRINTL アナル挿入命令
STR:0 = アナル挿入命令

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;実行判定操作
CALL KOJO_DENIAL_PLUS

;合計を表示(30以上で実行)
PRINT  = 
PRINTV A

V = 30
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

;実行不可口上/実行判定操作
SIF A < V
	CALL KOJO_DENIAL

WAIT

;実行できない
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM

;ストッキングびりびりフラグ
IF TEQUIP:特殊２ == 5 || TEQUIP:特殊２ == 6
	TFLAG:55 = 1
ENDIF

;調教対象がバックバージンだった場合の喪失フラグ
SIF TALENT:バックバージン
	TFLAG:161 |= 1

;調教者が童貞だった場合の喪失フラグ
SIF TALENT:ASSI:童貞
	TFLAG:160 |= 3


;-------------------------------------------------
;射精ゲージチェック（助手）
;-------------------------------------------------
B = 1000

CALL SHOOT_GAUGE_CHECK_AT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:ASSI:9 > 0
	B /= 20

SIF PENIS(ASSI)
	BASE:ASSI:射精 += B


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 100

SOURCE:快Ａ += 200
SIF ABL:ASSI:技巧
	SOURCE:快Ａ += 10*ABL:ASSI:技巧

SOURCE:露出 += 50
SOURCE:屈従 += 50
SOURCE:逸脱 += 50

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 70

;小人体型で助手が巨根か馬並みの場合、苦痛と恐怖のソースを追加
IF TALENT:小人体型
	IF TALENT:ASSI:巨根
		SOURCE:痛み += 200
		SOURCE:恐れ += 500
	ELSEIF TALENT:ASSI:馬並み
		SOURCE:痛み += 500
		SOURCE:恐れ += 1000
	ENDIF
ENDIF

;助手が馬並みの場合
IF TALENT:ASSI:馬並み
	;Ａ拡張経験一定以上で中毒充足のソースを追加
	IF EXP:Ａ拡張経験 >= 20
		SOURCE:中毒充足 += 500
	;Ａ拡張経験一定以下で苦痛と恐怖のソースを追加
	ELSE
		SOURCE:痛み += 500
		SOURCE:恐れ += 1000
	ENDIF
ENDIF

;助手の技巧により快Ａソース追加、下手すぎると痛みのソース追加
IF ABL:ASSI:2 == 0
	SOURCE:痛み += 100
ELSEIF ABL:ASSI:2 == 1
	SOURCE:痛み += 50
ENDIF
;助手が小人体型で、調教対象が小人体型でなく倒錯的でもないと逸脱のソース追加
SIF TALENT:小人体型 == 0 && TALENT:ASSI:小人体型 && TALENT:倒錯的
	SOURCE:逸脱 += 50	
;Aへの苦痛は先に処理しておく
;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES SOURCE:痛み , 3.00
	TIMES SOURCE:反発 , 3.00
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES SOURCE:痛み , 1.20
	TIMES SOURCE:反発 , 2.00
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES SOURCE:痛み , 0.80
	TIMES SOURCE:反発 , 1.50
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES SOURCE:痛み , 0.60
	TIMES SOURCE:反発 , 1.20
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES SOURCE:痛み , 0.40
ELSEIF EXP:Ａ経験 >= EXPLV:5
	TIMES SOURCE:痛み , 0.20
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:痛み , 1.20
	TIMES SOURCE:反発 , 2.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:痛み , 1.00
	TIMES SOURCE:反発 , 1.50
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:痛み , 0.60
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:痛み , 0.30
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:痛み , 0.20
ENDIF


;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のＡ⇔助手のペニスの汚れが移動
;ペニスバンドを使う場合、汚れは移動しない
IF PENIS(ASSI)
	STAIN:アナル |= STAIN:ASSI:ペニス
	STAIN:ASSI:ペニス |= STAIN:アナル
	STAIN:腸内 |= STAIN:ASSI:ペニス
	STAIN:ASSI:ペニス |= STAIN:腸内
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;A経験
EXP:Ａ経験 += 2
PRINTL Ａ経験＋２

;助手が馬並みで奴隷が小人体型だった場合、Ａ拡張経験
IF TALENT:小人体型 && TALENT:ASSI:馬並み
	IF EXP:Ａ拡張経験 == 0
		EXP:異常経験 += 1
		PRINTL Ａ拡張初経験で異常経験＋１
	ENDIF
	EXP:Ａ拡張経験 += 1
	PRINTL Ａ拡張経験＋１
ENDIF

;奴隷と助手に性交経験
EXP:性交経験 += 1
EXP:ASSI:性交経験 += 1

;挿入経験
EXP:ASSI:挿入経験 += 1

;助手に快Ｃ・習得・恭順・否定の珠
JUEL:ASSI:快Ｃ += 100
JUEL:ASSI:恭順 += 100
JUEL:ASSI:習得 += 100
JUEL:ASSI:否定 += 100
PRINTFORML %CALLNAME:ASSI%に%PALAMNAME:0%の珠＋100
PRINTFORML %CALLNAME:ASSI%に%PALAMNAME:5%の珠＋100
PRINTFORMW %CALLNAME:ASSI%に%PALAMNAME:8%の珠＋100
PRINTFORMW %CALLNAME:ASSI%に%PALAMNAME:100%の珠＋100

;ゲイ経験とか加算
CALL COM_EXP(5)

;依存度ベクトル
TFLAG:33 = 3


RETURN 1



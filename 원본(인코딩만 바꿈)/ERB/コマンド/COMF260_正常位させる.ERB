;-------------------------------------------------
;正常位させる
;主人受け系コマンド
;-------------------------------------------------
@COM260

;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)
	;奴隷に助手が挿入してると「連結（主人←奴隷←助手）」
	IF PREVCOM == 405 || PREVCOM == 406
		CALL COM_ABLE519
		IF RESULT == 1
			TFLAG:40 = 0
			IF PREVCOM == 405
				TFLAG:41 = 1
			ELSE
				TFLAG:41 = 2
			ENDIF
			TFLAG:49 = 6
			JUMP COM519
		ENDIF
	;前回コマンドが連結で、助手が挿入してる
	ELSEIF PREVCOM == 519 && (TFLAG:41 == 1 || TFLAG:41 == 2)
		CALL COM_ABLE519
		IF RESULT == 1
			TFLAG:40 = 0
			TFLAG:49 = 6
			JUMP COM519
		ENDIF
	ENDIF
ENDIF

;何も読まずにこっちに来たときはフラグ解除
TFLAG:40 = 0
TFLAG:41 = 0
TFLAG:49 = 0
TFLAG:42 = 0

PRINTL 正常位させる
STR:0 = 正常位させる

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;実行判定操作
CALL KOJO_DENIAL_PLUS

;合計を表示(24以上で実行)
PRINT  = 
PRINTV A

V = 24
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

;実行不可口上/実行判定操作
SIF A < V
	CALL KOJO_DENIAL

WAIT

;実行できない
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM

;ストッキング脱がせる
IF TEQUIP:特殊２ == 5 || TEQUIP:特殊２ == 6
	TEQUIP:特殊２ = 0
	TEQUIP:特殊２ = 0
ENDIF

;調教者が処女だった場合の喪失フラグ(処女膜再生対応)
TFLAG:19 |= 2

;奴隷が童貞だった場合の喪失フラグ
SIF TALENT:童貞
	TFLAG:160 |= 1
;主人によるセックスで童貞喪失
SIF TALENT:童貞 && (TALENT:恋慕 || TALENT:親愛) && ASSIPLAY == 0
	TFLAG:162 = 1

;実行者が奴隷であるフラグ
TFLAG:28 = 1


;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 1000

CALL SHOOT_GAUGE_CHECK_MT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:射精 += B

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
LOSEBASE:体力 += 100
LOSEBASE:気力 += 50

SOURCE:快Ｃ += 300

SOURCE:情愛 += 200

;スキンシップ好きだと情愛のソース追加
SIF TALENT:スキンシップ好き
	SOURCE:情愛 += 50


SOURCE:性行動 += 500
SOURCE:中毒充足 += 200
SOURCE:露出 += 50
SOURCE:屈従 += 400
SOURCE:反発 += 200

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 300
	
;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && (TALENT:恋慕 == 0 && TALENT:親愛 == 0)
	SOURCE:反発 += 1000

;中毒による中毒充足のソースは先に処理しておく
;セックス中毒を見る
IF ABL:セックス中毒 == 1
	TIMES SOURCE:中毒充足 , 0.00
ELSEIF ABL:セックス中毒 == 2
	TIMES SOURCE:中毒充足 , 0.30
ELSEIF ABL:セックス中毒 == 3
	TIMES SOURCE:中毒充足 , 0.60
ELSEIF ABL:セックス中毒 == 4
	TIMES SOURCE:中毒充足 , 0.90
ELSEIF ABL:セックス中毒 == 5
	TIMES SOURCE:中毒充足 , 1.20
ELSEIF ABL:セックス中毒 == 6
	TIMES SOURCE:中毒充足 , 1.50
ELSEIF ABL:セックス中毒 == 7
	TIMES SOURCE:中毒充足 , 1.80
ELSEIF ABL:セックス中毒 == 8
	TIMES SOURCE:中毒充足 , 2.30
ELSEIF ABL:セックス中毒 == 9
	TIMES SOURCE:中毒充足 , 3.00
ELSEIF ABL:セックス中毒 >= 10
	TIMES SOURCE:中毒充足 , 5.00
ENDIF

;馬並み持ちで、調教者が小人体型だった場合、逸脱ソース追加
IF TALENT:PLAYER:小人体型 && TALENT:馬並み
	SOURCE:逸脱 += 500
ENDIF


;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK(PLAYER,1)

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	TFLAG:703 = 9
	;射精先選択の可・不可
	TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF


;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のＣ⇔調教者のＶと膣内の汚れが移動
;ペニスバンドを使う場合、汚れは移動しない
SIF PENIS(TARGET)
	CALL COM_STAIN


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;調教者のV経験
EXP:PLAYER:Ｖ経験 += 1

;調教者が小人体型で奴隷が馬並みだった場合
IF TALENT:PLAYER:小人体型 && TALENT:馬並み
	IF EXP:PLAYER:Ｖ拡張経験 == 0
		EXP:PLAYER:異常経験 += 1
		PRINTFORML Ｖ拡張初経験で%CALLNAME:PLAYER%の異常経験＋１
	ENDIF
	EXP:PLAYER:Ｖ拡張経験 += 1
	PRINTFORML %CALLNAME:PLAYER%のＶ拡張経験＋１
ENDIF

;性交経験
EXP:PLAYER:性交経験 += 1
EXP:性交経験 += 1
PRINTS EXPNAME:4
PRINTL +1

;挿入経験
EXP:挿入経験 += 1
PRINTS EXPNAME:17
PRINTL +1

;ゲイ経験とか加算
CALL COM_EXP(4)

;愛情経験
IF TALENT:恋慕 == 1 && ASSIPLAY == 0
	PRINTS EXPNAME:23
	PRINTL +2
	EXP:愛情経験 += 2
ELSEIF TALENT:親愛 == 1 && ASSIPLAY == 0
	PRINTS EXPNAME:23
	PRINTL +4
	EXP:愛情経験 += 4
ENDIF

;依存度ベクトル
TFLAG:33 = 1

;依存度補正値(恋慕で1、親愛で2）
IF TALENT:恋慕 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:親愛 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

;奉仕快楽フラグ
TFLAG:100 = 1

RETURN 1


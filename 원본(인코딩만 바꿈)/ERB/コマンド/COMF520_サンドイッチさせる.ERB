;-------------------------------------------------
;サンドイッチさせる
;派生系コマンド
;
; 1:奴隷Ｖに挿入	 2:奴隷Ａに挿入
; 3:奴隷をフェラ	 4:奴隷をクンニ		 5:奴隷をイラマ
; 6:主人Ｖに挿入	 7:主人Ａに挿入
; 8:主人をフェラ	 9:主人をクンニ		10:主人をイラマ
;11:助手Ｖに挿入	12:助手Ａに挿入		13:助手をフェラ
;-------------------------------------------------
@COM520

PRINTFORML サンドイッチさせる
STR:0 = サンドイッチさせる

SELECTCOM = 520
CALL KOJO_MESSAGE_COM


;実行者が奴隷であるフラグ
TFLAG:28 = 1

;ストッキングびりびりフラグ
IF TEQUIP:特殊２ == 5 || TEQUIP:特殊２ == 6
	TFLAG:55 = 1
ENDIF


;処女喪失フラグ
SIF (TALENT:MASTER:処女 || TALENT:MASTER:再生処女) && (TFLAG:49 == 6 || TFLAG:41 == 6)
	TFLAG:19 |= 2

;処女喪失フラグ
SIF TALENT:MASTER:バックバージン && (TFLAG:49 == 7 || TFLAG:41 == 7)
	TFLAG:161 |= 2

;調教者が童貞だった場合の喪失フラグ
SIF TALENT:童貞 && (TFLAG:49 == 6 || TFLAG:49 == 7)
	TFLAG:160 |= 1
;主人によるセックスで童貞喪失
SIF TALENT:童貞 && (TALENT:恋慕 || TALENT:親愛) && ASSIPLAY == 0 && (TFLAG:49 == 6 || TFLAG:49 == 7)
	TFLAG:162 = 1

;調教者が童貞だった場合の喪失フラグ
SIF TALENT:ASSI:童貞 && (TFLAG:41 == 6 || TFLAG:41 == 7)
	TFLAG:160 |= 3


;-------------------------------------------------
;射精ゲージチェック（主人）
;-------------------------------------------------
B = 1000

CALL SHOOT_GAUGE_CHECK_3PM

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:MASTER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:MASTER:射精 += B

;-------------------------------------------------
;射精ゲージチェック（助手）
;-------------------------------------------------
B = 1000

CALL SHOOT_GAUGE_CHECK_3PA

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:ASSI:9 > 0
	B /= 20

SIF PENIS(ASSI)
	BASE:ASSI:射精 += B

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
LOSEBASE:体力 += 180
LOSEBASE:気力 += 300

SOURCE:快Ｃ += 350

SOURCE:情愛 += 100
;スキンシップ好きだと情愛のソース追加
SIF TALENT:スキンシップ好き
	SOURCE:情愛 += 50

SOURCE:性行動 += 520
SOURCE:達成感 += 150
SOURCE:中毒充足 += 500
SOURCE:屈従 += 500
SOURCE:逸脱 += 500
SOURCE:反発 += 500

;Ｃ愛撫されてる場合
IF TFLAG:40 == 3 || TFLAG:40 == 4 || TFLAG:40 == 5
	SOURCE:快Ｃ += 150
	
	;調教者のTALENT:舌使いをみる
	IF TALENT:MASTER:舌使い
		TIMES SOURCE:快Ｃ , 1.50
	ELSEIF TALENT:MASTER:猫舌
		TIMES SOURCE:快Ｃ , 0.75
	ENDIF
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && (TALENT:恋慕 == 0 && TALENT:親愛 == 0)
	SOURCE:反発 += 1000

;馬並み持ちで、調教者が小人体型だった場合、逸脱ソース追加
IF TALENT:MASTER:小人体型 && TALENT:馬並み
	SOURCE:逸脱 += 500
ENDIF

;セックス中毒を見る
IF ABL:セックス中毒 == 1
	TIMES SOURCE:中毒充足 , 0.00
ELSEIF ABL:セックス中毒 == 2
	TIMES SOURCE:中毒充足 , 0.30
ELSEIF ABL:セックス中毒 == 3
	TIMES SOURCE:中毒充足 , 0.60
ELSEIF ABL:セックス中毒 == 4
	TIMES SOURCE:中毒充足 , 0.90
ELSEIF ABL:セックス中毒 == 5
	TIMES SOURCE:中毒充足 , 1.20
ELSEIF ABL:セックス中毒 == 6
	TIMES SOURCE:中毒充足 , 1.50
ELSEIF ABL:セックス中毒 == 7
	TIMES SOURCE:中毒充足 , 1.80
ELSEIF ABL:セックス中毒 == 8
	TIMES SOURCE:中毒充足 , 2.30
ELSEIF ABL:セックス中毒 == 9
	TIMES SOURCE:中毒充足 , 3.00
ELSEIF ABL:セックス中毒 >= 10
	TIMES SOURCE:中毒充足 , 5.00
ENDIF


;-------------------------------------------------
;射精チェック１（主人）
;-------------------------------------------------
CALL SAMEN_CHECK(MASTER,1)

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	IF TFLAG:49 == 8 || TFLAG:49 == 9 || TFLAG:49 == 10
		TFLAG:703 = 4
	ELSE
		TFLAG:703 = 9
	ENDIF
	;射精先選択の可・不可
	TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;助手に犯させるで主人が射精したフラグ
IF TFLAG:41 == 6 || TFLAG:41 == 7
	IF E == 2
		TFLAG:7 = 4
	ELSEIF E == 1
		TFLAG:7 = 3
	ENDIF
ENDIF

;-------------------------------------------------
;射精チェック２（助手）
;-------------------------------------------------
CALL SAMEN_CHECK(ASSI)

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	TFLAG:703 = 6
	;射精先選択の可・不可
	TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;-------------------------------------------------
;汚れの処理
;挿入している部位によって、ソースの数値が変化する
;-------------------------------------------------
IF TFLAG:40 == 6 || TFLAG:41 == 6
	;処女の場合、Ｖと膣内に破瓜の血の汚れを付加
	IF TALENT:MASTER:処女 || TALENT:MASTER:再生処女
		STAIN:MASTER:膣内 |= 64
		STAIN:MASTER:ヴァギナ |= 64
	ENDIF
ENDIF


;助手が主人のＶに挿入
IF TFLAG:41 == 6 && PENIS(ASSI)
	STAIN:ASSI:ペニス |= STAIN:MASTER:膣内
	STAIN:MASTER:膣内 |= STAIN:ASSI:ペニス
	STAIN:ASSI:ペニス |= STAIN:MASTER:ヴァギナ
	STAIN:MASTER:ヴァギナ |= STAIN:ASSI:ペニス
ENDIF

;助手が主人のＡに挿入
IF TFLAG:41 == 7 && PENIS(ASSI)
	STAIN:ASSI:ペニス |= STAIN:MASTER:腸内
	STAIN:MASTER:腸内 |= STAIN:ASSI:ペニス
	STAIN:ASSI:ペニス |= STAIN:MASTER:アナル
	STAIN:MASTER:アナル |= STAIN:ASSI:ペニス
ENDIF

;奴隷が主人のＶに挿入
IF TFLAG:49 == 6 && PENIS(TARGET)
	STAIN:ペニス |= STAIN:MASTER:膣内
	STAIN:MASTER:膣内 |= STAIN:ペニス
	STAIN:ペニス |= STAIN:MASTER:ヴァギナ
	STAIN:MASTER:ヴァギナ |= STAIN:ペニス
ENDIF

;奴隷が主人のＡに挿入
IF TFLAG:49 == 7 && PENIS(TARGET)
	STAIN:ペニス |= STAIN:MASTER:腸内
	STAIN:MASTER:腸内 |= STAIN:ペニス
	STAIN:ペニス |= STAIN:MASTER:アナル
	STAIN:MASTER:アナル |= STAIN:ペニス
ENDIF

;奴隷→主人Ｃ
IF TFLAG:49 == 8 || TFLAG:49 == 9 || TFLAG:49 == 10
	STAIN:口 |= STAIN:MASTER:ペニス
	STAIN:MASTER:ペニス = 2
ENDIF

;主人→奴隷Ｃ
IF TFLAG:40 == 3 || TFLAG:40 == 4 || TFLAG:40 == 5
	STAIN:MASTER:口 |= STAIN:ペニス
	STAIN:ペニス = 2
ENDIF


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;主人がＶに挿入されてる
IF TFLAG:4 == 6 || TFLAG:4 == 6
	;調教者が馬並みで奴隷が小人体型だった場合、Ｖ拡張経験
	IF TALENT:MASTER:小人体型 && TALENT:ASSI:馬並み && TFLAG:41 == 6
		IF EXP:MASTER:Ｖ拡張経験 == 0
			EXP:MASTER:異常経験 += 1
			PRINTFORML Ｖ拡張初経験で異常経験＋１
		ENDIF
		EXP:MASTER:Ｖ拡張経験 += 1
		PRINTFORML Ｖ拡張経験＋１
	ENDIF
	IF TALENT:MASTER:小人体型 && TALENT:馬並み && TFLAG:49 == 6
		IF EXP:MASTER:Ｖ拡張経験 == 0
			EXP:MASTER:異常経験 += 1
			PRINTFORML Ｖ拡張初経験で異常経験＋１
		ENDIF
		EXP:MASTER:Ｖ拡張経験 += 1
		PRINTFORML Ｖ拡張経験＋１
	ENDIF
	EXP:MASTER:Ｖ経験 += 1
	PRINTFORML %CALLNAME:PLAYER%のＶ経験＋１
ENDIF

;主人がＡに挿入されてる
IF TFLAG:4 == 7 || TFLAG:4 == 7
	;調教者が馬並みで奴隷が小人体型だった場合、Ａ拡張経験
	IF TALENT:MASTER:小人体型 && TALENT:ASSI:馬並み && TFLAG:41 == 7
		IF EXP:MASTER:Ａ拡張経験 == 0
			EXP:MASTER:異常経験 += 1
			PRINTFORML Ａ拡張初経験で異常経験＋１
		ENDIF
		EXP:MASTER:Ａ拡張経験 += 1
		PRINTFORML Ａ拡張経験＋１
	ENDIF
	IF TALENT:MASTER:小人体型 && TALENT:馬並み && TFLAG:49 == 6
		IF EXP:MASTER:Ａ拡張経験 == 0
			EXP:MASTER:異常経験 += 1
			PRINTFORML Ａ拡張初経験で異常経験＋１
		ENDIF
		EXP:MASTER:Ａ拡張経験 += 1
		PRINTFORML Ａ拡張経験＋１
	ENDIF
	EXP:MASTER:Ａ経験 += 1
	PRINTFORML %CALLNAME:PLAYER%のＡ経験＋１
ENDIF

;挿入してるか、されてれば性交経験
IF TFLAG:49 == 6 || TFLAG:49 == 7
	EXP:性交経験 += 1
	PRINTFORML %CALLNAME%の性交経験＋1
	
	EXP:挿入経験 += 1
ENDIF
IF TFLAG:41 == 6 || TFLAG:41 == 7
	EXP:ASSI:性交経験 += 1
	PRINTFORML %CALLNAME:ASSI%の性交経験＋1
	
	EXP:ASSI:挿入経験 += 1
ENDIF
IF TFLAG:49 == 6 || TFLAG:49 == 7 || TFLAG:41 == 6 || TFLAG:41 == 7
	EXP:MASTER:性交経験 += 1
	PRINTFORML %CALLNAME:PLAYER%の性交経験＋1
ENDIF

;奉仕系
IF TFLAG:49 == 8 || TFLAG:49 == 9 || TFLAG:49 == 10
	;奉仕快楽フラグ
	TFLAG:100 = 1
	
	;フェラ経験
	IF TFLAG:49 == 8 || TFLAG:49 == 10 || TFLAG:49 == 13
		EXP:フェラ経験 += 2
		PRINTFORML %EXPNAME:22%＋２
	ENDIF

	;奴隷が猫舌持ちの場合は、奴隷に嗜虐快楽経験を追加
	IF TALENT:TARGET:猫舌
		EXP:嗜虐快楽経験 += 1
		PRINTFORML %EXPNAME:32%＋１
	ENDIF
ENDIF

;ゲイ経験とか加算
CALL COM_EXP(7)

;依存度ベクトル
TFLAG:33 = 1

RETURN 1



;-------------------------------------------------
;アナル愛撫させる
;愛撫させる系コマンド
;-------------------------------------------------
@COM225

PRINTL アナル愛撫させる
STR:0 = アナル愛撫させる

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;実行判定操作
CALL KOJO_DENIAL_PLUS

;合計を表示(26以上で実行)
PRINT  = 
PRINTV A

V = 26
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

;実行不可口上/実行判定操作
SIF A < V
	CALL KOJO_DENIAL

WAIT

;実行できない
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM


;実行者が奴隷であるフラグ
TFLAG:28 = 1


;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 600

CALL SHOOT_GAUGE_CHECK_MT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:射精 += B

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 5
LOSEBASE:気力 += 30

;スキンシップ好きだと情愛のソース追加
SIF TALENT:スキンシップ好き
	SOURCE:情愛 += 50

SOURCE:性行動 += 300
SOURCE:達成感 += 300
SOURCE:屈従 += 1500
SOURCE:逸脱 += 1200
SOURCE:反発 += 1200

;上のほうで計算した汚れデータ
SOURCE:不潔 += Y*80 + 50


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 1200


;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK(PLAYER,1)

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	TFLAG:703 = 7
	;射精先選択の可・不可
	;奴隷が攻め、かつ主人が受けかオメガだと選択不可
	IF TALENT:攻め && (TALENT:PLAYER:受け || TALENT:MASTER:オメガ)
		TFLAG:704 = 0
	ELSE
		TFLAG:704 = 1
	ENDIF
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
CALL COM_STAIN


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;ゲイ経験とか加算
CALL COM_EXP(6)

;依存度ベクトル
TFLAG:33 = 5

EXP:PLAYER:Ａ経験 += 1
PRINTFORML %CALLNAME:PLAYER%のＡ経験＋１

;依存度補正値(淫乱で1、娼婦で2）
IF TALENT:淫乱 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:娼婦 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

;奉仕快楽フラグ
TFLAG:100 = 1

RETURN 1



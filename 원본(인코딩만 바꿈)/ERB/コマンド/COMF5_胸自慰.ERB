;-------------------------------------------------
;胸自慰
;自慰系コマンド
;-------------------------------------------------
@COM5

;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)
	;前回の調教が騎乗位or騎乗位アナル、騎乗位乳房自慰のとき、騎乗乳房自慰に
	IF PREVCOM == 106 || PREVCOM == 116 || PREVCOM == 509
		IF PREVCOM == 106
			TFLAG:705 = 0
		ELSEIF PREVCOM == 116
			TFLAG:705 = 1
		ENDIF
		CALL COM_ABLE509
		SIF RESULT == 1
			JUMP COM509
	ENDIF
ENDIF

LOCALS = 
SIF TEQUIP:ビデオカメラ
	LOCALS = 公開
SIF TEQUIP:野外プレイ
	LOCALS =  %LOCALS%野外

LOCALS = %LOCALS%胸自慰
STR:0 = %LOCALS%
PRINTFORML %STR:0%

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;実行判定操作
CALL KOJO_DENIAL_PLUS

;合計を表示(33以上で実行)
PRINT  = 
PRINTV A

;難易度上昇
;公開で+10
V = 33
SIF TEQUIP:ビデオカメラ
	V += 10
SIF TEQUIP:野外プレイ
	V += 10
SIF TEQUIP:ニプルキャップ
	V += 3
SIF TEQUIP:乳首クリップ
	V += 10
SIF TEQUIP:搾乳機
	V += 5

SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

;実行不可口上/実行判定操作
SIF A < V
	CALL KOJO_DENIAL

WAIT

;実行できない
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM


;実行者が奴隷であるフラグ
TFLAG:28 = 1


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 50

SOURCE:快Ｂ += 80
SIF TEQUIP:ニプルキャップ || TEQUIP:乳首クリップ || TEQUIP:搾乳機
	TIMES SOURCE:快Ｂ , 1.50

SIF TEQUIP:乳首クリップ
	SOURCE:痛み += 200

SOURCE:性行動 += 40
SOURCE:恐れ += 100
SOURCE:中毒充足 += 100
SOURCE:露出 += 200
SOURCE:屈従 += 400
SOURCE:逸脱 += 150
SOURCE:反発 += 150

IF TEQUIP:ビデオカメラ || TEQUIP:野外プレイ
	SOURCE:液体 += 50
	SOURCE:逸脱 += 100
ENDIF


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;中毒による中毒充足のソースは先に処理しておく
;自慰中毒を見る
IF ABL:自慰中毒 == 1
	TIMES SOURCE:快Ｂ , 1.00
	TIMES SOURCE:中毒充足 , 0.00
ELSEIF ABL:自慰中毒 == 2
	TIMES SOURCE:快Ｂ , 1.10
	TIMES SOURCE:中毒充足 , 0.30
ELSEIF ABL:自慰中毒 == 3
	TIMES SOURCE:快Ｂ , 1.20
	TIMES SOURCE:中毒充足 , 0.60
ELSEIF ABL:自慰中毒 == 4
	TIMES SOURCE:快Ｂ , 1.30
	TIMES SOURCE:中毒充足 , 0.90
ELSEIF ABL:自慰中毒 == 5
	TIMES SOURCE:快Ｂ , 1.50
	TIMES SOURCE:中毒充足 , 1.20
ELSEIF ABL:自慰中毒 == 6
	TIMES SOURCE:快Ｂ , 1.70
	TIMES SOURCE:中毒充足 , 1.50
ELSEIF ABL:自慰中毒 == 7
	TIMES SOURCE:快Ｂ , 2.00
	TIMES SOURCE:中毒充足 , 1.80
ELSEIF ABL:自慰中毒 == 8
	TIMES SOURCE:快Ｂ , 2.30
	TIMES SOURCE:中毒充足 , 2.30
ELSEIF ABL:自慰中毒 == 9
	TIMES SOURCE:快Ｂ , 2.50
	TIMES SOURCE:中毒充足 , 3.00
ELSEIF ABL:自慰中毒 >= 10
	TIMES SOURCE:快Ｂ , 3.50
	TIMES SOURCE:中毒充足 , 5.00
ENDIF

;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 150


;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の手（手袋）⇔奴隷のＢの汚れが移動
;道具がついてると汚れは移動しない
IF TEQUIP:ニプルキャップ || TEQUIP:乳首クリップ || TEQUIP:搾乳機
ELSE
	CALL COM_STAIN
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;ビデオ撮影時は経験にプラス
IF TEQUIP:ビデオカメラ
	EXP:自慰経験 += 2
	PRINTL 自慰経験＋２
	EXP:調教自慰経験 += 2
	PRINTL 調教自慰経験＋２
	
	IF !EXP:公開オナニー && !TALENT:淫乱 && !TALENT:娼婦
		EXP:異常経験 += 1
		PRINTL 公開オナニー初経験で異常経験＋１
	ENDIF
	EXP:公開オナニー += 1
	PRINTL 公開オナニー経験＋１
ELSE
	EXP:自慰経験 += 1
	PRINTL 自慰経験＋１
	EXP:調教自慰経験 += 1
	PRINTL 調教自慰経験＋１
ENDIF

;ゲイ経験とか加算
CALL COM_EXP(3)

;依存度ベクトル
TFLAG:33 = 3

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:隷属 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF


RETURN 1




;-------------------------------------------------
;飲尿させる
;スカ系コマンド
;-------------------------------------------------
@COM184

PRINTL 飲尿させる
STR:0 = 飲尿させる

;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;地下室にいて、触手口辱中、尿意があって触手尿道挿入していると連結チューブ（小）
IF TEQUIP:地下室 && TEQUIP:触手口辱 && TEQUIP:カテーテル && BASE:尿意 > 60
	CALL COM_ABLE527
	IF RESULT == 1
		TFLAG:56 = 1
		JUMP COM527
	ENDIF
ENDIF

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;実行判定操作
CALL KOJO_DENIAL_PLUS

;合計を表示(40以上で実行)
PRINT  = 
PRINTV A

V = 40
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

;実行不可口上/実行判定操作
SIF A < V
	CALL KOJO_DENIAL

WAIT

;実行できない
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM


;実行者が奴隷であるフラグ
TFLAG:28 = 1


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 5
LOSEBASE:気力 += 100

;性行動、露出、屈服、不潔、逸脱、反感
SOURCE:性行動 += 300
SOURCE:露出 += 400
SOURCE:屈従 += 2200
SOURCE:不潔 += 1000
SOURCE:逸脱 += 1500
SOURCE:反発 += 1000

;上のほうで計算した汚れデータ
SOURCE:不潔 += Y*10 + 60

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 1000

;マゾっ気を見る
IF ABL:マゾっ気 == 0
	TIMES SOURCE:屈従 , 0.80
	SOURCE:中毒充足 += 0
	SOURCE:欲情 += 0
ELSEIF ABL:マゾっ気 == 1
	TIMES SOURCE:屈従 , 1.00
	SOURCE:中毒充足 += 20
	SOURCE:欲情 += 30
ELSEIF ABL:マゾっ気 == 2
	TIMES SOURCE:屈従 , 1.20
	SOURCE:中毒充足 += 40
	SOURCE:欲情 += 70
ELSEIF ABL:マゾっ気 == 3
	TIMES SOURCE:屈従 , 1.40
	SOURCE:中毒充足 += 70
	SOURCE:欲情 += 120
ELSEIF ABL:マゾっ気 == 4
	TIMES SOURCE:屈従 , 1.50
	SOURCE:中毒充足 += 110
	SOURCE:欲情 += 180
ELSEIF ABL:マゾっ気 == 5
	TIMES SOURCE:屈従 , 1.70
	SOURCE:中毒充足 += 150
	SOURCE:欲情 += 250
ELSEIF ABL:マゾっ気 == 6
	TIMES SOURCE:屈従 , 1.90
	SOURCE:中毒充足 += 200
	SOURCE:欲情 += 300
ELSEIF ABL:マゾっ気 == 7
	TIMES SOURCE:屈従 , 2.10
	SOURCE:中毒充足 += 250
	SOURCE:欲情 += 350
ELSEIF ABL:マゾっ気 == 8
	TIMES SOURCE:屈従 , 2.30
	SOURCE:中毒充足 += 300
	SOURCE:欲情 += 400
ELSEIF ABL:マゾっ気 == 9
	TIMES SOURCE:屈従 , 2.50
	SOURCE:中毒充足 += 350
	SOURCE:欲情 += 450
ELSEIF ABL:マゾっ気 >= 10
	TIMES SOURCE:屈従 , 3.00
	SOURCE:中毒充足 += 500
	SOURCE:欲情 += 500
ENDIF

;マゾを見る
IF TALENT:マゾ
	TIMES SOURCE:欲情 , 1.50
	TIMES SOURCE:反発 , 0.80
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;その場に尿があって、奴隷がスカ中毒３／マゾっ気４+スカ中毒２だと全部飲む
IF TFLAG:66 && (ABL:スカ中毒 >= 3 || (ABL:マゾっ気 >= 4 && ABL:スカ中毒 >= 2))
	TFLAG:66 = 0
ELSE
	;プレイヤーの尿が床に
	TFLAG:66 |= 2
ENDIF

;汚れの移動は、その場に尿がないときのみ
SIF TFLAG:66 == 0
	CALL COM_STAIN

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;飲尿経験
EXP:飲尿経験 += 1
PRINTFORML 飲尿経験＋１

;スカ経験を伴う調教による異常経験
IF EXP:スカ経験 == 0
	EXP:異常経験 += 1
	PRINTL スカトロ初経験で異常経験＋１
ENDIF
EXP:スカ経験 += 1
PRINTL スカ経験＋１

IF TFLAG:66 == 0
	;調教者に放尿経験
	PRINTFORML %CALLNAME:PLAYER%に%EXPNAME:31%＋１
	EXP:PLAYER:放尿経験 += 1
ENDIF

;調教者にスカ経験
IF EXP:PLAYER:スカ経験 == 0
	EXP:PLAYER:異常経験 += 1
	PRINTFORML スカトロ初経験で%CALLNAME:PLAYER%に異常経験＋１
ENDIF
EXP:PLAYER:スカ経験 += 1
PRINTFORML %CALLNAME:PLAYER%にスカ経験＋１

;ゲイ経験とか加算
CALL COM_EXP(2)

;依存度ベクトル
TFLAG:33 = 3

;依存度補正値(主人かつ恋慕、淫乱、服従で1。親愛、娼婦、隷属で2）
IF (TALENT:恋慕 || TALENT:淫乱 || TALENT:服従) && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF (TALENT:親愛 || TALENT:娼婦 || TALENT:隷属) && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF


RETURN 1


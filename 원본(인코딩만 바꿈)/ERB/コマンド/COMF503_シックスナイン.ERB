;-------------------------------------------------
;シックスナイン
;派生系コマンド
;-------------------------------------------------
@COM503

PRINTL シックスナイン
STR:0 = シックスナイン

SELECTCOM = 503
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:28 = 1

;ストッキングびりびりフラグ
IF TEQUIP:特殊２ == 5 || TEQUIP:特殊２ == 6
	TFLAG:55 = 1
ENDIF

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 1000

CALL SHOOT_GAUGE_CHECK_MT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:射精 += B

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 30
LOSEBASE:気力 += 100

SOURCE:快Ｃ += 150

;舌を使う調教では、調教者の舌使いによって、快感のソース増加
IF TALENT:PLAYER:舌使い
	TIMES SOURCE:快Ｃ , 1.50
ELSEIF TALENT:PLAYER:猫舌
	TIMES SOURCE:快Ｃ , 0.75
ENDIF

SOURCE:情愛 += 300

;スキンシップ好きだと情愛のソース追加
SIF TALENT:スキンシップ好き
	SOURCE:情愛 += 50

SOURCE:性行動 += 450
SOURCE:達成感 += 200
SOURCE:液体 += 80
SOURCE:露出 += 100
SOURCE:屈従 += 800
SOURCE:逸脱 += 600
SOURCE:反発 += 600

;上のほうで計算した汚れデータ
SOURCE:不潔 += Y*80 + 50

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 800


;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK(PLAYER,1)

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	TFLAG:703 = 4
	;射精先選択の可・不可
	TFLAG:704 = 1
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
CALL COM_STAIN

SIF E >= 1
	TFLAG:8 = 1

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;フェラ経験
IF PENIS(PLAYER)
	PRINTFORML %EXPNAME:22%＋１
	EXP:フェラ経験 += 1
ENDIF

;ゲイ経験とか加算
CALL COM_EXP(7)

;愛情経験
IF (TALENT:恋慕 == 1 || TALENT:親愛 == 1) && ASSIPLAY == 0
	PRINTS EXPNAME:23
	PRINTL +1
	EXP:愛情経験 += 1
ENDIF

;奴隷が猫舌持ちの場合は、奴隷に嗜虐快楽経験を追加
IF TALENT:猫舌
	EXP:嗜虐快楽経験 += 1
	PRINTFORML %EXPNAME:32%＋１
ENDIF

;依存度ベクトル
TFLAG:33 = 1

;依存度補正値(主人かつ恋慕、淫乱、服従で1。親愛、娼婦、隷属で2）
IF (TALENT:恋慕 || TALENT:淫乱 || TALENT:服従) && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF (TALENT:親愛 || TALENT:娼婦 || TALENT:隷属) && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

;奉仕快楽フラグ
TFLAG:100 = 1

RETURN 1



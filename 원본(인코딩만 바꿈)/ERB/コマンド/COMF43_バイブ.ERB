;-------------------------------------------------
;バイブ
;Ｖ系調教コマンド
;-------------------------------------------------
@COM43

IF TEQUIP:触手
	PRINTL 触手挿入
	STR:0 = 触手挿入
	SELECTCOM = 344
ELSE
	PRINTL バイブ
	STR:0 = バイブ
ENDIF

CALL KOJO_MESSAGE_COM

;ストッキングびりびりフラグ
IF TEQUIP:特殊２ == 5 || TEQUIP:特殊２ == 6
	TFLAG:55 = 1
ENDIF

;処女喪失フラグ
SIF TALENT:処女 || TALENT:再生処女
	TFLAG:19 |= 1

;着脱の処理
IF TEQUIP:バイブ == 0
	TEQUIP:バイブ = 1
ELSE
	TEQUIP:バイブ = 0
ENDIF


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 80

SOURCE:快Ｖ += 250

SOURCE:痛み += 50
;地下室にいると苦痛のソース追加
SIF TEQUIP:地下室
	SOURCE:痛み += 200

SOURCE:露出 += 20
SOURCE:逸脱 += 40
SOURCE:反発 += 40


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 40

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女 == 1
	SOURCE:痛み += 2000
	SOURCE:反発 += 3000
;再生処女だった場合は、苦痛のソースと反発のソースを追加する
ELSEIF TALENT:再生処女 == 1
	SOURCE:痛み += 500
	SOURCE:反発 += 1000
ENDIF

;Vへの苦痛は先に処理しておく
;EXP:Ｖ経験をみる（処女の反感は別途処理済みなので省略）
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:痛み , 2.50
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:痛み , 0.80
	TIMES SOURCE:反発 , 1.20
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:痛み , 0.40
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:痛み , 0.10
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:痛み , 0.00
ELSEIF EXP:Ｖ経験 >= EXPLV:5
	TIMES SOURCE:痛み , 0.00
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:痛み , 1.20
	TIMES SOURCE:反発 , 1.50
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:痛み , 0.70
	TIMES SOURCE:反発 , 1.20
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:痛み , 0.40
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:痛み , 0.10
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:痛み , 0.10
ENDIF


;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
CALL COM_STAIN


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;V経験
EXP:Ｖ経験 += 1
PRINTL Ｖ経験＋１

;ゲイ経験とか加算
CALL COM_EXP(1)

;触手召喚時、触手経験のリセットをする
SIF TEQUIP:触手 == 1
	T = 0

;依存度ベクトル
TFLAG:33 = 1


RETURN 1



;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM43
;バイブ挿入中

IF TEQUIP:触手
	PRINTL ＜触手挿入中＞
ELSE
	PRINTL ＜バイブ挿入中＞
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
LOSEBASE:体力 += 10
LOSEBASE:気力 += 30

SOURCE:快Ｖ += 125
SOURCE:露出 += 10
SOURCE:逸脱 += 20

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 20

;Vへの苦痛は先に処理しておく
;EXP:Ｖ経験をみる（処女はありえない）
A = 25
B = 20
IF EXP:Ｖ経験 < EXPLV:2
	TIMES A , 0.80
	TIMES B , 1.20
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES A , 0.40
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES A , 0.10
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES A , 0.00
ELSEIF EXP:Ｖ経験 >= EXPLV:5
	TIMES A , 0.00
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES A , 1.20
	TIMES B , 1.50
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES A , 0.70
	TIMES B , 1.20
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES A , 0.40
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES A , 0.10
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES A , 0.10
ENDIF
SOURCE:痛み += A
SOURCE:反発 += B

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;自慰実行時は経験＋１
IF SELECTCOM == 11
	EXP:Ｖ経験 += 2
	PRINTL Ｖ経験＋２
ELSE
	EXP:Ｖ経験 += 1
	PRINTL Ｖ経験＋１
ENDIF

;触手経験加算
SIF TEQUIP:触手 == 1
	T += 1


RETURN 1



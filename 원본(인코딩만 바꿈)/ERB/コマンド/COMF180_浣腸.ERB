;-------------------------------------------------
;浣腸器＋プラグ
;スカ系コマンド
;-------------------------------------------------
@COM180

IF TEQUIP:触手
	PRINTL 触手浣腸
	STR:0 = 触手浣腸
	SELECTCOM = 352
ELSE
	PRINTL 浣腸
	STR:0 = 浣腸
ENDIF

CALL KOJO_MESSAGE_COM

;ストッキングびりびりフラグ
IF TEQUIP:特殊２ == 5 || TEQUIP:特殊２ == 6
	TFLAG:55 = 1
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 50

SOURCE:快Ａ += 50

SOURCE:痛み += 100
;地下室にいると苦痛のソース追加
SIF TEQUIP:地下室
	TIMES SOURCE:痛み , 1.50

SOURCE:恐れ += 200

SOURCE:露出 += 300
SOURCE:屈従 += 100
SOURCE:不潔 += 500
SOURCE:逸脱 += 500
SOURCE:反発 += 500


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

;Aへの苦痛は先に処理しておく
;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES SOURCE:痛み , 3.00
	TIMES SOURCE:反発 , 3.00
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES SOURCE:痛み , 1.20
	TIMES SOURCE:反発 , 2.00
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES SOURCE:痛み , 0.80
	TIMES SOURCE:反発 , 1.50
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES SOURCE:痛み , 0.60
	TIMES SOURCE:反発 , 1.20
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES SOURCE:痛み , 0.40
ELSEIF EXP:Ａ経験 >= EXPLV:5
	TIMES SOURCE:痛み , 0.20
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:痛み , 1.20
	TIMES SOURCE:反発 , 2.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:痛み , 1.00
	TIMES SOURCE:反発 , 1.50
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:痛み , 0.60
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:痛み , 0.30
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:痛み , 0.20
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;触手がいる場合は触手の汚れを追加
SIF TEQUIP:触手
	CALL COM_STAIN

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;Ａ経験
EXP:Ａ経験 += 5
PRINTL Ａ経験＋5

IF EXP:浣腸経験 == 0
	EXP:異常経験 += 1
	PRINTFORML 初浣腸で異常経験＋１
ENDIF
EXP:浣腸経験 += 1

IF BASE:便意 >= 60
	BASE:便意 += 20
ELSE
	BASE:便意 += 60
ENDIF

;ゲイ経験とか加算
CALL COM_EXP(1)

;依存度ベクトル
TFLAG:33 = 5

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:隷属 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

RETURN 1



;-------------------------------------------------
;足コキ
;奉仕系コマンド
;-------------------------------------------------
@COM80

IF TEQUIP:靴
	PRINTL 靴コキ
	STR:0 = 靴コキ
ELSE
	PRINTL 足コキ
	STR:0 = 足コキ
ENDIF

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;実行判定操作
CALL KOJO_DENIAL_PLUS

;合計を表示(22以上で実行)
PRINT  = 
PRINTV A

V = 22
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

;実行不可口上/実行判定操作
SIF A < V
	CALL KOJO_DENIAL

WAIT

;実行できない
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM


;実行者が奴隷であるフラグ
TFLAG:28 = 1


;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 500

CALL SHOOT_GAUGE_CHECK_MT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:射精 += B

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 80

SOURCE:性行動 += 700
SOURCE:達成感 += 500
SOURCE:屈従 += 800
SOURCE:逸脱 += 400
SOURCE:反発 += 400

;上のほうで計算した汚れデータ
SOURCE:不潔 += Y*10 + 50

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 600

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK(PLAYER,1)

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	TFLAG:703 = 5
	;射精先選択の可・不可
	;奴隷のサドが高すぎると選択不可
	IF ABL:サドっ気 >= 5 || TALENT:サド
		TFLAG:704 = 0
	ELSE
		TFLAG:704 = 1
	ENDIF
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
CALL COM_STAIN


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;ゲイ経験とか加算
CALL COM_EXP(4)

;依存度ベクトル
TFLAG:33 = 1

;依存度補正値(主人かつ恋慕、淫乱、服従で1。親愛、娼婦、隷属で2）
IF (TALENT:恋慕 || TALENT:淫乱 || TALENT:服従) && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF (TALENT:親愛 || TALENT:娼婦 || TALENT:隷属) && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

A = 0
;サド持ちは嗜虐快楽経験も入れる（地の文から判断）
IF TALENT:サド || ABL:サドっ気 >= 3
	E = 1
	L = 0
	;靴履いてるならさらに入れる
	SIF TEQUIP:靴 && ABL:サドっ気 >= 4
		E += 1
	;それがピンヒールならさらに入れる
	SIF TEQUIP:靴 == 4 && ABL:サドっ気 >= 5
		E += 1
	;サドっ気に応じて嗜虐快楽経験
	IF ABL:サドっ気 >= 3
		E += 2
		L += 1
	ENDIF
	;調教者のマゾっ気で嗜虐快楽経験
	IF ABL:PLAYER:9 >= 3
		E += 1
		L += 2
	ENDIF
	PRINT 嗜虐快楽経験+
	PRINTVL E
	EXP:嗜虐快楽経験 += E
	IF L > 0
		PRINTFORM %CALLNAME:PLAYER%の
		PRINT 苦痛快楽経験+
		PRINTVL L
		EXP:PLAYER:苦痛快楽経験 += L
		TFLAG:27 = L
	ENDIF
ENDIF


TFLAG:100 = 1

RETURN 1




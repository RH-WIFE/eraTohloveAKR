;-------------------------------------------------
;パイズリフェラ
;奉仕系コマンド
;-------------------------------------------------
@COM75

PRINTL パイズリフェラ
STR:0 = パイズリフェラ


;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;実行判定操作
CALL KOJO_DENIAL_PLUS

;合計を表示(30以上で実行)
PRINT  = 
PRINTV A

V = 35
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

;実行不可口上/実行判定操作
SIF A < V
	CALL KOJO_DENIAL

WAIT

;実行できない
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM


;実行者が奴隷であるフラグ
TFLAG:28 = 1


;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 1000

CALL SHOOT_GAUGE_CHECK_MT

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:射精 += B

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 80

SOURCE:快Ｂ += 30

SOURCE:情愛 += 250

;スキンシップ好きだと情愛のソース追加
SIF TALENT:スキンシップ好き
	SOURCE:情愛 += 50
;巨乳もしくは爆乳だと情愛のソース追加
SIF TALENT:巨乳 || TALENT:爆乳
	TIMES SOURCE:情愛 , 1.20

SOURCE:性行動 += 500
SOURCE:達成感 += 300
SOURCE:屈従 += 1000
SOURCE:逸脱 += 400
SOURCE:反発 += 400

;上のほうで計算した汚れデータ
SOURCE:不潔 += Y*10 + 60


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 600


;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK(PLAYER,1)

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	TFLAG:703 = 4
	;射精先選択の可・不可
	TFLAG:704 = 1
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

SIF E >= 1
	TFLAG:8 = 1

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
CALL COM_STAIN

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;フェラ経験
PRINTFORML フェラ経験＋１
EXP:フェラ経験 += 1

;ゲイ経験とか加算
CALL COM_EXP(7)

;愛情経験
IF (TALENT:恋慕 == 1 || TALENT:親愛 == 1) && ASSIPLAY == 0
	EXP:愛情経験 += 1
	PRINTL 愛情経験+１
ENDIF

;奴隷が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
IF TALENT:TARGET:猫舌
	EXP:嗜虐快楽経験 += 1
	PRINTFORML 嗜虐快楽経験＋１
ENDIF

;依存度ベクトル
TFLAG:33 = 1

;依存度補正値(主人かつ恋慕、淫乱、服従で1。親愛、娼婦、隷属で2）
IF (TALENT:恋慕 || TALENT:淫乱 || TALENT:服従) && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF (TALENT:親愛 || TALENT:娼婦 || TALENT:隷属) && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

TFLAG:100 = 1

RETURN 1



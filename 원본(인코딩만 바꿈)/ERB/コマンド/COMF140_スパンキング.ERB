;-------------------------------------------------
;スパンキング
;ＳＭ系コマンド
;-------------------------------------------------
@COM140

;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)
	;前回の調教が後背位、背面立位、スパンキングセックスのとき、スパンキングセックスに
	IF PREVCOM == 101 || PREVCOM == 105 || PREVCOM == 511
		CALL COM_ABLE511
		SIF RESULT == 1
			JUMP COM511
	;前回の調教が後背位Ａ、背面立位Ａ、スパンキングアナルのとき、スパンキングアナルに
	ELSEIF PREVCOM == 111 || PREVCOM == 115 || PREVCOM == 511
		CALL COM_ABLE512
		SIF RESULT == 1
			JUMP COM512
	ENDIF
ENDIF

PRINTL スパンキング
STR:0 = スパンキング
CALL KOJO_MESSAGE_COM


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
IF TEQUIP:地下室
	;消費体力、気力
	LOSEBASE:体力 += 50
	LOSEBASE:気力 += 20
	
	SOURCE:痛み += 900
ELSE
	;消費体力、気力
	LOSEBASE:体力 += 30
	LOSEBASE:気力 += 10
	
	SOURCE:痛み += 450
ENDIF
;着衣のとき、痛みが軽減
SIF BOTTOMS(TARGET) >= 4
	TIMES SOURCE:痛み , 0.70
SIF TEQUIP:下着（下） && TEQUIP:下着（下） != 5
	TIMES SOURCE:痛み , 0.90

SOURCE:恐れ += 400
;地下室にいると恐れのソース追加
SIF TEQUIP:地下室
	TIMES SOURCE:恐れ , 2.00

SOURCE:逸脱 += 50
SOURCE:反発 += 300


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;苦痛のパラメータによる苦痛のソース増加は先に処理しておく
IF PALAM:苦痛 < PALAMLV:1
	TIMES SOURCE:痛み , 1.00
ELSEIF PALAM:苦痛 < PALAMLV:2
	TIMES SOURCE:痛み , 1.50
ELSEIF PALAM:苦痛 < PALAMLV:3
	TIMES SOURCE:痛み , 2.00
ELSEIF PALAM:苦痛 < PALAMLV:4
	TIMES SOURCE:痛み , 2.50
ELSEIF PALAM:苦痛 >= PALAMLV:4
	TIMES SOURCE:痛み , 3.00
ENDIF


;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;下衣類を着てるとき、調教者の手の汚れ⇔奴隷の下衣類の汚れが移動
;地下室にいるときは道具を使用するので、汚れない
IF TEQUIP:地下室
ELSEIF TEQUIP:下衣 > 1 || TEQUIP:全身上着 == 8 || TEQUIP:全身上着 == 11 || TEQUIP:全身上着 == 12 || (TEQUIP:下着（下） && TEQUIP:下着（下） != 5)
	CALL COM_STAIN
ENDIF


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;ゲイ経験とか加算
CALL COM_EXP(2)

;依存度ベクトル
TFLAG:33 = 3

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:隷属 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF


RETURN 1





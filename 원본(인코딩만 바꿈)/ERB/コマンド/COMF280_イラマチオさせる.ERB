;-------------------------------------------------
;イラマチオさせる
;主人マゾ系コマンド
;-------------------------------------------------
@COM280

;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)
	;すでに挿入命令中は「サンドイッチ」
	IF PREVCOM == 405 || PREVCOM == 406
		CALL COM_ABLE515
		IF RESULT == 1
			TFLAG:40 = 5
			IF PREVCOM == 405
				TFLAG:41 = 1
			ELSE
				TFLAG:41 = 2
			ENDIF
			TFLAG:49 = 0
			JUMP COM515
		ENDIF
	;前回コマンドが助手に犯させるとき、「サンドイッチさせる」
	ELSEIF PREVCOM == 270 || PREVCOM == 271
		CALL COM_ABLE520
		IF RESULT == 1
			TFLAG:40 = 5
			IF PREVCOM == 270
				TFLAG:41 = 6
			ELSE
				TFLAG:41 = 7
			ENDIF
			TFLAG:49 = 0
			JUMP COM520
		ENDIF
	;前回がサンドイッチか、サンドイッチさせる
	ELSEIF PREVCOM == 515 || PREVCOM == 520
		;助手が奴隷に挿入してれば再び「サンドイッチ」に
		IF TFLAG:41 == 1 || TFLAG:41 == 2
			CALL COM_ABLE515
			IF RESULT == 1
				TFLAG:40 = 5
				IF PREVCOM == 405
					TFLAG:41 = 1
				ELSE
					TFLAG:41 = 2
				ENDIF
				TFLAG:49 = 0
				JUMP COM515
			ENDIF
		;助手が主人に挿入してると「サンドイッチさせる」に
		ELSEIF TFLAG:41 == 6 || TFLAG:41 == 7
			CALL COM_ABLE520
			IF RESULT == 1
				TFLAG:40 = 5
				TFLAG:49 = 0
				JUMP COM520
			ENDIF
		ENDIF
	ENDIF
ENDIF

;何も読まずにこっちに来たときはフラグ解除
TFLAG:40 = 0
TFLAG:41 = 0
TFLAG:49 = 0
TFLAG:42 = 0

PRINTL イラマチオさせる
STR:0 = イラマチオさせる

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;実行判定操作
CALL KOJO_DENIAL_PLUS

;合計を表示(26以上で実行)
PRINT  = 
PRINTV A

V = 26
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

;実行不可口上/実行判定操作
SIF A < V
	CALL KOJO_DENIAL

WAIT

;実行できない
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM


;ストッキングびりびりフラグ
IF TEQUIP:特殊２ == 5 || TEQUIP:特殊２ == 6
	TFLAG:55 = 1
ENDIF


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 20
LOSEBASE:気力 += 50

SOURCE:快Ｃ += 200

;調教者のTALENT:舌使いをみる
IF TALENT:PLAYER:舌使い
	TIMES SOURCE:快Ｃ , 1.50
ELSEIF TALENT:PLAYER:猫舌
	TIMES SOURCE:快Ｃ , 0.75
ENDIF

SOURCE:露出 += 50
SOURCE:逸脱 += 50
SOURCE:反発 += 50


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 40


;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
CALL COM_STAIN


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;ゲイ経験とか加算
CALL COM_EXP(3)

;サドっ気に応じて嗜虐快楽経験
E = 1
L = 0
IF ABL:サドっ気 == 0
	E += 0
	L += 0
ELSEIF ABL:サドっ気 == 1
	E += 1
	L += 1
ELSEIF ABL:サドっ気 == 2
	E += 3
	L += 2
ELSEIF ABL:サドっ気 == 3
	E += 5
	L += 3
ELSEIF ABL:サドっ気 == 4
	E += 7
	L += 4
ELSEIF ABL:サドっ気 >= 5
	E += 9
	L += 5
ENDIF
;調教者のマゾっ気で嗜虐快楽経験
IF ABL:PLAYER:9 == 0
	E += 0
	L += 0
ELSEIF ABL:PLAYER:9 == 1
	E += 1
	L += 1
ELSEIF ABL:PLAYER:9 == 2
	E += 2
	L += 3
ELSEIF ABL:PLAYER:9 == 3
	E += 3
	L += 5
ELSEIF ABL:PLAYER:9 == 4
	E += 4
	L += 7
ELSEIF ABL:PLAYER:9 >= 5
	E += 5
	L += 9
ENDIF
PRINT 嗜虐快楽経験+
PRINTVL E
EXP:嗜虐快楽経験 += E
IF L > 0
	PRINTFORM %CALLNAME:PLAYER%の
	PRINT 苦痛快楽経験+
	PRINTVL L
	EXP:PLAYER:苦痛快楽経験 += L
	TFLAG:27 = L
ENDIF

;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
IF TALENT:PLAYER:猫舌
	EXP:PLAYER:嗜虐快楽経験 += 1
	PRINTFORML %CALLNAME:PLAYER%の%EXPNAME:32%＋１
ENDIF

;依存度ベクトル
TFLAG:33 = 3


RETURN 1



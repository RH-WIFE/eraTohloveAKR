;-------------------------------------------------
;ローター挿入
;Ｖ系調教コマンド
;-------------------------------------------------
@COM42

PRINTL ローター挿入
STR:0 = ローター挿入
CALL KOJO_MESSAGE_COM

;ストッキングびりびりフラグ
IF TEQUIP:特殊２ == 5 || TEQUIP:特殊２ == 6
	TFLAG:55 = 1
ENDIF

;着脱の処理
IF TEQUIP:ローター挿入 == 0
	TEQUIP:ローター挿入 = 1
ELSE
	TEQUIP:ローター挿入 = 0
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 10
LOSEBASE:気力 += 50

SOURCE:快Ｖ += 150
SOURCE:痛み += 15
SOURCE:露出 += 15
SOURCE:逸脱 += 30
SOURCE:反発 += 30

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
IF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 30
	;処女
	SIF TALENT:処女
		SOURCE:鬱屈 += 20
ENDIF

;Vへの苦痛は先に処理しておく
;EXP:Ｖ経験をみる（処女の反感は別途処理済みなので省略）
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:痛み , 2.50
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:痛み , 0.80
	TIMES SOURCE:反発 , 1.20
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:痛み , 0.40
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:痛み , 0.10
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:痛み , 0.00
ELSEIF EXP:Ｖ経験 >= EXPLV:5
	TIMES SOURCE:痛み , 0.00
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:痛み , 1.20
	TIMES SOURCE:反発 , 1.50
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:痛み , 0.70
	TIMES SOURCE:反発 , 1.20
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:痛み , 0.40
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:痛み , 0.10
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:痛み , 0.10
ENDIF


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;V経験
EXP:Ｖ経験 += 1
PRINTL Ｖ経験＋１

;ゲイ経験とか加算
CALL COM_EXP(1)

;依存度ベクトル
TFLAG:33 = 1


RETURN 1



;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM42

PRINTL ＜ローター挿入中＞

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
LOSEBASE:体力 += 5
LOSEBASE:気力 += 20

SOURCE:快Ｖ += 80
SOURCE:痛み += 10
SOURCE:露出 += 7
SOURCE:逸脱 += 15
SOURCE:反発 += 15

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 15

;Vへの苦痛は先に処理しておく
;EXP:Ｖ経験をみる（処女はありえない）
A = 10
B = 15
IF EXP:Ｖ経験 < EXPLV:2
	TIMES A , 0.80
	TIMES B , 1.20
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES A , 0.40
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES A , 0.10
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES A , 0.00
ELSEIF EXP:Ｖ経験 >= EXPLV:5
	TIMES A , 0.00
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES A , 1.20
	TIMES B , 1.50
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES A , 0.70
	TIMES B , 1.20
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES A , 0.40
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES A , 0.10
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES A , 0.10
ENDIF
SOURCE:痛み += A
SOURCE:反発 += B

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:Ｖ経験 += 1
PRINTL Ｖ経験＋１

RETURN 1


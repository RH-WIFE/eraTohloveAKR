;-------------------------------------------------
;烙印
;ＳＭ-ハード系コマンド
;-------------------------------------------------
@COM156

PRINTL 烙印
STR:0 = 烙印

TFLAG:56 = 0
CALL KOJO_MESSAGE_COM


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;どの烙印を付けたか
IF TFLAG:56
	;痛み、恐れ、屈従、逸脱、反発
	SOURCE:痛み += 6000
	SOURCE:恐れ += 6000
	SOURCE:屈従 += 2000
	SOURCE:逸脱 += 800
	SOURCE:反発 += 2500

	;初烙印のとき異常経験、屈従・逸脱・反発のソース
	IF TALENT:TARGET:烙印 == 0
		EXP:異常経験 += 1
		PRINTL 烙印初経験で異常経験＋１
		SOURCE:屈従 += 500
		SOURCE:逸脱 += 800
		SOURCE:反発 += 2000
	ENDIF

	;傷痕
	IF TALENT:烙印 & 1
		;消費体力、気力
		LOSEBASE:体力 += 100
		LOSEBASE:気力 += 300
		
		TALENT:烙印 |= 1
	ENDIF
	;刺青
	IF TALENT:烙印 & 2
		;消費体力、気力
		LOSEBASE:体力 += 500
		LOSEBASE:気力 += 400
		
		TALENT:烙印 |= 2
	ENDIF
	;焼き印
	IF TALENT:烙印 & 4
		;消費体力、気力
		LOSEBASE:体力 += 500
		LOSEBASE:気力 += 400
		
		TALENT:烙印 |= 4
	ENDIF
	;ピアス
	IF TALENT:烙印 & 8
		;すでにピアスがついてるとき、つけかえるだけなので痛みは少ない…という設定
		IF TALENT:烙印 & 8
			;消費体力、気力
			LOSEBASE:体力 += 0
			LOSEBASE:気力 += 20
			
			TIMES SOURCE:痛み , 0.00
			TIMES SOURCE:恐れ , 0.50
			TIMES SOURCE:反発 , 1.50
		ELSE
			;消費体力、気力
			LOSEBASE:体力 += 100
			LOSEBASE:気力 += 300
			
			TALENT:烙印 |= 8
		ENDIF
	ENDIF

	PRINTFORML %CALLNAME%の体に【烙印】が付けられた

	;へたくしょん用面識判定の更新
	IF TALENT:烙印 && NO:MASTER != S
		;最大使用ビット数を17ビットと仮定
		G = GETCHARA(S)
		H = (2099 + NO:TARGET)
		LOCAL = S + 2099
		SIF G >= 0
			CFLAG:G:H &= 130047
		CFLAG:TARGET:(LOCAL) &= 129023
		G = (2099 + NO:PLAYER)
	ELSE
		G = (2099 + NO:PLAYER)
		H = (2099 + NO:TARGET)
	ENDIF
	
	CFLAG:PLAYER:H |= 1024
	CFLAG:TARGET:G |= 2048

	;烙印を押した相手の名前を記録
	CSTR:1 = %CALLNAME:MASTER%
ENDIF


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
IF TFLAG:56
	;恋慕・親愛・服従・隷属で烙印がついているとき恭順のソース追加、恐れと反発のソース減少
	IF TALENT:烙印 && (TALENT:恋慕 || TALENT:服従 || TALENT:親愛 || TALENT:隷属)
		SOURCE:恭順 += 2000
		TIMES SOURCE:恐れ , 0.80
		TIMES SOURCE:反発 , 0.50
	ENDIF

	;苦痛のパラメータによる苦痛のソース増加は先に処理しておく
	IF PALAM:苦痛 < PALAMLV:1
		TIMES SOURCE:痛み , 1.00
	ELSEIF PALAM:苦痛 < PALAMLV:2
		TIMES SOURCE:痛み , 1.50
	ELSEIF PALAM:苦痛 < PALAMLV:3
		TIMES SOURCE:痛み , 2.00
	ELSEIF PALAM:苦痛 < PALAMLV:4
		TIMES SOURCE:痛み , 2.50
	ELSEIF PALAM:苦痛 >= PALAMLV:4
		TIMES SOURCE:痛み , 3.00
	ENDIF
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
IF TFLAG:56
	;ゲイ経験とか加算
	CALL COM_EXP(5)
	
	;依存度ベクトル
	TFLAG:33 = 3
	
	;依存度補正値(恋慕・服従で1、親愛・隷属で2）
	IF TALENT:恋慕 || TALENT:服従
		TFLAG:30 += 1
	ELSEIF TALENT:親愛 || TALENT:隷属
		TFLAG:30 += 2
	ENDIF
ENDIF


RETURN 1


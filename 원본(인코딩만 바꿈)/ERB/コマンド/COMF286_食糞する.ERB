;-------------------------------------------------
;食糞する
;主人マゾ系コマンド
;-------------------------------------------------
@COM286

PRINTL 食糞する
STR:0 = 食糞する
;アナル舐めからの派生の場合
SIF SELECTCOM == 50
	TFLAG:56 = 1

SELECTCOM = 286
CALL KOJO_MESSAGE_COM


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 5
LOSEBASE:気力 += 150

;アナル舐めからの派生の場合
SIF TFLAG:56 == 1
	SOURCE:快Ａ += 30

SOURCE:恐れ += 20
SOURCE:露出 += 400
SOURCE:不潔 += 500
SOURCE:逸脱 += 400
SOURCE:反発 += 300

;アナル舐めからの派生の場合
IF TFLAG:56 == 1
	SOURCE:露出 += 700
	SOURCE:屈従 += 800
	SOURCE:不潔 += 400
	SOURCE:逸脱 += 700
	SOURCE:反発 += 800
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 200

;陥落素質によって逸脱のソースが上下
IF TALENT:恋慕
	TIMES SOURCE:逸脱 , 0.70
ELSEIF TALENT:親愛
	TIMES SOURCE:逸脱 , 0.30
ELSEIF TALENT:淫乱 || TALENT:娼婦
	TIMES SOURCE:逸脱 , 0.50
ELSEIF TALENT:服従 || TALENT:隷属
	TIMES SOURCE:逸脱 , 0.30
ENDIF

;サドっ気を見る
IF ABL:サドっ気 == 0
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:サドっ気 == 1
	TIMES SOURCE:不潔 , 0.95
	SOURCE:欲情 += 30
ELSEIF ABL:サドっ気 == 2
	TIMES SOURCE:不潔 , 0.80
	SOURCE:欲情 += 50
ELSEIF ABL:サドっ気 == 3
	TIMES SOURCE:不潔 , 0.75
	SOURCE:欲情 += 80
ELSEIF ABL:サドっ気 == 4
	TIMES SOURCE:不潔 , 0.60
	SOURCE:欲情 += 100
ELSEIF ABL:サドっ気 == 5
	TIMES SOURCE:不潔 , 0.50
	SOURCE:欲情 += 150
ELSEIF ABL:サドっ気 == 6
	TIMES SOURCE:不潔 , 0.45
	SOURCE:欲情 += 200
ELSEIF ABL:サドっ気 == 7
	TIMES SOURCE:不潔 , 0.40
	SOURCE:欲情 += 250
ELSEIF ABL:サドっ気 == 8
	TIMES SOURCE:不潔 , 0.35
	SOURCE:欲情 += 300
ELSEIF ABL:サドっ気 == 9
	TIMES SOURCE:不潔 , 0.30
	SOURCE:欲情 += 350
ELSEIF ABL:サドっ気 >= 10
	TIMES SOURCE:不潔 , 0.20
	SOURCE:欲情 += 400
ENDIF

;スカ中毒で欲情増、反発減
IF ABL:スカ中毒 == 0
	TIMES SOURCE:逸脱 , 1.50
ELSEIF ABL:スカ中毒 == 1
	SOURCE:中毒充足 += 100
	TIMES SOURCE:逸脱 , 1.00
ELSEIF ABL:スカ中毒 == 2
	SOURCE:中毒充足 += 200
	TIMES SOURCE:逸脱 , 0.70
ELSEIF ABL:スカ中毒 == 3
	SOURCE:中毒充足 += 800
	TIMES SOURCE:逸脱 , 0.30
ELSEIF ABL:スカ中毒 == 4
	SOURCE:中毒充足 += 1000
	TIMES SOURCE:逸脱 , 0.20
ELSE
	SOURCE:中毒充足 += 1500
	TIMES SOURCE:逸脱 , 0.10
ENDIF

;アナル舐めからの派生の場合
IF TFLAG:56 == 1
	;調教者のTALENT:舌使いをみる
	SIF TALENT:PLAYER:舌使い
		TIMES SOURCE:快Ａ , 1.50
		
	;調教者のTALENT:猫舌をみる
	SIF TALENT:PLAYER:猫舌
		TIMES SOURCE:快Ａ , 0.75
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
CALL COM_STAIN

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;アナル舐めからの派生の場合
IF TFLAG:56 == 1
	;排便経験
	PRINTL 食糞経験＋２
	EXP:排便経験 += 2

	;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
	IF TALENT:PLAYER:猫舌
		EXP:PLAYER:嗜虐快楽経験 += 1
		PRINTFORML %CALLNAME:PLAYER%の%EXPNAME:32%＋１
	ENDIF
ENDIF

;食糞経験なければ異常経験
IF EXP:PLAYER:食糞経験 == 0
	EXP:PLAYER:異常経験 += 1
	PRINTFORML 食糞初経験で%CALLNAME:PLAYER%に異常経験＋１
ENDIF
EXP:PLAYER:食糞経験 += 1

;ゲイ経験とか加算
CALL COM_EXP(4)

;依存度ベクトル
TFLAG:33 = 5

;依存度補正値(主人かつ恋慕、親愛で2。服従、隷属で３）
IF (TALENT:恋慕 || TALENT:親愛) && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF (TALENT:服従 ||TALENT:隷属) && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

;初スカ経験のとき異常経験
IF EXP:スカ経験 == 0
	EXP:異常経験 += 1
	PRINTL スカトロ初経験で異常経験＋１
ENDIF
EXP:スカ経験 += 1
PRINTL スカ経験＋１

;調教者にスカ経験
IF EXP:PLAYER:スカ経験 == 0
	EXP:PLAYER:異常経験 += 1
	PRINTFORML スカトロ初経験で%CALLNAME:PLAYER%に異常経験＋１
ENDIF
EXP:PLAYER:スカ経験 += 1
PRINTFORML %CALLNAME:PLAYER%にスカ経験＋１

;スカ中毒４以上なら全部食べる
IF ABL:PLAYER:スカ中毒 >= 4
	;便意ゲージを０に
	BASE:便意 = 0
	
	;アナル舐めからの派生でない場合
	SIF TFLAG:56 != 1
		TFLAG:57 = 0
ELSE
	;アナル舐めからの派生の場合
	SIF TFLAG:56 == 1
		CALL DEFECATION_CHECK2
ENDIF

RETURN 1


